import GroupoidModel.ForMathlib
import GroupoidModel.ForMathlib.CategoryTheory.Grpd

/-!
## Main definitions
* `CategoryTheory.Grothendieck.Groupoidal`
  takes a functor from a groupoid into `Grpd` the category of groupoids,
  composes it with the forgetful functor into `Cat` the category of categories,
  then applies `CategoryTheory.Grothendieck`.
  This is a groupoid.

## Main statements

* `CategoryTheory.Grothendieck.Groupoidal.groupoid`
  is an instance of a groupoid structure on the groupidal
  Grothendieck construction.

## Implementation notes
* To avoid `Grpd.forgetToCat` cluttering the user's context,
  although we use `Grothendieck` to define `Grothendieck.Groupoidal`,
  all definitions and lemmas are restated.
  This _probably_ also means that Lean has an easier time when type checking,
  as long as the user does not unfold down
  to `Grothendieck` definitions.
-/

universe v u v‚ÇÅ u‚ÇÅ v‚ÇÇ u‚ÇÇ v‚ÇÉ u‚ÇÉ

namespace CategoryTheory.Functor

/--
  In Mathlib.CategoryTheory.Grothendieck we find the Grothendieck construction
  for the functors `F : C ‚•§ Cat`. Given a functor `F : G ‚•§ Grpd`, we show that
  the Grothendieck construction of the composite `F ‚ãô Grpd.forgetToCat`, where
  `forgetToCat : Grpd ‚•§ Cat` is the embedding of groupoids into categories, is a groupoid.
-/
def Groupoidal {C : Type u‚ÇÅ} [Category.{v‚ÇÅ,u‚ÇÅ} C] (F : C ‚•§ Grpd.{v‚ÇÇ,u‚ÇÇ}) :=
  Grothendieck (F ‚ãô Grpd.forgetToCat)

namespace Groupoidal

attribute [local simp] Grpd.id_eq_id Grpd.comp_eq_comp Functor.assoc

-- FIXME: maybe move the dsimprocs up into an earlier module so that more files can use them.
/-- A `dsimp` rule that reduces `ùü≠ _ ‚ãô F` to `F`
even if the domain categories are not syntactically equal. -/
dsimproc Functor.simpIdComp (@Functor.comp _ _ _ _ _ _ (@Functor.id _ _) _) := fun e => do
  let_expr Functor.comp _ _ _ _ _ _ _ F ‚Üê e | return .continue
  return .done F

/-- A `dsimp` rule that reduces `F ‚ãô ùü≠ _` to `F`
even if the codomain categories are not syntactically equal. -/
dsimproc Functor.simpCompId (@Functor.comp _ _ _ _ _ _ _ (@Functor.id _ _)) := fun e => do
  let_expr Functor.comp _ _ _ _ _ _ F _ ‚Üê e | return .continue
  return .done F

scoped prefix:75 "‚à´ " => Functor.Groupoidal

section

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ,u‚ÇÅ} C] {F : C ‚•§ Grpd.{v‚ÇÇ,u‚ÇÇ}}

/-- A morphism in the groupoidal Grothendieck category `F : C ‚•§ Grpd`
is defined to be a morphism in the Grothendieck category `F ‚ãô Grpd.forgetToCat`.
-/
def Hom (x y : ‚à´(F)) := Grothendieck.Hom x y

def id (x : ‚à´(F)) : Hom x x := Grothendieck.Hom.id x

def comp {x y z : ‚à´(F)} (f : Hom x y) (g : Hom y z) : Hom x z := Grothendieck.Hom.comp f g

attribute [local simp] eqToHom_map

instance : Category (‚à´ F) := {
  (inferInstanceAs $ Category (Grothendieck (F ‚ãô Grpd.forgetToCat))) with
  Hom := Hom
  id := id
  comp := comp
  }

def base (p : ‚à´(F)) : C := Grothendieck.base p

def fiber (p : ‚à´(F)) : F.obj p.base := Grothendieck.fiber p

namespace Hom

variable {x y : ‚à´(F)} (f : Hom x y)

/-- The morphism between base objects. -/
def base : x.base ‚ü∂ y.base := Grothendieck.Hom.base f

/-- The morphism from the pushforward to the source fiber object to the target fiber object. -/
def fiber : (F.map f.base).obj x.fiber ‚ü∂ y.fiber := Grothendieck.Hom.fiber f

end Hom

def forget : ‚à´(F) ‚•§ C := Grothendieck.forget _

@[simp] theorem forget_obj (x : ‚à´(F)) : forget.obj x = x.base :=
  rfl

@[simp] theorem forget_map {x y : ‚à´(F)} (f : x ‚ü∂ y) : forget.map f = f.base :=
  rfl

/--
  We should use this to introduce objects,
  rather than the API for `Grothendieck`.
  This might seem redundant, but it simplifies the goal when
  making a point so that it does not show the composition with `Grpd.forgetToCat`
-/
def objMk (c : C) (x : F.obj c) : ‚à´(F) where
  base := c
  fiber := x

@[simp] theorem objMk_base (c : C) (x : F.obj c) : (objMk c x).base = c :=
  rfl

@[simp] theorem objMk_fiber (c : C) (x : F.obj c) : (objMk c x).fiber = x :=
  rfl

/--
  We should use this to introduce morphisms,
  rather than the API for `Grothendieck`.
  This might seem redundant, but it simplifies the goal when
  making a path in the fiber so that it does not show the
  composition with `Grpd.forgetToCat`
-/
def homMk {X Y : ‚à´(F)} (fb : X.base ‚ü∂ Y.base) (ff : (F.map fb).obj X.fiber ‚ü∂ Y.fiber)
    : X ‚ü∂ Y where
  base := fb
  fiber := ff

-- FIXME should this be done by adding @[simps] to homMk?
@[simp] theorem homMk_base {X Y : ‚à´(F)} (fb : X.base ‚ü∂ Y.base)
    (ff : (F.map fb).obj X.fiber ‚ü∂ Y.fiber) : (homMk fb ff).base = fb :=
  rfl

-- FIXME should this be done by adding @[simps] to homMk?
@[simp] theorem homMk_fiber {X Y : ‚à´(F)} (fb : X.base ‚ü∂ Y.base)
    (ff : (F.map fb).obj X.fiber ‚ü∂ Y.fiber) : (homMk fb ff).fiber = ff :=
  rfl

@[simp]
theorem id_base (X : ‚à´(F)) :
    Hom.base (ùüô X) = ùüô X.base := rfl

@[simp]
theorem id_fiber (X : ‚à´(F)) :
    Hom.fiber (ùüô X) = eqToHom (by rw! [Functor.map_id]; simp) :=
  Grothendieck.Hom.id_fiber _

@[simp]
theorem comp_base {X Y Z : ‚à´(F)} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).base = f.base ‚â´ g.base :=
  rfl

@[simp]
theorem comp_fiber {X Y Z : ‚à´(F)} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g).fiber =
      eqToHom (by simp) ‚â´ (F.map g.base).map f.fiber ‚â´ g.fiber :=
  Grothendieck.Hom.comp_fiber _ _

@[simp]
lemma base_eqToHom {X Y : ‚à´ F} (h : X = Y) :
    (eqToHom h).base = eqToHom (by simp [h]) := by
  subst h; rfl

@[simp]
lemma fiber_eqToHom {X Y : ‚à´ F} (h : X = Y) :
    (eqToHom h).fiber = eqToHom (by subst h; simp) := by
  subst h; simp

end

@[simp] lemma Grothendieck.Groupoidal.eta {Œì : Type*} [Category Œì]
  {A : Œì ‚•§ Grpd} (x : ‚à´(A)) : objMk x.base x.fiber = x :=
  rfl

@[simp] lemma Grothendieck.Groupoidal.Hom.eta {Œì : Type*} [Category Œì]
  {A : Œì ‚•§ Grpd} {x y : ‚à´(A)} (f : x ‚ü∂ y) : homMk f.base f.fiber = f :=
  rfl

section

variable {C : Type u‚ÇÅ} [Groupoid.{v‚ÇÅ,u‚ÇÅ} C] {F : C ‚•§ Grpd.{v‚ÇÇ,u‚ÇÇ}}

instance (X : C) : Groupoid (F ‚ãô Grpd.forgetToCat |>.obj X) where
  inv f := ((F.obj X).str').inv f

/--
If `F : C ‚•§ Grpd` is a functor and `t : c ‚ü∂ d` is a morphism in `C`,
then `transport` maps each `c`-based element of `‚à´(F)` to a `d`-based element.
-/
def transport (x : ‚à´(F)) {c : C} (t : x.base ‚ü∂ c) : ‚à´(F) :=
  Grothendieck.transport x t

@[simp] theorem transport_base (x : ‚à´(F)) {c : C} (t : x.base ‚ü∂ c) :
    (x.transport t).base = c :=
  Grothendieck.transport_base x t

@[simp] theorem transport_fiber (x : ‚à´(F)) {c : C} (t : x.base ‚ü∂ c) :
    (x.transport t).fiber = (F.map t).obj x.fiber :=
  Grothendieck.transport_fiber x t

/--
If `F : C ‚•§ Cat` is a functor and `t : c ‚ü∂ d` is a morphism in `C`, then `transport` maps each
`c`-based element `x` of `Grothendieck F` to a `d`-based element `x.transport t`.

`toTransport` is the morphism `x ‚ü∂ x.transport t` induced by `t` and the identity on fibers.
-/
def toTransport (x : ‚à´(F)) {c : C} (t : x.base ‚ü∂ c) : x ‚ü∂ x.transport t :=
  Grothendieck.toTransport x t

@[simp] theorem toTransport_base (x : ‚à´(F)) {c : C} (t : x.base ‚ü∂ c) :
    (x.toTransport t).base = t :=
  Grothendieck.toTransport_base _ _

@[simp] theorem toTransport_fiber (x : ‚à´(F)) {c : C} (t : x.base ‚ü∂ c) :
    (x.toTransport t).fiber = ùüô ((F.map t).obj x.fiber) :=
  Grothendieck.toTransport_fiber _ _

def isoMk {X Y : ‚à´(F)} (f : X ‚ü∂ Y) : X ‚âÖ Y := by
  fapply Grothendieck.isoMk
  ¬∑ exact (Groupoid.isoEquivHom _ _).2 f.base
  ¬∑ apply (Groupoid.isoEquivHom _ _).2 f.fiber

def inv {X Y : ‚à´(F)} (f : X ‚ü∂ Y) : Y ‚ü∂ X  :=
  isoMk f |>.inv

instance groupoid : Groupoid (‚à´ F) where
  inv f :=  inv f
  inv_comp f := (isoMk f).inv_hom_id
  comp_inv f := (isoMk f).hom_inv_id

end

section FunctorFrom

variable {C : Type u} [Category.{v} C]
    (F : C ‚•§ Grpd.{v‚ÇÅ,u‚ÇÅ})

/-- The inclusion of a fiber `F.obj c` of a functor `F : C ‚•§ Cat` into its
groupoidal Grothendieck construction.-/
def Œπ (c : C) : F.obj c ‚•§ Groupoidal F :=
  Grothendieck.Œπ (F ‚ãô Grpd.forgetToCat) c

/-
JH: `Œπ_obj` in my opinion is a bad lemma. However,

1. The analogous thing for Grothendieck.Œπ exists, and is even a simp lemma.
2. Some proofs are shorter when feeding it to simp.

So for now let's avoid using it, but maybe we can put it back if
proves to be useful
-/
-- theorem Œπ_obj (c : C) (d : F.obj c) :
--     (Œπ F c).obj d = { base := c, fiber := d } :=
--   Grothendieck.Œπ_obj _ _ _

@[simp] theorem Œπ_obj_base (c : C) (d : F.obj c) : ((Œπ F c).obj d).base = c :=
  rfl

@[simp] theorem Œπ_obj_fiber (c : C) (d : F.obj c) : ((Œπ F c).obj d).fiber = d :=
  rfl

/-
Similar to `Œπ_obj`
NOTE the `Grothendieck` version in `mathlib` should NOT be a simp lemma
NOTE when `f = eqToHom` this is not the rewrite I want.
Instead I want to do `eqToHom_map`
theorem Œπ_map (c : C) {X Y : F.obj c} (f : X ‚ü∂ Y) :
    (Œπ F c).map f = ‚ü®ùüô _, eqToHom (by simp [Œπ_obj, Grpd.forgetToCat]) ‚â´ f‚ü© :=
  Grothendieck.Œπ_map _ _ _
-/
@[simp] theorem Œπ_map_base (c : C) {X Y : F.obj c} (f : X ‚ü∂ Y) :
    ((Œπ F c).map f).base = ùüô _ :=
  rfl

-- NOTE maybe this should be an HEq?
@[simp] theorem Œπ_map_fiber (c : C) {X Y : F.obj c} (f : X ‚ü∂ Y) :
    ((Œπ F c).map f).fiber = eqToHom (by simp) ‚â´ f :=
  rfl

@[simp] theorem Œπ_comp_forget (c : C) : Œπ F c ‚ãô forget = (const (F.obj c)).obj c :=
  rfl

variable {F}

section ext

theorem hext {x y : ‚à´ F} (hbase : x.base = y.base) (hfiber : HEq x.fiber y.fiber) : x = y := by
  rcases x with ‚ü®xbase, xfiber‚ü©
  subst hbase
  subst hfiber
  rfl

theorem hext_iff {x y : ‚à´ F} : x.base = y.base ‚àß HEq x.fiber y.fiber
    ‚Üî x = y := by
  constructor
  ¬∑ intro ‚ü® hŒ± , hstr ‚ü©
    exact hext hŒ± hstr
  ¬∑ intro hCD
    subst hCD
    exact ‚ü® rfl , HEq.rfl ‚ü©

theorem Hom.hext {X Y : ‚à´ F} (f g : Hom X Y) (w_base : f.base = g.base)
    (w_fiber : HEq f.fiber g.fiber) : f = g := by
  cases f; cases g
  congr

theorem Hom.hext_iff (x y : ‚à´ F) (f g : x ‚ü∂ y) :
    f.base = g.base ‚àß HEq f.fiber g.fiber ‚Üî f = g := by
  constructor
  ¬∑ intro h
    exact Hom.hext _ _ h.1 h.2
  ¬∑ aesop

theorem Hom.ext {X Y : ‚à´ F} (f g : Hom X Y) (w_base : f.base = g.base)
    (w_fiber : f.fiber = eqToHom (by rw [w_base]) ‚â´ g.fiber) : f = g := by
  apply Hom.hext <;> aesop

theorem Hom.ext_iff (x y : ‚à´ F) (f g : x ‚ü∂ y) :
    (‚àÉ w_base : f.base = g.base, f.fiber = eqToHom (by rw [w_base]) ‚â´ g.fiber) ‚Üî f = g := by
  constructor
  ¬∑ intro h
    exact Hom.ext _ _ h.1 h.2
  ¬∑ aesop

variable {F' : C ‚•§ Grpd}

theorem hext' (h : F = F') {x : ‚à´ F} {y : ‚à´ F'}
    (hbase : HEq x.base y.base) (hfiber : HEq x.fiber y.fiber) : HEq x y := by
  rcases x; rcases y
  subst hbase
  congr

theorem Hom.hext' (h : F = F') {X Y : ‚à´ F} {X' Y' : ‚à´ F'} (hX : HEq X X') (hY : HEq Y Y')
    (f : Hom X Y) (g : Hom X' Y') (w_base : HEq f.base g.base) (w_fiber : HEq f.fiber g.fiber) :
    HEq f g := by
  cases f; cases g
  congr

variable {D : Type*} [Category D]

theorem FunctorTo.hext (G H : D ‚•§ ‚à´ F)
    (hbase : G ‚ãô forget = H ‚ãô forget)
    (hfiber_obj : ‚àÄ x : D, HEq (G.obj x).fiber (H.obj x).fiber)
    (hfiber_map : ‚àÄ {x y : D} (f : x ‚ü∂ y), HEq (G.map f).fiber (H.map f).fiber)
    : G = H :=
  Grothendieck.FunctorTo.hext _ _ hbase hfiber_obj hfiber_map

end ext

/-- Every morphism `f : X ‚ü∂ Y` in the base category induces a natural transformation from the fiber
inclusion `Œπ F X` to the composition `F.map f ‚ãô Œπ F Y`. -/
def ŒπNatTrans {X Y : C} (f : X ‚ü∂ Y) : Œπ F X ‚ü∂ F.map f ‚ãô Œπ F Y :=
  Grothendieck.ŒπNatTrans _

@[simp] theorem ŒπNatTrans_id_app {X : C} {a : F.obj X} :
    (@ŒπNatTrans _ _ F _ _ (ùüô X)).app a =
    eqToHom (by simp) := Grothendieck.ŒπNatTrans_id_app

@[simp] theorem ŒπNatTrans_comp_app {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} {a} :
    (@ŒπNatTrans _ _ F _ _ (f ‚â´ g)).app a =
    (@ŒπNatTrans _ _ F _ _ f).app a ‚â´
    (@ŒπNatTrans _ _ F _ _ g).app ((F.map f).obj a) ‚â´ eqToHom (by simp) :=
  Grothendieck.ŒπNatTrans_comp_app

@[simp] theorem ŒπNatTrans_app_base {X Y : C} (f : X ‚ü∂ Y) (d : ‚Üë(F.obj X)) :
    ((ŒπNatTrans f).app d).base = f :=
  Grothendieck.ŒπNatTrans_app_base _ _

@[simp] theorem ŒπNatTrans_app_fiber {X Y : C} (f : X ‚ü∂ Y) (d : F.obj X) :
    ((ŒπNatTrans f).app d).fiber
    = ùüô ((F.map f).obj ((Groupoidal.Œπ F X).obj d).fiber) :=
  Grothendieck.ŒπNatTrans_app_fiber _ _

variable {E : Type*} [Category E]
variable (fib : ‚àÄ c, F.obj c ‚•§ E) (hom : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), fib c ‚ü∂ F.map f ‚ãô fib c')
variable (hom_id : ‚àÄ c, hom (ùüô c) = eqToHom (by simp only [Functor.map_id]; rfl))
variable (hom_comp : ‚àÄ c‚ÇÅ c‚ÇÇ c‚ÇÉ (f : c‚ÇÅ ‚ü∂ c‚ÇÇ) (g : c‚ÇÇ ‚ü∂ c‚ÇÉ), hom (f ‚â´ g) =
  hom f ‚â´ (F.map f).whiskerLeft (hom g) ‚â´ eqToHom (by simp only [Functor.map_comp]; rfl))

/-- Construct a functor from `Groupoidal F` to another category `E` by providing a family of
functors on the fibers of `Groupoidal F`, a family of natural transformations on morphisms in the
base of `Groupoidal F` and coherence data for this family of natural transformations. -/
def functorFrom : ‚à´(F) ‚•§ E :=
  Grothendieck.functorFrom fib hom hom_id hom_comp

@[simp] theorem functorFrom_obj (X : ‚à´(F)) :
    (functorFrom fib hom hom_id hom_comp).obj X = (fib X.base).obj X.fiber := by
  apply Grothendieck.functorFrom_obj

@[simp] theorem functorFrom_map {X Y : ‚à´(F)} (f : X ‚ü∂ Y) :
    (functorFrom fib hom hom_id hom_comp).map f
    = (hom f.base).app X.fiber ‚â´ (fib Y.base).map f.fiber := by
  apply Grothendieck.functorFrom_map

/-- `Groupoidal.Œπ F c` composed with `Groupoidal.functorFrom` is isomorphic a functor on a fiber
on `F` supplied as the first argument to `Groupoidal.functorFrom`. -/
def ŒπCompFunctorFrom (c : C) : Œπ F c ‚ãô (functorFrom fib hom hom_id hom_comp) ‚âÖ fib c :=
  Grothendieck.ŒπCompFunctorFrom _ _ _ _ _

section

variable {D : Type*} [Category D]

def fib' (c) : (F ‚ãô Grpd.forgetToCat).obj c ‚•§ E := fib c

variable {fib} in
def hom' {c c' : C} (f : c ‚ü∂ c') : fib' fib c ‚ü∂ (F ‚ãô Grpd.forgetToCat).map f ‚ãô fib' fib c' :=
  hom f

variable (G : E ‚•§ D)

def functorFrom_comp_fib' (c : C) : (F ‚ãô Grpd.forgetToCat).obj c ‚•§ D :=
  Grothendieck.functorFrom_comp_fib (fib' fib) G c

def functorFrom_comp_fib (c : C) : F.obj c ‚•§ D :=
 functorFrom_comp_fib' fib G c

def functorFrom_comp_hom' {c c' : C}  (f : c ‚ü∂ c') :
    functorFrom_comp_fib' fib G c ‚ü∂ (F ‚ãô Grpd.forgetToCat).map f ‚ãô functorFrom_comp_fib' fib G c' :=
  Grothendieck.functorFrom_comp_hom (fib' fib) (hom' hom) _ _

def functorFrom_comp_hom {c c' : C}  (f : c ‚ü∂ c') :
    functorFrom_comp_fib' fib G c ‚ü∂ F.map f ‚ãô functorFrom_comp_fib' fib G c' :=
  functorFrom_comp_hom' fib hom G f

include hom_id in
lemma functorFrom_comp_hom_id (c : C) : functorFrom_comp_hom fib hom G (ùüô c)
    = eqToHom (by simp) :=
  Grothendieck.functorFrom_comp_hom_id _ _ hom_id _ c

include hom_comp in
lemma functorFrom_comp_hom_comp (c‚ÇÅ c‚ÇÇ c‚ÇÉ : C) (f : c‚ÇÅ ‚ü∂ c‚ÇÇ) (g : c‚ÇÇ ‚ü∂ c‚ÇÉ):
    functorFrom_comp_hom fib (fun {c c'} ‚Ü¶ hom) G (f ‚â´ g)
    = functorFrom_comp_hom fib (fun {c c'} ‚Ü¶ hom) G f ‚â´
    Functor.whiskerLeft (F.map f) (functorFrom_comp_hom fib hom G g) ‚â´
    eqToHom (by simp) :=
  Grothendieck.functorFrom_comp_hom_comp _ _ hom_comp _ _ _ _ _ _


lemma functorFrom_comp_hom_eq {c c' : C}  (f : c ‚ü∂ c') :
    functorFrom_comp_hom fib hom G f = whiskerRight (hom f) G :=
  rfl

theorem functorFrom_comp' :
    functorFrom (fib' fib) (hom' hom) hom_id hom_comp ‚ãô G =
    functorFrom (functorFrom_comp_fib' fib G) (functorFrom_comp_hom' fib hom G)
 (functorFrom_comp_hom_id _ _ hom_id _) (functorFrom_comp_hom_comp _ _ hom_comp _) :=
  Grothendieck.functorFrom_comp (fib' fib) (hom' hom) hom_id hom_comp G

theorem functorFrom_comp :
    functorFrom fib hom hom_id hom_comp ‚ãô G =
    functorFrom (functorFrom_comp_fib fib G) (functorFrom_comp_hom fib hom G)
    (Grothendieck.functorFrom_comp_hom_id _ _ hom_id _)
    (Grothendieck.functorFrom_comp_hom_comp _ _ hom_comp _) :=
  functorFrom_comp' fib hom hom_id hom_comp G

variable (K : ‚à´(F) ‚•§ E)

def asFunctorFrom_fib (c : C) : F.obj c ‚•§ E :=
  Grothendieck.asFunctorFrom_fib K c

lemma asFunctorFrom_fib' (c : C) : asFunctorFrom_fib K c = Œπ F c ‚ãô K :=
  rfl

def asFunctorFrom_hom {c c' : C} (f: c ‚ü∂ c') :
    asFunctorFrom_fib K c ‚ü∂ F.map f ‚ãô asFunctorFrom_fib K c' :=
  Grothendieck.asFunctorFrom_hom K f

lemma asFunctorFrom_hom' {c c' : C} (f: c ‚ü∂ c') :
  asFunctorFrom_hom K f = whiskerRight (ŒπNatTrans f) K := rfl

lemma asFunctorFrom_hom_app {c c' : C} (f: c ‚ü∂ c') (p : F.obj c) :
    (asFunctorFrom_hom K f).app p = K.map ((ŒπNatTrans f).app p) :=
  rfl

lemma asFunctorFrom_hom_id (c : C) : asFunctorFrom_hom K (ùüô c) =
    eqToHom (by simp) :=
  Grothendieck.asFunctorFrom_hom_id _ _
  --   by
  -- ext p
  -- simp [asFunctorFrom_hom_app, eqToHom_map, ŒπNatTrans_id_app]

lemma asFunctorFrom_hom_comp (c‚ÇÅ c‚ÇÇ c‚ÇÉ : C) (f : c‚ÇÅ ‚ü∂ c‚ÇÇ) (g: c‚ÇÇ ‚ü∂ c‚ÇÉ) :
    asFunctorFrom_hom K (f ‚â´ g) =
    asFunctorFrom_hom K f ‚â´ Functor.whiskerLeft (F.map f) (asFunctorFrom_hom K g) ‚â´ eqToHom
    (by simp) :=
  Grothendieck.asFunctorFrom_hom_comp _ _ _ _ _ _

/-- Groupoidal version of `Grothendieck.asFunctorFrom` -/
theorem asFunctorFrom : functorFrom (asFunctorFrom_fib K) (asFunctorFrom_hom K)
    (asFunctorFrom_hom_id K) (asFunctorFrom_hom_comp K) = K :=
  Grothendieck.asFunctorFrom K

theorem functorFrom_ext {K K' : ‚à´(F) ‚•§ E}
    (hfib : asFunctorFrom_fib K = asFunctorFrom_fib K')
    (hhom : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), asFunctorFrom_hom K f ‚â´ eqToHom (by rw [hfib])
      = eqToHom (by rw[hfib]) ‚â´ asFunctorFrom_hom K' f)
    : K = K' :=
  Grothendieck.functorFrom_ext hfib hhom

theorem functorFrom_hext {K K' : ‚à´(F) ‚•§ E}
    (hfib : asFunctorFrom_fib K = asFunctorFrom_fib K')
    (hhom : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), asFunctorFrom_hom K f ‚âç asFunctorFrom_hom K' f)
    : K = K' :=
  Grothendieck.functorFrom_hext hfib hhom

end
end FunctorFrom

section
variable {C : Type u} [Category.{v} C] {F G : C ‚•§ Grpd.{v‚ÇÇ,u‚ÇÇ}}

/-- The groupoidal Grothendieck construction is functorial:
a natural transformation `Œ± : F ‚ü∂ G` induces
a functor `Groupoidal.map : Groupoidal F ‚•§ Groupoidal G`.
-/
def map (Œ± : F ‚ü∂ G) : Groupoidal F ‚•§ Groupoidal G :=
  Grothendieck.map (whiskerRight Œ± _)

variable {Œ± : F ‚ü∂ G}

theorem map_obj_objMk (xb : C) (xf : F.obj xb) :
    (Groupoidal.map Œ±).obj (objMk xb xf) = objMk xb ((Œ±.app xb).obj xf) :=
  rfl

theorem map_id_eq : map (ùüô F) = Functor.id (Cat.of <| Groupoidal <| F) :=
  Grothendieck.map_id_eq

theorem map_comp_forget (Œ± : F ‚ü∂ G) : map Œ± ‚ãô forget = forget :=
  rfl

@[simp] theorem map_obj_base (X) : ((map Œ±).obj X).base = X.base :=
  Grothendieck.map_obj_base _ _

@[simp] theorem map_obj_fiber (X) :
    ((map Œ±).obj X).fiber = (Œ±.app X.base).obj X.fiber :=
  Grothendieck.map_obj_fiber _ _

variable {X} {Y : ‚à´(F)} (f : X ‚ü∂ Y)

@[simp] theorem map_map_base : ((Groupoidal.map Œ±).map f).base = f.base
    := Grothendieck.map_map_base _ _

@[simp] theorem map_map_fiber :
  ((Groupoidal.map Œ±).map f).fiber =
    eqToHom (Functor.congr_obj (Œ±.naturality f.base).symm X.fiber)
    ‚â´ (Œ±.app Y.base).map f.fiber := Grothendieck.map_map_fiber _ _

end

section

variable {C : Type u} [Category.{v} C] {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
    (F : C ‚•§ Grpd.{v‚ÇÇ,u‚ÇÇ}) (G : D ‚•§ C)

/-- Applying a functor `G : D ‚•§ C` to the base of the groupoidal Grothendieck
  construction induces a functor `‚à´(G ‚ãô F) ‚•§ ‚à´(F)`. -/
def pre : ‚à´(G ‚ãô F) ‚•§ ‚à´(F) :=
  Grothendieck.pre (F ‚ãô Grpd.forgetToCat) G

@[simp]
lemma pre_obj_base (x) : ((pre F G).obj x).base = G.obj x.base := by
  aesop

@[simp]
lemma pre_obj_fiber (x) : ((pre F G).obj x).fiber = x.fiber := by
  aesop_cat

@[simp]
lemma pre_map_base {x y} (f : x ‚ü∂ y) : ((pre F G).map f).base = G.map f.base := by
  aesop_cat

@[simp]
lemma pre_map_fiber {x y} (f : x ‚ü∂ y) : ((pre F G).map f).fiber = f.fiber := by
  aesop_cat

@[simp]
theorem pre_id : pre F (ùü≠ C) = ùü≠ _ := rfl

/--
An natural isomorphism between functors `G ‚âÖ H` induces a natural isomorphism between the canonical
morphism `pre F G` and `pre F H`, up to composition with
`‚à´(G ‚ãô F) ‚•§ ‚à´(H ‚ãô F)`.
-/
def preNatIso {G H : D ‚•§ C} (Œ± : G ‚âÖ H) :
    pre F G ‚âÖ map (whiskerRight Œ±.hom F) ‚ãô (pre F H) :=
  Grothendieck.preNatIso _ _

/--
Given an equivalence of categories `G`, `preInv _ G` is the (weak) inverse of the `pre _ G.functor`.
-/
def preInv (G : D ‚âå C) : ‚à´(F) ‚•§ ‚à´(G.functor ‚ãô F) :=
  map (whiskerRight G.counitInv F) ‚ãô pre (G.functor ‚ãô F) G.inverse

variable {F} in
lemma pre_comp_map (G: D ‚•§ C) {H : C ‚•§ Grpd} (Œ± : F ‚ü∂ H) :
    pre F G ‚ãô map Œ± = map (whiskerLeft G Œ±) ‚ãô pre H G := rfl

variable {F} in
lemma pre_comp_map_assoc (G: D ‚•§ C) {H : C ‚•§ Grpd} (Œ± : F ‚ü∂ H) {E : Type*} [Category E]
    (K : ‚à´(H) ‚•§ E) : pre F G ‚ãô map Œ± ‚ãô K= map (whiskerLeft G Œ±) ‚ãô pre H G ‚ãô K := rfl

variable {E : Type*} [Category E] in
@[simp]
lemma pre_comp (G : D ‚•§ C) (H : E ‚•§ D) : pre F (H ‚ãô G) = pre (G ‚ãô F) H ‚ãô pre F G := rfl

theorem pre_comp_forget (Œ± : D ‚•§ C) (A : C ‚•§ Grpd) :
    pre A Œ± ‚ãô forget = forget ‚ãô Œ± := rfl

@[simp] theorem Œπ_comp_pre {D : Type*} [Category D] (G : D ‚•§ C) (x : D)
    : Œπ (G ‚ãô F) x ‚ãô pre F G = Œπ F (G.obj x) := by
  apply FunctorTo.hext
  ¬∑ rw [Œπ_comp_forget, Functor.assoc, pre_comp_forget, ‚Üê Functor.assoc, Œπ_comp_forget]
    apply Functor.ext <;> simp
  ¬∑ intro
    simp
  ¬∑ simp

end

section

variable {Œì : Type*} [Category Œì] {F G : Œì ‚•§ Grpd}

lemma comp_forget_naturality {Œ± : F ‚ü∂ G} {X Y : Œì} (f : X ‚ü∂ Y) :
    (F ‚ãô Grpd.forgetToCat).map f ‚â´ Grpd.forgetToCat.map (Œ±.app Y)=
  Grpd.forgetToCat.map (Œ±.app X) ‚â´ (G ‚ãô Grpd.forgetToCat).map f := by
  simp only [Functor.comp_obj, Functor.comp_map]
  rw [‚Üê Grpd.forgetToCat.map_comp, ‚Üê Grpd.forgetToCat.map_comp]
  simp

lemma map_map_eqToHom {Œ± : F ‚ü∂ G} {X Y : ‚à´(F)} (f : X ‚ü∂ Y) :
    ((G ‚ãô Grpd.forgetToCat).map f.base).obj ((map Œ±).obj X).fiber =
  (Œ±.app Y.base).obj (((F ‚ãô Grpd.forgetToCat).map f.base).obj X.fiber) := by
    apply Eq.symm
    have equ1 :
      (Œ±.app Y.base).obj ((Grpd.forgetToCat.map (F.map f.base)).obj X.fiber) =
      ((Grpd.forgetToCat.map (F.map f.base)) ‚ãô (Œ±.app Y.base)).obj X.fiber := by simp
    have equ2 :
      (Grpd.forgetToCat.map (G.map f.base)).obj ((Œ±.app X.base).obj X.fiber) =
      ((Œ±.app X.base) ‚ãô (Grpd.forgetToCat.map (G.map f.base))).obj X.fiber := by simp
    simp only [Functor.comp_obj, Functor.comp_map, map_obj_fiber]
    rw[equ1, equ2]
    refine Functor.congr_obj ?_ X.fiber
    apply comp_forget_naturality

@[simp] theorem eqToHom_comp_fiber {C : Type u} [Category.{v} C] {A : C ‚•§ Grpd.{v‚ÇÅ, u‚ÇÅ}}
    {p q r : ‚à´(A)} (h : p = q) {f : q ‚ü∂ r} :
    (eqToHom h ‚â´ f).fiber = eqToHom (by subst h; simp) ‚â´ f.fiber := by
  simp [eqToHom_map]

end

theorem congr {C : Type u} [Category.{v, u} C] {F : C ‚•§ Grpd}
    {X Y : Groupoidal F} {f g : X ‚ü∂ Y} (h : f = g) :
    f.fiber = eqToHom (by subst h; rfl) ‚â´ g.fiber := by
  subst h
  dsimp
  simp

section
variable {C : Type u} [Category.{v} C] {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
    (F : C ‚•§ Grpd.{v‚ÇÇ,u‚ÇÇ})

theorem map_comp_eq {G H : C ‚•§ Grpd.{v‚ÇÇ,u‚ÇÇ}} (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H) :
    map (Œ± ‚â´ Œ≤) = map Œ± ‚ãô map Œ≤ := by
  simp [map, Grothendieck.map_comp_eq]

theorem preNatIso_congr {G H : D ‚•§ C} {Œ± Œ≤ : G ‚âÖ H} (h : Œ± = Œ≤) :
    preNatIso F Œ± = preNatIso F Œ≤ ‚â™‚â´ eqToIso (by subst h; simp) :=
  Grothendieck.preNatIso_congr _ h

@[simp] theorem preNatIso_eqToIso {G H : D ‚•§ C} {h : G = H} :
    preNatIso F (eqToIso h) = eqToIso (by
      subst h
      simp [Groupoidal.map_id_eq]) :=
  Grothendieck.preNatIso_eqToIso _

theorem preNatIso_comp {G1 G2 G3 : D ‚•§ C} (Œ± : G1 ‚âÖ G2) (Œ≤ : G2 ‚âÖ G3) :
    preNatIso F (Œ± ‚â™‚â´ Œ≤) = preNatIso F Œ± ‚â™‚â´ Functor.isoWhiskerLeft _ (preNatIso F Œ≤) ‚â™‚â´
    eqToIso (by simp [map_comp_eq, Functor.assoc]) :=
  Grothendieck.preNatIso_comp _ _ _

end

section
variable {Œì : Type u} [Groupoid.{v} Œì] (A : Œì ‚•§ Grpd.{v‚ÇÅ,u‚ÇÅ})

theorem map_eqToHom_base {G1 G2 : ‚à´(A)} (eq : G1 = G2)
    : A.map (eqToHom eq).base = eqToHom (by subst eq; simp) := by
  aesop_cat

open CategoryTheory.Functor in
/-- Every morphism `f : X ‚ü∂ Y` in the base category induces a natural transformation from the fiber
inclusion `Œπ F X` to the composition `F.map f ‚ãô Œπ F Y`. -/
def ŒπNatIso {X Y : Œì} (f : X ‚ü∂ Y) : Œπ A X ‚âÖ A.map f ‚ãô Œπ A Y where
  hom := ŒπNatTrans f
  inv := whiskerLeft (A.map f) (ŒπNatTrans (Groupoid.inv f)) ‚â´ eqToHom (by
    convert_to A.map (f ‚â´ Groupoid.inv f) ‚ãô Œπ A X = Œπ A X
    ¬∑ simp only [Functor.map_comp, Grpd.comp_eq_comp, Functor.assoc]
    ¬∑ simp)
  hom_inv_id := by
    ext a
    apply Groupoidal.Hom.hext
    ¬∑ simp
    . simp only [NatTrans.comp_app, whiskerLeft_app, comp_base,  comp_fiber, Grpd.map_comp_map,
        Category.assoc, eqToHom_trans_assoc, eqToHom_refl, Category.id_comp,
        NatTrans.id_app, id_fiber, eqToHom_comp_heq_iff]
      rw! (castMode := .all) [eqToHom_app, map_eqToHom_base, Category.id_comp,
        Functor.map_id, Category.id_comp]
      simp only [eqToHom_refl, fiber_eqToHom, eqRec_heq_iff_heq]
      -- these should be automated
      apply HEq.trans (eqToHom_heq_id_cod _ _ _)
      apply HEq.symm (eqToHom_heq_id_cod _ _ _)
  inv_hom_id := by
    ext a
    apply Groupoidal.Hom.hext
    ¬∑ simp
    . simp only [NatTrans.comp_app, whiskerLeft_app, comp_fiber, ŒπNatTrans_app_fiber,
        map_comp, eqToHom_map, Category.assoc, eqToHom_trans_assoc, NatTrans.id_app,
        id_fiber, eqToHom_comp_heq_iff]
      rw! [Functor.map_id, Functor.map_id, eqToHom_app]
      simp only [comp_obj, fiber_eqToHom, eqToHom_map, Category.id_comp, id_base,
        eqToHom_comp_heq_iff]
      exact (eqToHom_heq_id_cod _ _ _).symm

theorem ŒπNatIso_hom {x y : Œì} (f : x ‚ü∂ y) :
    (ŒπNatIso A f).hom = ŒπNatTrans f := by
  simp [ŒπNatIso]

@[simp] theorem ŒπNatIso_id (x : Œì) :
    ŒπNatIso A (ùüô x) = eqToIso (by simp) := by
  ext
  simp [ŒπNatIso]

theorem ŒπNatIso_comp {x y z : Œì} (f : x ‚ü∂ y) (g : y ‚ü∂ z) :
    ŒπNatIso A (f ‚â´ g) = ŒπNatIso A f ‚â™‚â´ Functor.isoWhiskerLeft (A.map f) (ŒπNatIso A g)
    ‚â™‚â´ eqToIso (by simp) := by
  ext
  simp [ŒπNatIso]

end

section
variable {C : Type u} [Category.{v} C] {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
variable {F : C ‚•§ Grpd.{v‚ÇÇ, u‚ÇÇ}} (A : D ‚•§ C) (fibObj : Œ† (x : D), (A ‚ãô F).obj x)
    (fibMap : Œ† {x y : D} (f : x ‚ü∂ y),
      ((A ‚ãô F).map f).obj (fibObj x) ‚ü∂ fibObj y)

theorem functorTo_map_id_aux (x : D) : ((A ‚ãô F).map (ùüô x)).obj (fibObj x) = fibObj x := by
  simp

theorem functorTo_map_comp_aux {x y z : D} (f : x ‚ü∂ y) (g : y ‚ü∂ z) :
    ((A ‚ãô F).map (f ‚â´ g)).obj (fibObj x)
    = (F.map (A.map g)).obj (((A ‚ãô F).map f).obj (fibObj x)) := by
  simp

variable
    (map_id : Œ† (x : D), fibMap (CategoryStruct.id x)
      = eqToHom (functorTo_map_id_aux A fibObj x))
    (map_comp : Œ† {x y z : D} (f : x ‚ü∂ y) (g : y ‚ü∂ z), fibMap (f ‚â´ g)
      = eqToHom (functorTo_map_comp_aux A fibObj f g)
      ‚â´ (F.map (A.map g)).map (fibMap f) ‚â´ fibMap g)

/-- To define a functor into `Grothendieck F` we can make use of an existing
  functor into the base. -/
def functorTo : D ‚•§ ‚à´(F) := Grothendieck.functorTo A fibObj fibMap map_id map_comp

@[simp] theorem functorTo_obj_base (x) :
    ((functorTo A fibObj fibMap map_id map_comp).obj x).base = A.obj x :=
  rfl

@[simp] theorem functorTo_obj_fiber (x) :
    ((functorTo A fibObj fibMap map_id map_comp).obj x).fiber = fibObj x :=
  rfl

@[simp] theorem functorTo_map_base {x y} (f : x ‚ü∂ y) :
    ((functorTo A fibObj fibMap map_id map_comp).map f).base = A.map f :=
  rfl

@[simp] theorem functorTo_map_fiber {x y} (f : x ‚ü∂ y) :
    ((functorTo A fibObj fibMap map_id map_comp).map f).fiber = fibMap f :=
  rfl

variable {A} {fibObj} {fibMap} {map_id} {map_comp}
@[simp] theorem functorTo_forget :
    functorTo _ _ _ map_id map_comp ‚ãô Grothendieck.forget _ = A :=
  rfl

end

section

-- TODO factor through Grothendieck
lemma eqToHom_eq_homOf_map {Œì : Type*} [Groupoid Œì] {F G : Œì ‚•§ Grpd} (h : F = G) :
    eqToHom (by rw [h]) = Grpd.homOf (map (eqToHom h)) := by
  subst h
  fapply CategoryTheory.Functor.ext
  ¬∑ intro x
    apply hext
    ¬∑ simp
    ¬∑ simp
  ¬∑ intro x y f
    rw! [Groupoidal.map_id_eq]
    simp

-- TODO factor through Grothendieck
theorem map_eqToHom_heq_id_dom {Œì : Type*} [Category Œì] {A A' : Œì ‚•§ Grpd}
    (h : A = A') : HEq (map (eqToHom h)) (ùü≠ (‚à´A)) := by
  subst h
  simp [map_id_eq]

-- TODO factor through Grothendieck
theorem map_eqToHom_heq_id_cod {Œì : Type*} [Category Œì] {A A' : Œì ‚•§ Grpd}
    (h : A = A') : HEq (map (eqToHom h)) (ùü≠ (‚à´A')) := by
  subst h
  simp [map_id_eq]

-- TODO factor through Grothendieck
theorem map_eqToHom_comp_heq {Œì D : Type*} [Category Œì] [Category D] {A A' : Œì ‚•§ Grpd}
    (h : A = A') (F : ‚à´(A') ‚•§ D) : HEq (map (eqToHom h) ‚ãô F) F := by
  apply Functor.precomp_heq_of_heq_id
  ¬∑ rw [h]
  ¬∑ rw [h]
  ¬∑ apply map_eqToHom_heq_id_cod

-- TODO factor through Grothendieck
theorem comp_map_eqToHom_heq {Œì D : Type*} [Category Œì] [Category D] {A A' : Œì ‚•§ Grpd}
    (h : A = A') (F : D ‚•§ ‚à´(A)) : HEq (F ‚ãô map (eqToHom h)) F := by
  apply Functor.comp_heq_of_heq_id
  ¬∑ rw [h]
  ¬∑ rw [h]
  ¬∑ apply map_eqToHom_heq_id_dom

-- TODO factor through Grothendieck
lemma pre_congr_functor {Œì Œî : Type*} [Category Œì] [Category Œî] (œÉ : Œî ‚•§ Œì)
    {F G : Œì ‚•§ Grpd} (h : F = G) :
  map (eqToHom (by rw[‚Üê h])) ‚ãô pre F œÉ ‚ãô map (eqToHom h) =
  pre G œÉ := by
  subst h
  simp only [eqToHom_refl, map_id_eq]
  exact rfl

end

end Groupoidal
end Functor
end CategoryTheory
