import GroupoidModel.Russell_PER_MS.Substitution

/-! In this file we specify typing judgments of the type theory
as `Prop`-valued relations. -/

section Notation -- TODO make notation local

declare_syntax_cat judgment
syntax:25 term:51 " âŠ¢[" term:51 "] " judgment:50 : term

syntax:50 term:51 : judgment
syntax:50 term:51 " â‰¡ " term:51 : judgment
syntax:50 term:51 " : " term:51 : judgment
syntax:50 term:51 " â‰¡ " term:51 " : " term:51 : judgment

set_option hygiene false in
macro_rules
  | `($Î“ âŠ¢[$l:term] $t:term : $A:term) => `($Î“ âŠ¢[$l] $t:term â‰¡ $t : $A)
  | `($Î“ âŠ¢[$l:term] $A:term â‰¡ $B:term) => `(EqTp $Î“ $l $A $B)
  | `($Î“ âŠ¢[$l:term] $A:term) => `($Î“ âŠ¢[$l] $A:term â‰¡ $A)
  | `($Î“ âŠ¢[$l:term] $t:term â‰¡ $u:term : $A:term) => `(EqTm $Î“ $l $t $u $A)

end Notation

/-- A typing context consisting of expressions and their universe levels. -/
abbrev Ctx := List (Expr Ã— Nat)

/-- `Lookup Î“ i A l` means that `(A, l)` is stored at index `i` in `Î“`.
This implies `Î“ âŠ¢[l] .bvar i : A`. -/
inductive Lookup : Ctx â†’ Nat â†’ Expr â†’ Nat â†’ Prop where
  | zero (Î“ A l) : Lookup ((A,l) :: Î“) 0 A.lift l
  | succ {Î“ A i l} : Lookup Î“ i A l â†’ Lookup ((A,l) :: Î“) (i+1) A.lift l

/-- The maximum `l` for which `Î“ âŠ¢[l] ğ’¥` makes sense.
When set to `0`, types cannot be quantified over at all. -/
def univMax := 37

/- `presupp` indicates presuppositions.
We don't add literally all of them,
just the ones needed that make inversion easy. -/

/- Convention on order of implicit parameters:
contexts, types, de Bruijn indices, universe levels. -/

mutual
inductive EqTp : Ctx â†’ Nat â†’ Expr â†’ Expr â†’ Prop
  -- Congruences / constructors
  | cong_pi {Î“ A A' B B' l l'} :
    Î“ âŠ¢[l] A â‰¡ A'â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] .pi l l' A B â‰¡ .pi l l' A' B'

  | cong_univ (Î“ l) :
    l < univMax â†’
    Î“ âŠ¢[l+1] .univ l

  | cong_el {Î“ A A' l} :
    Î“ âŠ¢[l+1] A â‰¡ A' : .univ l â†’
    Î“ âŠ¢[l] .el A â‰¡ .el A'

  -- Substitution
  | inst_tp {Î“ A B B' t u l l'} :
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[l] t â‰¡ u : A â†’
    Î“ âŠ¢[l'] B.inst t â‰¡ B.inst u

  -- lift

  -- Symmetric-transitive closure
  | symm_tp {Î“ A A' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] A' â‰¡ A

  | trans_tp {Î“ A A' A'' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] A' â‰¡ A'' â†’
    Î“ âŠ¢[l] A â‰¡ A''

inductive EqTm : Ctx â†’ Nat â†’ Expr â†’ Expr â†’ Expr â†’ Prop
  -- Congruences / constructors
  | cong_bvar {Î“ A i l} :
    Î“ âŠ¢[l] A â†’
    Lookup Î“ i A l â†’
    Î“ âŠ¢[l] .bvar i : A

  | cong_lam {Î“ A A' B t t' l l'} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] t â‰¡ t' : B â†’
    Î“ âŠ¢[max l l'] .lam l l' A t â‰¡ .lam l l' A' t' : .pi l l' A B

  | cong_app {Î“ A B B' f f' a a' l l'} :
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] f â‰¡ f' : .pi l l' A B â†’
    Î“ âŠ¢[l] a â‰¡ a' : A â†’
    Î“ âŠ¢[l'] .app l l' B f a â‰¡ .app l l' B' f' a' : B.inst a

  | cong_code {Î“ A A' l} :
    l < univMax â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l+1] .code A â‰¡ .code A' : .univ l

  -- Reductions
  | app_lam {Î“ A B t u l l'} :
    (A,l) :: Î“ âŠ¢[l'] t : B â†’
    Î“ âŠ¢[l] u : A â†’
    Î“ âŠ¢[l'] .app l l' B (.lam l l' A t) u â‰¡ t.inst u : B.inst u

  -- Expansions
  | eta {Î“ A B f l l'} :
    Î“ âŠ¢[max l l'] f : .pi l l' A B â†’
    Î“ âŠ¢[max l l'] f â‰¡ .lam l l' A (.app l l' (B.liftN 1 1) f.lift (.bvar 0)) : .pi l l' A B

  -- Conversion
  | conv {Î“ A A' t t' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t â‰¡ t' : A'

  -- Substitution
  | inst_tm {Î“ A B a b t u l l'} :
    (A,l) :: Î“ âŠ¢[l'] a â‰¡ b : B â†’
    Î“ âŠ¢[l] t â‰¡ u : A â†’
    Î“ âŠ¢[l'] a.inst t â‰¡ b.inst u : B.inst t

  -- lift

  -- Symmetric-transitive closure
  | symm_tm {Î“ A t t' l} :
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t' â‰¡ t : A

  | trans_tm {Î“ A t t' t'' l} :
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t' â‰¡ t'' : A â†’
    Î“ âŠ¢[l] t â‰¡ t'' : A
end

/-! Pretty-printers. -/

section PrettyPrinting
open Lean PrettyPrinter

@[app_unexpander EqTp]
def EqTp.unexpand : Unexpander
  | `($_ $Î“ $l $A $A') =>
    if A == A' then
      `($Î“ âŠ¢[$l] $A:term)
    else
      `($Î“ âŠ¢[$l] $A:term â‰¡ $A')
  | _ => throw ()

@[app_unexpander EqTm]
def EqTm.unexpand : Unexpander
  | `($_ $Î“ $l $t $t' $A) =>
    if t == t' then
      `($Î“ âŠ¢[$l] $t:term : $A)
    else
      `($Î“ âŠ¢[$l] $t:term â‰¡ $t' : $A)
  | _ => throw ()

end PrettyPrinting
