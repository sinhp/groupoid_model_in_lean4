import GroupoidModel.Russell_PER_MS.Autosubst

/-! ## Typing rules

In this file we specify typing judgments of the type theory
as `Prop`-valued relations. -/

section Notation -- TODO make notation local

declare_syntax_cat judgment
syntax:25 term:51 " âŠ¢[" term:51 "] " judgment:50 : term

syntax:50 term:51 : judgment
syntax:50 term:51 " â‰¡ " term:51 : judgment
syntax:50 term:51 " : " term:51 : judgment
syntax:50 term:51 " â‰¡ " term:51 " : " term:51 : judgment

set_option hygiene false in
macro_rules
  | `($Î“ âŠ¢[$l:term] $t:term : $A:term) => `(WfTm $Î“ $l $t $A)
  | `($Î“ âŠ¢[$l:term] $A:term â‰¡ $B:term) => `(EqTp $Î“ $l $A $B)
  | `($Î“ âŠ¢[$l:term] $A:term) => `(WfTp $Î“ $l $A)
  | `($Î“ âŠ¢[$l:term] $t:term â‰¡ $u:term : $A:term) => `(EqTm $Î“ $l $t $u $A)

end Notation

/-- A typing context consisting of type expressions and their universe levels. -/
abbrev Ctx := List (Expr Ã— Nat)

/-- The maximum `l` for which `Î“ âŠ¢[l] ğ’¥` makes sense.
When set to `0`, types cannot be quantified over at all. -/
-- TODO: this should be a parameter,
-- but adding an `optParam` to all judgments is super noisy.
-- If only we had parameterized modules..
def univMax : Nat := 3

/-- `Lookup Î“ i A l` means that `A = A'[â†‘â±âºÂ¹]`
where `(A', l)` is stored at index `i` in `Î“`.
Together with `âŠ¢ Î“`, this implies `Î“ âŠ¢[l] .bvar i : A`. -/
inductive Lookup : Ctx â†’ Nat â†’ Expr â†’ Nat â†’ Prop where
  | zero (Î“ A l) : Lookup ((A,l) :: Î“) 0 (A.subst Expr.wk) l
  | succ {Î“ A i l} (B l') : Lookup Î“ i A l â†’ Lookup ((B,l') :: Î“) (i+1) (A.subst Expr.wk) l

mutual

/-- All types in the given context are well-formed. -/
inductive WfCtx : Ctx â†’ Prop
  | nil : WfCtx []
  | snoc {Î“ A l} :
    WfCtx Î“ â†’
    Î“ âŠ¢[l] A â†’
    WfCtx ((A,l) :: Î“)

/-- The type is well-formed at the specified universe level.

Many of the inference rules have redundant premises ("presuppositions");
these rules are postfixed with a prime (').
This makes it easier to push syntactic metatheory through.
After proving inversion lemmas,
we define more efficient rules with fewer premises,
named the same but without the prime.
This is not just for usability:
it also means the Lean kernel is checking smaller derivation trees.

Convention on order of implicit parameters:
contexts, types, terms, de Bruijn indices, universe levels. -/
inductive WfTp : Ctx â†’ Nat â†’ Expr â†’ Prop
  -- Type formers
  | pi' {Î“ A B l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] .pi l l' A B

  | sigma' {Î“ A B l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] .sigma l l' A B

  | univ {Î“ l} :
    WfCtx Î“ â†’
    l < univMax â†’
    Î“ âŠ¢[l+1] .univ l

  | el {Î“ A l} :
    Î“ âŠ¢[l+1] A : .univ l â†’
    Î“ âŠ¢[l] .el A

/-- The two types are equal at the specified universe level. -/
inductive EqTp : Ctx â†’ Nat â†’ Expr â†’ Expr â†’ Prop
  -- Congruences
  | cong_pi' {Î“ A A' B B' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A' â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] .pi l l' A B â‰¡ .pi l l' A' B'

  | cong_sigma' {Î“ A A' B B' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A' â†’
    Î“ âŠ¢[l] A â‰¡ A'â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] .sigma l l' A B â‰¡ .sigma l l' A' B'

  | cong_el {Î“ A A' l} :
    Î“ âŠ¢[l+1] A â‰¡ A' : .univ l â†’
    Î“ âŠ¢[l] .el A â‰¡ .el A'

  -- Reflexive-symmetric-transitive closure
  | refl_tp {Î“ A l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A â‰¡ A

  | symm_tp {Î“ A A' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] A' â‰¡ A

  | trans_tp {Î“ A A' A'' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] A' â‰¡ A'' â†’
    Î“ âŠ¢[l] A â‰¡ A''

/-- The term has the specified type at the specified universe level. -/
inductive WfTm : Ctx â†’ Nat â†’ Expr â†’ Expr â†’ Prop
  -- Term formers
  | bvar {Î“ A i l} :
    WfCtx Î“ â†’
    Lookup Î“ i A l â†’
    Î“ âŠ¢[l] .bvar i : A

  | lam' {Î“ A B t l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] t : B â†’
    Î“ âŠ¢[max l l'] .lam l l' A t : .pi l l' A B

  | app' {Î“ A B f a l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] f : .pi l l' A B â†’
    Î“ âŠ¢[l] a : A â†’
    Î“ âŠ¢[l'] .app l l' A B f a : B.subst a.toSb

  | pair' {Î“ A B t u l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l'] u : B.subst t.toSb â†’
    Î“ âŠ¢[max l l'] .pair l l' B t u : .sigma l l' A B

  | fst' {Î“ A B p l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] p : .sigma l l' A B â†’
    Î“ âŠ¢[l] .fst l l' A B p : A

  | snd' {Î“ A B p l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] p : .sigma l l' A B â†’
    Î“ âŠ¢[l'] .snd l l' A B p : B.subst (Expr.fst l l' A B p).toSb

  | code {Î“ A l} :
    l < univMax â†’
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l+1] .code A : .univ l

  -- Conversion
  | conv {Î“ A A' t l} :
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] t : A'

/-- The two terms are equal at the specified type and universe level. -/
inductive EqTm : Ctx â†’ Nat â†’ Expr â†’ Expr â†’ Expr â†’ Prop
  -- Congruences
  | cong_lam' {Î“ A A' B t t' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A' â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] t â‰¡ t' : B â†’
    Î“ âŠ¢[max l l'] .lam l l' A t â‰¡ .lam l l' A' t' : .pi l l' A B

  | cong_app' {Î“ A A' B B' f f' a a' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] f â‰¡ f' : .pi l l' A B â†’
    Î“ âŠ¢[l] a â‰¡ a' : A â†’
    Î“ âŠ¢[l'] .app l l' A B f a â‰¡ .app l l' A' B' f' a' : B.subst a.toSb

  | cong_pair' {Î“ A B B' t t' u u' l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l'] u â‰¡ u' : B.subst t.toSb â†’
    Î“ âŠ¢[max l l'] .pair l l' B t u â‰¡ .pair l l' B' t' u' : .sigma l l' A B

  | cong_fst' {Î“ A A' B B' p p' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] p â‰¡ p' : .sigma l l' A B â†’
    Î“ âŠ¢[l] .fst l l' A B p â‰¡ .fst l l' A' B' p' : A

  | cong_snd' {Î“ A A' B B' p p' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] p â‰¡ p' : .sigma l l' A B â†’
    Î“ âŠ¢[l'] .snd l l' A B p â‰¡ .snd l l' A' B' p' : B.subst (Expr.fst l l' A B p).toSb

  | cong_code {Î“ A A' l} :
    l < univMax â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l+1] .code A â‰¡ .code A' : .univ l

  -- Reductions
  | app_lam' {Î“ A B t u l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    (A,l) :: Î“ âŠ¢[l'] t : B â†’
    Î“ âŠ¢[l] u : A â†’
    Î“ âŠ¢[l'] .app l l' A B (.lam l l' A t) u â‰¡ t.subst u.toSb : B.subst u.toSb

  | fst_pair' {Î“} {A B t u : Expr} {l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l'] u : B.subst t.toSb â†’
    Î“ âŠ¢[l] .fst l l' A B (.pair l l' B t u) â‰¡ t : A

  | snd_pair' {Î“} {A B t u : Expr} {l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l'] u : B.subst t.toSb â†’
    Î“ âŠ¢[l'] .snd l l' A B (.pair l l' B t u) â‰¡ u : B.subst t.toSb

  -- Expansions
  | lam_app' {Î“ A B f l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] f : .pi l l' A B â†’
    Î“ âŠ¢[max l l'] f â‰¡ .lam l l' A (.app l l' (A.subst Expr.wk) (B.subst (Expr.up Expr.wk)) (f.subst Expr.wk) (.bvar 0)) : .pi l l' A B

  | pair_fst_snd' {Î“ A B p l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] p : .sigma l l' A B â†’
    Î“ âŠ¢[max l l'] p â‰¡ .pair l l' B (.fst l l' A B p) (.snd l l' A B p) : .sigma l l' A B

  -- Conversion
  | conv_eq {Î“ A A' t t' l} :
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] t â‰¡ t' : A'

  -- Reflexive-symmetric-transitive closure
  | refl_tm {Î“ A t l} :
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l] t â‰¡ t : A

  | symm_tm' {Î“ A t t' l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t' â‰¡ t : A

  | trans_tm' {Î“ A t t' t'' l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t' â‰¡ t'' : A â†’
    Î“ âŠ¢[l] t â‰¡ t'' : A
end

/-! ## Pretty-printers -/

section PrettyPrinting
open Lean PrettyPrinter

@[app_unexpander WfTp]
def WfTp.unexpand : Unexpander
  | `($_ $Î“ $l $A) => `($Î“ âŠ¢[$l] $A:term)
  | _ => throw ()

@[app_unexpander EqTp]
def EqTp.unexpand : Unexpander
  | `($_ $Î“ $l $A $A') => `($Î“ âŠ¢[$l] $A:term â‰¡ $A')
  | _ => throw ()

@[app_unexpander WfTm]
def WfTm.unexpand : Unexpander
  | `($_ $Î“ $l $t $A) => `($Î“ âŠ¢[$l] $t:term : $A)
  | _ => throw ()

@[app_unexpander EqTm]
def EqTm.unexpand : Unexpander
  | `($_ $Î“ $l $t $t' $A) => `($Î“ âŠ¢[$l] $t:term â‰¡ $t' : $A)
  | _ => throw ()

end PrettyPrinting
