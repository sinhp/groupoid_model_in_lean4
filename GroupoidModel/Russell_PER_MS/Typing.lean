import GroupoidModel.Russell_PER_MS.Substitution

/-! In this file we specify typing judgments of the type theory
as `Prop`-valued relations. -/

section Notation -- TODO make notation local

declare_syntax_cat judgment
syntax:25 term:51 " âŠ¢[" term:51 "] " judgment:50 : term

syntax:50 term:51 : judgment
syntax:50 term:51 " â‰¡ " term:51 : judgment
syntax:50 term:51 " : " term:51 : judgment
syntax:50 term:51 " â‰¡ " term:51 " : " term:51 : judgment

set_option hygiene false in
macro_rules
  | `($Î“ âŠ¢[$l:term] $t:term : $A:term) => `($Î“ âŠ¢[$l] $t:term â‰¡ $t : $A)
  | `($Î“ âŠ¢[$l:term] $A:term â‰¡ $B:term) => `(EqTp $Î“ $l $A $B)
  | `($Î“ âŠ¢[$l:term] $A:term) => `($Î“ âŠ¢[$l] $A:term â‰¡ $A)
  | `($Î“ âŠ¢[$l:term] $t:term â‰¡ $u:term : $A:term) => `(EqTm $Î“ $l $t $u $A)

end Notation

/-- `Lookup Î“ i A` implies that `Î“ âŠ¢ .bvar i : A`. -/
inductive Lookup : List Expr â†’ Nat â†’ Expr â†’ Prop where
  | zero {Î“ A} : Lookup (A::Î“) 0 A.lift
  | succ {Î“ i A} : Lookup Î“ i A â†’ Lookup (A::Î“) (i+1) A.lift

/-- The maximum `l` for which `Î“ âŠ¢[l] ğ’¥` makes sense.
When set to `0`, types cannot be quantified over at all. -/
def univMax := 37

/- `presupp` indicates presuppositions.
We don't add literally all of them,
just the ones needed that make inversion easy. -/

/- Convention on order of implicit parameters:
contexts, types, de Bruijn indices, universe levels. -/

mutual
inductive EqTp : List Expr â†’ Nat â†’ Expr â†’ Expr â†’ Prop
  -- Congruences / constructors
  | cong_pi {Î“ A A' B B' l l'} :
    Î“ âŠ¢[l] A â‰¡ A'â†’
    A :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] .pi A B â‰¡ .pi A' B'

  | cong_univ (Î“ l) :
    l < univMax â†’
    Î“ âŠ¢[l+1] .univ l

  | cong_el {Î“ A A' l} :
    Î“ âŠ¢[l+1] A â‰¡ A' : .univ l â†’
    -- NOTE: the `el` is silent here.
    -- If needed, we can add it as a term former to the syntax,
    -- and continue interpreting it as before.
    Î“ âŠ¢[l] A â‰¡ A'

  -- Substitution
  | inst {Î“ A B B' t u l l'} :
    A :: Î“ âŠ¢[l] B â‰¡ B' â†’
    Î“ âŠ¢[l'] t â‰¡ u : A â†’
    Î“ âŠ¢[l] B.inst t â‰¡ B.inst u

  -- lift

  -- Symmetric-transitive closure
  | symm {Î“ A A' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] A' â‰¡ A

  | trans {Î“ A A' A'' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] A' â‰¡ A'' â†’
    Î“ âŠ¢[l] A â‰¡ A''

inductive EqTm : List Expr â†’ Nat â†’ Expr â†’ Expr â†’ Expr â†’ Prop
  -- Congruences / constructors
  | cong_bvar {Î“ A i l} :
    Î“ âŠ¢[l] A â†’
    Lookup Î“ i A â†’
    Î“ âŠ¢[l] .bvar i : A

  | cong_lam {Î“ A A' B t t' l l'} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    A :: Î“ âŠ¢[l'] t â‰¡ t' : B â†’
    Î“ âŠ¢[max l l'] .lam A t â‰¡ .lam A' t' : .pi A B

  | cong_app {Î“ A B B' f f' a a' l l'} :
    A :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] f â‰¡ f' : .pi A B â†’
    Î“ âŠ¢[l] a â‰¡ a' : A â†’
    Î“ âŠ¢[l'] .app B f a â‰¡ .app B' f' a' : B.inst a

  | cong_code {Î“ A A' l} :
    l < univMax â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    -- NOTE: See note on `cong_el`.
    Î“ âŠ¢[l+1] A â‰¡ A' : .univ l

  -- Substitution
  | inst {Î“ A B t u a b l l'} :
    A :: Î“ âŠ¢[l] t â‰¡ u : B â†’
    Î“ âŠ¢[l'] a â‰¡ b : A â†’
    Î“ âŠ¢[l] t.inst a â‰¡ u.inst b : B.inst a

  -- lift

  -- Reductions
  | app_lam {Î“ A B t u l l'} :
    A :: Î“ âŠ¢[l'] t : B â†’
    Î“ âŠ¢[l] u : A â†’
    Î“ âŠ¢[l'] .app B (.lam A t) u â‰¡ t.inst u : B.inst u

  -- Expansions
  | eta {Î“ A B t l} :
    Î“ âŠ¢[l] t : .pi A B â†’
    Î“ âŠ¢[l] t â‰¡ .lam A (.app B t.lift (.bvar 0)) : .pi A B

  -- Conversion
  | conv {Î“ A A' t t' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t â‰¡ t' : A'

  -- Symmetric-transitive closure
  | symm {Î“ A t t' l} :
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t' â‰¡ t : A

  | trans {Î“ A t t' t'' l} :
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t' â‰¡ t'' : A â†’
    Î“ âŠ¢[l] t â‰¡ t'' : A
end

/-! Pretty-printers. -/

section PrettyPrinting
open Lean PrettyPrinter

@[app_unexpander EqTp]
def EqTp.unexpand : Unexpander
  | `($_ $Î“ $l $A $A') =>
    if A == A' then
      `($Î“ âŠ¢[$l] $A:term)
    else
      `($Î“ âŠ¢[$l] $A:term â‰¡ $A')
  | _ => throw ()

@[app_unexpander EqTm]
def EqTm.unexpand : Unexpander
  | `($_ $Î“ $l $t $t' $A) =>
    if t == t' then
      `($Î“ âŠ¢[$l] $t:term : $A)
    else
      `($Î“ âŠ¢[$l] $t:term â‰¡ $t' : $A)
  | _ => throw ()

end PrettyPrinting
