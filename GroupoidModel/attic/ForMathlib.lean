import Mathlib.CategoryTheory.Limits.Shapes.Pullback.PullbackCone
import Mathlib.CategoryTheory.Groupoid.Discrete
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Mathlib.CategoryTheory.Category.ULift
import Mathlib.Logic.Function.ULift
import Mathlib.CategoryTheory.Category.Cat
import Mathlib.CategoryTheory.Category.Grpd
import Mathlib.CategoryTheory.Grothendieck
import Mathlib.Data.Part
import Mathlib.CategoryTheory.Monoidal.Cartesian.Basic
import Mathlib.CategoryTheory.Core
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Grothendieck
import SEq.Tactic.DepRewrite

/-! This file contains declarations missing from mathlib,
to be upstreamed. -/


/-

This comment space is for notes about mathlib definitions/theorems that should be fixed, refactored,
or redesigned.

- AsSmall.down and AsSmall.up should have their universe level order changed so that the third value comes first.
- currently I often write AsSmall.{_,_,w} because the first two can be inferred but not the max universe.

-/

namespace CategoryTheory

attribute [reassoc (attr := simp)] Limits.IsTerminal.comp_from
attribute [reassoc (attr := simp)] Limits.IsInitial.to_comp

@[reassoc]
theorem Limits.PullbackCone.IsLimit.comp_lift {C : Type*} [Category C]
    {X Y Z W' W : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} {t : PullbackCone f g}
    (œÉ : W' ‚ü∂ W) (ht : Limits.IsLimit t) (h : W ‚ü∂ X) (k : W ‚ü∂ Y) (w : h ‚â´ f = k ‚â´ g) :
    œÉ ‚â´ ht.lift (PullbackCone.mk h k w) =
      ht.lift (PullbackCone.mk (œÉ ‚â´ h) (œÉ ‚â´ k) (by simp [w])) := by
  refine ht.hom_ext fun j => ?_
  rcases j with _ | _ | _ <;> simp

end CategoryTheory

@[simp]
theorem Part.assert_dom {Œ± : Type*} (P : Prop) (x : P ‚Üí Part Œ±) :
    (Part.assert P x).Dom ‚Üî ‚àÉ h : P, (x h).Dom :=
  Iff.rfl

/-
  Mathlib.CategoryTheory.Category.ULift
-/
universe w v u v‚ÇÅ u‚ÇÅ v‚ÇÇ u‚ÇÇ v‚ÇÉ u‚ÇÉ

namespace CategoryTheory.ULift

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]

/- Composing with downFunctor is injective.
   This requires an explicit universe variable in its fifth universe argument `u`. -/
theorem comp_downFunctor_inj (F G : C ‚•§ ULift.{u} D) :
    F ‚ãô downFunctor = G ‚ãô downFunctor ‚Üî F = G := by
  constructor
  ¬∑ intro hFG
    apply Functor.ext
    ¬∑ intro x y
      exact Functor.congr_hom hFG
    ¬∑ intro x
      have h := Functor.congr_obj hFG x
      simp only [downFunctor, Functor.comp_obj, ULift.down_inj] at h
      exact h
  ¬∑ intro hFG
    subst hFG
    rfl

-- TODO change this to first universe argument

/- Composing with upFunctor is injective.
   This requires an explicit universe variable in its fifth universe paargument. -/
theorem comp_upFunctor_inj (F G : C ‚•§ D) : F ‚ãô upFunctor = G ‚ãô upFunctor ‚Üî F = G := by
  constructor
  ¬∑ intro hFG
    apply Functor.ext
    ¬∑ intro _ _
      exact Functor.congr_hom hFG
    ¬∑ intro x
      have h := Functor.congr_obj hFG x
      simp only [upFunctor, Functor.comp_obj, ULift.up_inj] at h
      exact h
  ¬∑ intro hFG
    subst hFG
    rfl

end CategoryTheory.ULift

/-
  Cat
-/

namespace CategoryTheory.Cat

/-- This is the proof of equality used in the eqToHom in `Cat.eqToHom_hom` -/
theorem eqToHom_hom_aux {C1 C2 : Cat.{v,u}} (x y: C1) (eq : C1 = C2) :
    (x ‚ü∂ y) = ((eqToHom eq).obj x ‚ü∂ (eqToHom eq).obj y) := by
  cases eq
  simp[CategoryStruct.id]

/-- This is the turns the hom part of eqToHom functors into a cast-/
theorem eqToHom_hom {C1 C2 : Cat.{v,u}} {x y: C1} (f : x ‚ü∂ y) (eq : C1 = C2) :
    (eqToHom eq).map f = (cast (Cat.eqToHom_hom_aux x y eq) f) := by
  cases eq
  simp[CategoryStruct.id]

/-- This turns the object part of eqToHom functors into casts -/
theorem eqToHom_obj {C1 C2 : Cat.{v,u}} (x : C1) (eq : C1 = C2) :
    (eqToHom eq).obj x = cast (congrArg Bundled.Œ± eq) x := by
  cases eq
  simp[CategoryStruct.id]

abbrev homOf {C D : Type u} [Category.{v} C] [Category.{v} D] (F : C ‚•§ D) :
    Cat.of C ‚ü∂ Cat.of D := F

@[simps] def ULift_lte_iso_self {C : Type (max u u‚ÇÅ)} [Category.{v} C] :
    Cat.of (ULift.{u} C) ‚âÖ Cat.of C where
  hom := ULift.downFunctor
  inv := ULift.upFunctor

@[simp] def ULift_succ_iso_self {C : Type (u + 1)} [Category.{v} C] :
    of (ULift.{u, u + 1} C) ‚âÖ of C := ULift_lte_iso_self.{v,u,u+1}

@[simp] def ULift_iso_self {C : Type u} [Category.{v} C] :
    of (ULift.{u, u} C) ‚âÖ of C := ULift_lte_iso_self

def ofULift (C : Type u) [Category.{v} C] : Cat.{v, max u w} :=
  of $ ULift.{w} C

def uLiftFunctor : Cat.{v,u} ‚•§ Cat.{v, max u w} where
  obj X := Cat.ofULift.{w} X
  map F := Cat.homOf $ ULift.downFunctor ‚ãô F ‚ãô ULift.upFunctor

end CategoryTheory.Cat

/-
  CategoryTheory.Grothedieck

-/

namespace CategoryTheory

section

variable (C : Type*) [Category C] (D : Type*) [Category D]

@[simp] lemma prod.eqToHom_fst (x y : C √ó D) (h : x = y) :
    (eqToHom h).1 = eqToHom (by aesop) := by
  subst h
  rfl

@[simp] lemma prod.eqToHom_snd (x y : C √ó D) (h : x = y) :
    (eqToHom h).2 = eqToHom (by aesop) := by
  subst h
  rfl

end

namespace Grothendieck

variable {Œì : Type*} [Category Œì] {A : Œì ‚•§ Cat}
  {x y : Grothendieck A}

theorem cast_eq {F G : Œì ‚•§ Cat}
    (h : F = G) (p : Grothendieck F) :
    (cast (by subst h; rfl) p : Grothendieck G)
    = ‚ü® p.base , cast (by subst h; rfl) p.fiber ‚ü© := by
  subst h
  rfl

theorem obj_hext
    (hbase : x.base = y.base) (hfiber : HEq x.fiber y.fiber) : x = y := by
  rcases x with ‚ü®xbase, xfiber‚ü©
  subst hbase
  subst hfiber
  rfl

theorem obj_hext_iff : x.base = y.base ‚àß HEq x.fiber y.fiber
    ‚Üî x = y := by
  constructor
  ¬∑ intro ‚ü® hŒ± , hstr ‚ü©
    exact obj_hext hŒ± hstr
  ¬∑ intro hCD
    subst hCD
    exact ‚ü® rfl , HEq.rfl ‚ü©

theorem obj_hext' {A' : Œì ‚•§ Cat.{v‚ÇÅ,u‚ÇÅ}} (h : A = A') {x : Grothendieck A} {y : Grothendieck A'}
  (hbase : HEq x.base y.base) (hfiber : HEq x.fiber y.fiber) : HEq x y := by
  rcases x; rcases y
  subst hbase
  congr

theorem hext' {A' : Œì ‚•§ Cat.{v‚ÇÅ,u‚ÇÅ}} (h : A = A') {X Y : Grothendieck A} {X' Y' : Grothendieck A'}
    (f : Hom X Y) (g : Hom X' Y') (hX : HEq X X') (hY : HEq Y Y')
    (w_base : HEq f.base g.base) (w_fiber : HEq f.fiber g.fiber) : HEq f g := by
  cases f; cases g
  congr

/-- This proves that base of an eqToHom morphism in the category Grothendieck A is an eqToHom morphism -/
theorem eqToHom_base (eq : x = y) :
    (eqToHom eq).base = eqToHom (by simp [eq]) := by
  cases eq
  simp

/-- This is the proof of equality used in the eqToHom in `Grothendieck.eqToHom_fiber` -/
theorem eqToHom_fiber_aux {Œì : Type u} [Category.{v} Œì] {A : Œì ‚•§ Cat.{v‚ÇÅ,u‚ÇÅ}} {g1 g2 : Grothendieck A}
    (eq : g1 = g2) : (A.map (eqToHom eq).base).obj g1.fiber = g2.fiber := by
  cases eq
  simp

/-- This proves that fiber of an eqToHom morphism in the category Grothendieck A is an eqToHom morphism -/
theorem eqToHom_fiber {Œì : Type u} [Category.{v} Œì] {A : Œì ‚•§ Cat.{v‚ÇÅ,u‚ÇÅ}} {g1 g2 : Grothendieck A}
    (eq : g1 = g2) : (eqToHom eq).fiber = eqToHom (Grothendieck.eqToHom_fiber_aux eq) := by
  cases eq
  simp

theorem obj_ext_cast
    (hbase : x.base = y.base)
    (hfiber : cast (congrArg (Œª x ‚Ü¶ (A.obj x).Œ±) hbase) x.fiber = y.fiber)
    : x = y := obj_hext hbase (heq_of_cast_eq (by simp[hbase]) (by simp[hfiber]))

theorem map_eqToHom_base_pf {G1 G2 : Grothendieck A} (eq : G1 = G2) :
    A.obj G1.base = A.obj G2.base := by subst eq; rfl

theorem map_eqToHom_base {G1 G2 : Grothendieck A} (eq : G1 = G2)
    : A.map (eqToHom eq).base = eqToHom (map_eqToHom_base_pf eq) := by
  simp [eqToHom_map]

theorem map_eqToHom_obj_base {F G : Œì ‚•§ Cat.{v,u}} (h : F = G)
  (x) : ((Grothendieck.map (eqToHom h)).obj x).base = x.base := rfl

theorem map_forget {F G : Œì ‚•§ Cat.{v,u}} (Œ± : F ‚ü∂ G) :
    Grothendieck.map Œ± ‚ãô Grothendieck.forget G =
    Grothendieck.forget F :=
  rfl

open Iso

variable {C : Type*} [Category C] {G : C ‚•§ Cat}

/-- A morphism in the Grothendieck construction is an isomorphism if
- the morphism in the base is an isomorphism; and
- the fiber morphism is an isomorphism. -/
def mkIso {X Y : Grothendieck G}
    (s : X.base ‚âÖ Y.base) (t : (G |>.map s.hom).obj X.fiber ‚âÖ Y.fiber) :
    X ‚âÖ Y where
  hom := { base := s.hom, fiber := t.hom }
  inv.base := s.inv
  inv.fiber := (G.map (s.inv)).map (t.inv) ‚â´
    eqToHom (by simpa only [Functor.map_comp, Functor.map_id] using
      congr((G.map $(s.hom_inv_id)).obj X.fiber))
  hom_inv_id := by
    apply ext
    erw [comp_fiber]
    simp only [map_hom_inv_id_assoc,
      eqToHom_trans, id_fiber] at *
    erw [comp_base, id_base]
    dsimp
    rw [s.hom_inv_id]
  inv_hom_id := by
    suffices ‚àÄ {Z g} (_ : g ‚â´ s.hom = Z) (_ : Z = ùüô _)
        {g'} (eq : g' ‚â´ (G.map g).map t.hom = ùüô _)
        (W) (eqW : G.map g ‚â´ G.map s.hom = W)
        (eq2 : ‚àÉ w1 w2, W.map t.hom = eqToHom w1 ‚â´ t.hom ‚â´ eqToHom w2) h1 h2,
        { base := Z, fiber := eqToHom h1 ‚â´ (G.map s.hom).map (g' ‚â´ eqToHom h2) ‚â´ t.hom } =
        ({..} : Grothendieck.Hom ..) from
      this rfl s.inv_hom_id (by simp)
        (W := ùüô _) (eqW := by simp) (eq2 := ‚ü®rfl, rfl, by simp‚ü©) ..
    rintro _ g - rfl g' eq _ rfl ‚ü®w1, w2, eq2 : (G.map s.hom).map _ = _‚ü© h1 h2; congr
    replace eq := congr((G.map s.hom).map $eq)
    simp only [Functor.map_comp, eq2, eqToHom_map, Category.assoc] at eq ‚ä¢
    conv at eq => lhs; slice 1 3
    rw [(comp_eqToHom_iff ..).1 eq]; simp

theorem Functor.hext (F G : C ‚•§ Grothendieck A)
    (hbase : F ‚ãô forget _ = G ‚ãô forget _)
    (hfiber_obj : ‚àÄ x : C, HEq (F.obj x).fiber (G.obj x).fiber)
    (hfiber_map : ‚àÄ {x y : C} (f : x ‚ü∂ y), HEq (F.map f).fiber (G.map f).fiber)
    : F = G := by
  fapply CategoryTheory.Functor.ext
  ¬∑ intro x
    apply obj_hext
    ¬∑ exact Functor.congr_obj hbase x
    ¬∑ apply hfiber_obj
  ¬∑ intro x y f
    fapply Grothendieck.ext
    ¬∑ simp only [comp_base, base_eqToHom]
      exact Functor.congr_hom hbase f
    ¬∑ apply eq_of_heq
      simp only [eqToHom_comp_heq_iff, comp_fiber, fiber_eqToHom, eqToHom_map, heq_eqToHom_comp_iff]
      rw! [eqToHom_base, eqToHom_map, Cat.eqToHom_hom]
      simp [hfiber_map]

theorem hext_iff (x y : Grothendieck A) (f g : x ‚ü∂ y) : f.base = g.base ‚àß HEq f.fiber g.fiber ‚Üî f = g := by
  constructor
  ¬∑ intro h
    apply Grothendieck.ext
    ¬∑ apply eq_of_heq
      simp only [eqToHom_comp_heq_iff, h.2]
    ¬∑ exact h.1
  ¬∑ aesop

section
variable {C : Type u} [Category.{v} C] {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
   (F : C ‚•§ Cat) {G H : D ‚•§ C} (Œ± : G ‚âÖ H)

@[simp] theorem preNatIso_hom_app_base (x) :
    ((preNatIso F Œ±).hom.app x).base = Œ±.hom.app x.base := by
  simp [Grothendieck.preNatIso]

@[simp] theorem preNatIso_hom_app_fiber (x) :
    ((preNatIso F Œ±).hom.app x).fiber = ùüô _ := by
  simp [Grothendieck.preNatIso]

end

end Grothendieck

namespace IsPullback

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]

variable {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

theorem of_iso_isPullback (h : IsPullback fst snd f g) {Q} (i : Q ‚âÖ P) :
      IsPullback (i.hom ‚â´ fst) (i.hom ‚â´ snd) f g := by
  have : Limits.HasPullback f g := ‚ü® h.cone , h.isLimit ‚ü©
  refine IsPullback.of_iso_pullback (by simp [h.w]) (i ‚â™‚â´ h.isoPullback) (by simp) (by simp)

end IsPullback
end CategoryTheory

namespace CategoryTheory

namespace AsSmall

@[simp] theorem up_map_down_map
    {C : Type u‚ÇÅ} [Category.{v‚ÇÅ, u‚ÇÅ} C] {X Y : C} (f : X ‚ü∂ Y) :
  AsSmall.down.map (AsSmall.up.map f) = f := rfl

@[simp] theorem down_map_up_map
    {C : Type u‚ÇÅ} [Category.{v‚ÇÅ, u‚ÇÅ} C]
    {X Y : AsSmall C} (f : X ‚ü∂ Y) :
  AsSmall.up.map (AsSmall.down.map f) = f := rfl

theorem comp_up_inj {C : Type u} [Category.{v} C]
  {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
    {F G : C ‚•§ D}
    (h : F ‚ãô (AsSmall.up : D ‚•§ AsSmall.{w} D) =
      G ‚ãô AsSmall.up)
    : F = G := by
  convert_to F ‚ãô (AsSmall.up : D ‚•§ AsSmall.{w} D)
    ‚ãô AsSmall.down
    = G ‚ãô (AsSmall.up : D ‚•§ AsSmall.{w} D)
    ‚ãô AsSmall.down
  simp [‚Üê Functor.assoc, h]

theorem comp_down_inj {C : Type u} [Category.{v} C]
  {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
    {F G : C ‚•§ AsSmall.{w} D}
    (h : F ‚ãô AsSmall.down = G ‚ãô AsSmall.down)
    : F = G := by
  convert_to F ‚ãô AsSmall.down
    ‚ãô AsSmall.up
    = G ‚ãô AsSmall.down ‚ãô AsSmall.up
  simp [‚Üê Functor.assoc, h]

@[simp] theorem up_comp_down
    {C : Type u‚ÇÅ} [Category.{v‚ÇÅ, u‚ÇÅ} C] :
  AsSmall.up ‚ãô AsSmall.down = Functor.id C := rfl

@[simp] theorem down_comp_up
    {C : Type u‚ÇÅ} [Category.{v‚ÇÅ, u‚ÇÅ} C] :
  AsSmall.down ‚ãô AsSmall.up = Functor.id (AsSmall C) := rfl

instance {C : Type u} [Category.{v} C] :
    Functor.IsEquivalence (AsSmall.up : C ‚•§ AsSmall C) :=
  AsSmall.equiv.isEquivalence_functor

end AsSmall

namespace Groupoid

instance asSmallGroupoid (Œì : Type u) [Groupoid.{v} Œì] :
    Groupoid (AsSmall.{w} Œì) where
  inv f := AsSmall.up.map (inv (AsSmall.down.map f))

end Groupoid

namespace Grpd

abbrev homOf {C D : Type u} [Groupoid.{v} C] [Groupoid.{v} D] (F : C ‚•§ D) :
    Grpd.of C ‚ü∂ Grpd.of D := F

lemma homOf_id {A : Type u} [Groupoid.{v} A] : Grpd.homOf (ùü≠ A) = ùüô _ :=
  rfl

lemma homOf_comp {A B C : Type u} [Groupoid.{v} A] [Groupoid.{v} B] [Groupoid.{v} C]
    (F : A ‚•§ B) (G : B ‚•§ C) : Grpd.homOf (F ‚ãô G) = Grpd.homOf F ‚â´ Grpd.homOf G :=
  rfl

def asSmallFunctor : Grpd.{v, u} ‚•§ Grpd.{max w v u, max w v u} where
  obj Œì := Grpd.of $ AsSmall.{max w v u} Œì
  map F := AsSmall.down ‚ãô F ‚ãô AsSmall.up

end Grpd

namespace Equivalence
noncomputable section
open Limits MonoidalCategory CartesianMonoidalCategory

variable {C : Type u‚ÇÅ} {D : Type u‚ÇÇ}
  [Category.{v‚ÇÅ} C] [Category.{v‚ÇÇ} D]
  [CartesianMonoidalCategory C]
  (e : Equivalence C D)

/-- The chosen terminal object in `D`. -/
abbrev chosenTerminal : D :=
  e.functor.obj (ùüô_ C)

/-- The chosen terminal object in `D` is terminal. -/
def chosenTerminalIsTerminal :
    IsTerminal (e.chosenTerminal : D) :=
  (IsTerminal.ofUnique _).isTerminalObj e.functor

/-- Product cones in `D` are defined using chosen products in `C` -/
def prodCone (X Y : D) : BinaryFan X Y :=
  .mk
  (P := e.functor.obj (MonoidalCategory.tensorObj
    (e.inverse.obj X) (e.inverse.obj Y)))
  (e.functor.map (fst _ _) ‚â´ (e.counit.app _))
  (e.functor.map (snd _ _) ‚â´ (e.counit.app _))

/-- The chosen product cone in `D` is a limit. -/
def isLimitProdCone (X Y : D) : IsLimit (e.prodCone X Y) :=
  IsLimit.ofIsoLimit (
  BinaryFan.isLimitCompRightIso _ (e.counit.app _) (
  BinaryFan.isLimitCompLeftIso _ (e.counit.app _) (
  isLimitCartesianMonoidalCategoryOfPreservesLimits e.functor
    (e.inverse.obj X) (e.inverse.obj Y))))
  (BinaryFan.ext (eqToIso rfl) (by aesop_cat) (by aesop_cat))

def chosenFiniteProducts [CartesianMonoidalCategory C] : CartesianMonoidalCategory D :=
  .ofChosenFiniteProducts
    { cone := asEmptyCone e.chosenTerminal
      isLimit := e.chosenTerminalIsTerminal }
    (fun X Y => {
      cone := e.prodCone X Y
      isLimit := e.isLimitProdCone X Y })

end
end Equivalence

section equivalence

def functorToAsSmallEquiv {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D] {C : Type u} [Category.{v} C]
    : D ‚•§ AsSmall.{w} C ‚âÉ D ‚•§ C where
  toFun A := A ‚ãô AsSmall.down
  invFun A := A ‚ãô AsSmall.up
  left_inv _ := rfl
  right_inv _ := rfl

open ULift

instance (C : Type u) [Category.{v} C] :
    (downFunctor : ULift.{w} C ‚•§ C).ReflectsIsomorphisms :=
  ULift.equivalence.fullyFaithfulInverse.reflectsIsomorphisms

instance (C : Type u) [Category.{v} C] :
    (upFunctor : C ‚•§ ULift.{w} C).ReflectsIsomorphisms :=
  ULift.equivalence.fullyFaithfulFunctor.reflectsIsomorphisms

instance (C : Type u) [Category.{v} C] :
    (AsSmall.down : AsSmall.{w} C ‚•§ C).ReflectsIsomorphisms :=
  AsSmall.equiv.fullyFaithfulInverse.reflectsIsomorphisms

instance (C : Type u) [Category.{v} C] :
    (AsSmall.up : C ‚•§ AsSmall.{w} C).ReflectsIsomorphisms :=
  AsSmall.equiv.fullyFaithfulFunctor.reflectsIsomorphisms

end equivalence

section
variable {Œì : Type u‚ÇÇ} [Category.{v‚ÇÇ} Œì] {A : Œì ‚•§ Grpd.{v‚ÇÅ,u‚ÇÅ}}

@[simp] theorem Cat.map_id_obj {A : Œì ‚•§ Cat.{v‚ÇÅ,u‚ÇÅ}}
    {x : Œì} {a : A.obj x} :
    (A.map (ùüô x)).obj a = a := by
  have : A.map (ùüô x) = ùüô (A.obj x) := by simp
  exact Functor.congr_obj this a

theorem Cat.map_id_map {A : Œì ‚•§ Cat.{v‚ÇÅ,u‚ÇÅ}}
    {x : Œì} {a b : A.obj x} {f : a ‚ü∂ b} :
    (A.map (ùüô x)).map f = eqToHom Cat.map_id_obj
      ‚â´ f ‚â´ eqToHom Cat.map_id_obj.symm := by
  have : A.map (ùüô x) = ùüô (A.obj x) := by simp
  exact Functor.congr_hom this f

end

namespace Grothendieck

variable {C : Type u} [Category.{v} C]
variable {F : C ‚•§ Cat.{v‚ÇÇ, u‚ÇÇ}}

theorem ŒπNatTrans_id_app {X : C} {a : F.obj X} :
    (@ŒπNatTrans _ _ F _ _ (ùüô X)).app a = eqToHom (by simp) := by
  apply ext <;> simp

theorem ŒπNatTrans_comp_app {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} {a} :
    (@ŒπNatTrans _ _ F _ _ (f ‚â´ g)).app a =
    (@ŒπNatTrans _ _ F _ _ f).app a ‚â´
    (@ŒπNatTrans _ _ F _ _ g).app ((F.map f).obj a) ‚â´ eqToHom (by simp) := by
  apply Grothendieck.ext
  ¬∑ simp
  ¬∑ simp

variable {Œì : Type u‚ÇÇ} [Category.{v‚ÇÇ} Œì] {Œî : Type u‚ÇÉ} [Category.{v‚ÇÉ} Œî]
    (œÉ : Œî ‚•§ Œì)

@[simp] theorem Œπ_pre (A : Œì ‚•§ Cat.{v‚ÇÅ,u‚ÇÅ}) (x : Œî)
    : Œπ (œÉ ‚ãô A) x ‚ãô Grothendieck.pre A œÉ = Œπ A (œÉ.obj x) := by
  apply CategoryTheory.Functor.ext
  ¬∑ intro x y f
    apply Grothendieck.ext
    ¬∑ simp [eqToHom_map, Cat.map_id_map]
    ¬∑ simp
  ¬∑ intro x
    rfl

section
variable {C : Type u} [Category.{v} C] {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
    (F : C ‚•§ Cat.{v‚ÇÇ,u‚ÇÇ})

theorem preNatIso_congr {G H : D ‚•§ C} {Œ± Œ≤ : G ‚âÖ H} (h : Œ± = Œ≤) :
    preNatIso F Œ± = preNatIso F Œ≤ ‚â™‚â´ eqToIso (by subst h; simp) := by
  subst h
  simp

@[simp] theorem preNatIso_eqToIso {G H : D ‚•§ C} {h : G = H} :
    preNatIso F (eqToIso h) = eqToIso (by
      subst h
      simp [Grothendieck.map_id_eq, Cat.id_eq_id, Functor.id_comp]) := by
  subst h
  ext
  apply Grothendieck.ext
  ¬∑ simp only [eqToIso_refl, Iso.refl_hom, eqToIso.hom, Category.comp_id,
      pre_obj_fiber, preNatIso, transportIso, transport_base,
      isoMk, transport_fiber, Iso.refl_inv, Iso.symm_mk, NatIso.ofComponents_hom_app]
    rw! [eqToHom_app, eqToHom_fiber]
  ¬∑ simp [preNatIso]

open CategoryTheory.Functor in
theorem preNatIso_comp {G1 G2 G3 : D ‚•§ C} (Œ± : G1 ‚âÖ G2) (Œ≤ : G2 ‚âÖ G3) :
    preNatIso F (Œ± ‚â™‚â´ Œ≤) = preNatIso F Œ± ‚â™‚â´ Functor.isoWhiskerLeft _ (preNatIso F Œ≤) ‚â™‚â´
    eqToIso (by simp [map_comp_eq, Functor.assoc]) := by
  ext p
  apply Grothendieck.ext
  ¬∑ simp only [Iso.trans_hom, Functor.comp_obj, pre_obj_base, map_obj_base, preNatIso,
      Iso.app_hom, isoWhiskerLeft_hom, eqToIso.hom, NatTrans.comp_app,
      NatIso.ofComponents_hom_app, Iso.symm_hom, whiskerLeft_app,
      map_obj_fiber, transportIso_inv_base, pre_obj_fiber,
      transportIso_inv_fiber, Category.comp_id, comp_fiber, Functor.map_id,
      Category.id_comp, eqToHom_trans_assoc]
    rw! [eqToHom_app, eqToHom_fiber, eqToHom_trans]
  ¬∑ simp [preNatIso]

end

variable {C : Type u} [Category.{v} C]
    {F : C ‚•§ Cat.{v‚ÇÅ,u‚ÇÅ}}

variable {E : Type*} [Category E]
variable (fib : ‚àÄ c, F.obj c ‚•§ E) (hom : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), fib c ‚ü∂ F.map f ‚ãô fib c')
variable (hom_id : ‚àÄ c, hom (ùüô c) = eqToHom (by simp only [Functor.map_id]; rfl))
variable (hom_comp : ‚àÄ c‚ÇÅ c‚ÇÇ c‚ÇÉ (f : c‚ÇÅ ‚ü∂ c‚ÇÇ) (g : c‚ÇÇ ‚ü∂ c‚ÇÉ), hom (f ‚â´ g) =
  hom f ‚â´ Functor.whiskerLeft (F.map f) (hom g) ‚â´ eqToHom (by simp only [Functor.map_comp]; rfl))

variable (K : Grothendieck F ‚•§ E)

def asFunctorFrom_fib (c : C) : (F.obj c) ‚•§ E := Œπ F c ‚ãô K

def asFunctorFrom_hom {c c' : C} (f: c ‚ü∂ c') :
    asFunctorFrom_fib K c ‚ü∂ F.map f ‚ãô asFunctorFrom_fib K c' :=
  Functor.whiskerRight (ŒπNatTrans f) K

lemma asFunctorFrom_hom_app {c c' : C} (f: c ‚ü∂ c') (p : F.obj c) :
    (asFunctorFrom_hom K f).app p = K.map ((ŒπNatTrans f).app p) :=
  rfl

lemma asFunctorFrom_hom_id (c : C) : asFunctorFrom_hom K (ùüô c) =
    eqToHom (by simp only[Functor.map_id,Cat.id_eq_id,Functor.id_comp]) := by
  ext p
  simp [asFunctorFrom_hom_app, eqToHom_map, ŒπNatTrans_id_app]

lemma asFunctorFrom_hom_comp (c‚ÇÅ c‚ÇÇ c‚ÇÉ : C) (f : c‚ÇÅ ‚ü∂ c‚ÇÇ) (g: c‚ÇÇ ‚ü∂ c‚ÇÉ) :
    asFunctorFrom_hom K (f ‚â´ g) =
    asFunctorFrom_hom K f ‚â´ Functor.whiskerLeft (F.map f) (asFunctorFrom_hom K g) ‚â´ eqToHom
    (by simp[‚Üê Functor.assoc]; congr) := by
  ext p
  simp [asFunctorFrom_hom, eqToHom_map, ŒπNatTrans_comp_app]

theorem asFunctorFrom : Grothendieck.functorFrom (asFunctorFrom_fib K) (asFunctorFrom_hom K)
    (asFunctorFrom_hom_id K) (asFunctorFrom_hom_comp K) = K := by
  fapply CategoryTheory.Functor.ext
  ¬∑ intro X
    rfl
  ¬∑ intro x y f
    simp only [functorFrom_obj, asFunctorFrom_fib, Functor.comp_obj, Œπ_obj, functorFrom_map,
      asFunctorFrom_hom, Functor.whiskerRight_app, Functor.comp_map, Œπ_map, ‚Üê Functor.map_comp,
      eqToHom_refl, Category.comp_id, Category.id_comp]
    congr
    fapply Grothendieck.ext
    ¬∑ simp
    ¬∑ simp

variable {D : Type*} [Category D] (G : E ‚•§ D)

def functorFrom_comp_fib (c : C) : F.obj c ‚•§ D := fib c ‚ãô G

def functorFrom_comp_hom {c c' : C} (f : c ‚ü∂ c') :
    functorFrom_comp_fib fib G c ‚ü∂ F.map f ‚ãô functorFrom_comp_fib fib G c' :=
  Functor.whiskerRight (hom f) G

include hom_id in
lemma functorFrom_comp_hom_id (c : C) : functorFrom_comp_hom fib hom G (ùüô c)
    = eqToHom (by simp [Cat.id_eq_id, Functor.id_comp]) := by
  ext x
  simp [hom_id, eqToHom_map, functorFrom_comp_hom]

include hom_comp in
lemma functorFrom_comp_hom_comp (c‚ÇÅ c‚ÇÇ c‚ÇÉ : C) (f : c‚ÇÅ ‚ü∂ c‚ÇÇ) (g : c‚ÇÇ ‚ü∂ c‚ÇÉ):
    functorFrom_comp_hom fib (fun {c c'} ‚Ü¶ hom) G (f ‚â´ g)
    = functorFrom_comp_hom fib (fun {c c'} ‚Ü¶ hom) G f ‚â´
    Functor.whiskerLeft (F.map f) (functorFrom_comp_hom fib hom G g) ‚â´
    eqToHom (by simp[Cat.comp_eq_comp, Functor.map_comp, Functor.assoc]) := by
  ext
  simp [functorFrom_comp_hom, hom_comp, eqToHom_map]

theorem functorFrom_comp : functorFrom fib hom hom_id hom_comp ‚ãô G =
    functorFrom (functorFrom_comp_fib fib G) (functorFrom_comp_hom fib hom G)
  (functorFrom_comp_hom_id fib hom hom_id G)
  (functorFrom_comp_hom_comp fib hom hom_comp G) := by
  fapply CategoryTheory.Functor.ext
  ¬∑ intro X
    simp [functorFrom_comp_fib]
  ¬∑ intro x y f
    simp [functorFrom_comp_hom, functorFrom_comp_fib]

variable (fib' : ‚àÄ c, F.obj c ‚•§ E) (hom' : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), fib' c ‚ü∂ F.map f ‚ãô fib' c')
variable (hom_id' : ‚àÄ c, hom' (ùüô c) = eqToHom (by simp only [Functor.map_id]; rfl))
variable (hom_comp' : ‚àÄ c‚ÇÅ c‚ÇÇ c‚ÇÉ (f : c‚ÇÅ ‚ü∂ c‚ÇÇ) (g : c‚ÇÇ ‚ü∂ c‚ÇÉ), hom' (f ‚â´ g) =
  hom' f ‚â´ Functor.whiskerLeft (F.map f) (hom' g) ‚â´ eqToHom (by simp only [Functor.map_comp]; rfl))

theorem functorFrom_eq_of (ef : fib = fib')
    (hhom : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), hom f ‚â´ eqToHom (by rw[ef]) = eqToHom (by rw[ef]) ‚â´ hom' f) :
    functorFrom fib hom hom_id hom_comp = functorFrom fib' hom' hom_id' hom_comp' := by
  subst ef
  congr!
  ¬∑ aesop_cat

theorem functorFrom_ext {K K' : Grothendieck F ‚•§ E}
    (hfib : asFunctorFrom_fib K = asFunctorFrom_fib K')
    (hhom : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), asFunctorFrom_hom K f ‚â´ eqToHom (by rw [hfib])
      = eqToHom (by rw[hfib]) ‚â´ asFunctorFrom_hom K' f)
    : K = K' :=
    calc K
     _ = functorFrom (asFunctorFrom_fib K) (asFunctorFrom_hom K)
         (asFunctorFrom_hom_id K) (asFunctorFrom_hom_comp K) :=
         (CategoryTheory.Grothendieck.asFunctorFrom K).symm
     _ = functorFrom (asFunctorFrom_fib K') (asFunctorFrom_hom K')
         (asFunctorFrom_hom_id K') (asFunctorFrom_hom_comp K') := by
         apply functorFrom_eq_of
         ¬∑ exact hhom
         ¬∑ exact hfib
     _ = K' := CategoryTheory.Grothendieck.asFunctorFrom K'

theorem functorFrom_hext {K K' : Grothendieck F ‚•§ E}
    (hfib : asFunctorFrom_fib K = asFunctorFrom_fib K')
    (hhom : ‚àÄ {c c' : C} (f : c ‚ü∂ c'), asFunctorFrom_hom K f ‚âç asFunctorFrom_hom K' f)
    : K = K' := by
  fapply functorFrom_ext
  ¬∑ assumption
  ¬∑ intros
    apply eq_of_heq
    simp only [heq_eqToHom_comp_iff, comp_eqToHom_heq_iff]
    apply hhom

theorem hext {X Y : Grothendieck F} (f g : Hom X Y) (w_base : f.base = g.base)
    (w_fiber : HEq f.fiber g.fiber) : f = g := by
  cases f; cases g
  congr

section functorTo
variable {C : Type u} [Category.{v} C] {D : Type u‚ÇÅ} [Category.{v‚ÇÅ} D]
variable {F : C ‚•§ Cat.{v‚ÇÇ, u‚ÇÇ}} (A : D ‚•§ C) (fibObj : Œ† (x : D), (A ‚ãô F).obj x)
    (fibMap : Œ† {x y : D} (f : x ‚ü∂ y),
      ((A ‚ãô F).map f).obj (fibObj x) ‚ü∂ fibObj y)

theorem functorTo_map_id_aux (x : D) : ((A ‚ãô F).map (ùüô x)).obj (fibObj x) = fibObj x := by
  simp

theorem functorTo_map_comp_aux {x y z : D} (f : x ‚ü∂ y) (g : y ‚ü∂ z) :
    ((A ‚ãô F).map (f ‚â´ g)).obj (fibObj x)
    = (F.map (A.map g)).obj (((A ‚ãô F).map f).obj (fibObj x)) := by
  simp

section
variable
    (map_id : Œ† (x : D), fibMap (CategoryStruct.id x)
      = eqToHom (functorTo_map_id_aux A fibObj x))
    (map_comp : Œ† {x y z : D} (f : x ‚ü∂ y) (g : y ‚ü∂ z), fibMap (f ‚â´ g)
      = eqToHom (functorTo_map_comp_aux A fibObj f g)
      ‚â´ (F.map (A.map g)).map (fibMap f) ‚â´ fibMap g)

/-- To define a functor into `Grothendieck F` we can make use of an existing
  functor into the base. -/
def functorTo : D ‚•§ Grothendieck F where
  obj x := ‚ü® A.obj x, fibObj x ‚ü©
  map f := ‚ü® A.map f, fibMap f ‚ü©
  map_id x := by
    fapply Grothendieck.ext
    ¬∑ simp
    ¬∑ simp [map_id]
  map_comp f g := by
    fapply Grothendieck.ext
    ¬∑ simp
    ¬∑ simp [map_comp]

@[simp] theorem functorTo_obj_base (x) :
    ((functorTo A fibObj fibMap map_id map_comp).obj x).base = A.obj x :=
  rfl

@[simp] theorem functorTo_obj_fiber (x) :
    ((functorTo A fibObj fibMap map_id map_comp).obj x).fiber = fibObj x :=
  rfl

@[simp] theorem functorTo_map_base {x y} (f : x ‚ü∂ y) :
    ((functorTo A fibObj fibMap map_id map_comp).map f).base = A.map f :=
  rfl

@[simp] theorem functorTo_map_fiber {x y} (f : x ‚ü∂ y) :
    ((functorTo A fibObj fibMap map_id map_comp).map f).fiber = fibMap f :=
  rfl

variable {A} {fibObj} {fibMap} {map_id} {map_comp}
@[simp] theorem functorTo_forget :
    functorTo _ _ _ map_id map_comp ‚ãô Grothendieck.forget _ = A :=
  rfl

end

variable
    (map_id : Œ† (x : D), fibMap (CategoryStruct.id x)
      = eqToHom (functorTo_map_id_aux A fibObj x))
    (map_comp : Œ† {x y z : D} (f : x ‚ü∂ y) (g : y ‚ü∂ z), HEq (fibMap (f ‚â´ g))
     ((F.map (A.map g)).map (fibMap f) ‚â´ fibMap g))

/-- To define a functor into `Grothendieck F` we can make use of an existing
  functor into the base. -/
def functorTo' : D ‚•§ Grothendieck F where
  obj x := ‚ü® A.obj x, fibObj x ‚ü©
  map f := ‚ü® A.map f, fibMap f ‚ü©
  map_id x := by
    fapply ext
    ¬∑ simp
    ¬∑ simp [map_id]
  map_comp f g := by
    fapply hext
    ¬∑ simp
    ¬∑ simp
      exact map_comp _ _

@[simp] theorem functorTo'_obj_base (x) :
    ((functorTo' A fibObj fibMap map_id map_comp).obj x).base = A.obj x :=
  rfl

@[simp] theorem functorTo'_obj_fiber (x) :
    ((functorTo' A fibObj fibMap map_id map_comp).obj x).fiber = fibObj x :=
  rfl

@[simp] theorem functorTo'_map_base {x y} (f : x ‚ü∂ y) :
    ((functorTo' A fibObj fibMap map_id map_comp).map f).base = A.map f :=
  rfl

@[simp] theorem functorTo'_map_fiber {x y} (f : x ‚ü∂ y) :
    ((functorTo' A fibObj fibMap map_id map_comp).map f).fiber = fibMap f :=
  rfl

variable {A} {fibObj} {fibMap} {map_id} {map_comp}
@[simp] theorem functorTo'_forget :
    functorTo' _ _ _ map_id map_comp ‚ãô Grothendieck.forget _ = A :=
  rfl
end functorTo

end Grothendieck

section
variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
  {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D]
  {E : Type u‚ÇÉ} [Category.{v‚ÇÉ} E]
  {B : Type u} [Category.{v} B]

@[simp]
theorem isoWhiskerLeft_eqToIso (F : C ‚•§ D) {G H : D ‚•§ E} (Œ∑ : G = H) :
    Functor.isoWhiskerLeft F (eqToIso Œ∑) = eqToIso (by subst Œ∑; rfl) := by
  subst Œ∑
  rfl
end
end CategoryTheory

namespace Equiv
def psigmaCongrProp {Œ±‚ÇÅ Œ±‚ÇÇ} {Œ≤‚ÇÅ : Œ±‚ÇÅ ‚Üí Prop} {Œ≤‚ÇÇ : Œ±‚ÇÇ ‚Üí Prop} (f : Œ±‚ÇÅ ‚âÉ Œ±‚ÇÇ)
    (F : ‚àÄ a, Œ≤‚ÇÅ a ‚Üî Œ≤‚ÇÇ (f a)) : PSigma Œ≤‚ÇÅ ‚âÉ PSigma Œ≤‚ÇÇ where
  toFun x := .mk (f x.1) (by rw [‚Üê F]; exact x.2)
  invFun x := .mk (f.symm x.1) (by
    simp only [F, apply_symm_apply]; exact x.2)
  left_inv _ := by simp
  right_inv _ := by simp

@[simp] theorem sigmaCongr_apply_fst {Œ±‚ÇÅ Œ±‚ÇÇ} {Œ≤‚ÇÅ : Œ±‚ÇÅ ‚Üí Sort _} {Œ≤‚ÇÇ : Œ±‚ÇÇ ‚Üí Sort _} (f : Œ±‚ÇÅ ‚âÉ Œ±‚ÇÇ)
    (F : ‚àÄ a, Œ≤‚ÇÅ a ‚âÉ Œ≤‚ÇÇ (f a)) (x : Sigma Œ≤‚ÇÅ) : (sigmaCongr f F x).fst = f x.fst := by
  simp [sigmaCongr]

@[simp] def sigmaCongr_apply_snd {Œ±‚ÇÅ Œ±‚ÇÇ} {Œ≤‚ÇÅ : Œ±‚ÇÅ ‚Üí Sort _} {Œ≤‚ÇÇ : Œ±‚ÇÇ ‚Üí Sort _} (f : Œ±‚ÇÅ ‚âÉ Œ±‚ÇÇ)
    (F : ‚àÄ a, Œ≤‚ÇÅ a ‚âÉ Œ≤‚ÇÇ (f a)) (x : Sigma Œ≤‚ÇÅ) : (sigmaCongr f F x).snd = F x.fst x.snd := by
  simp [sigmaCongr]

end Equiv

namespace CategoryTheory.Limits

variable {ùíû : Type u} [Category.{v} ùíû]

noncomputable def pullbackHomEquiv {A B C: ùíû} {Œì : ùíû} {f : A ‚ü∂ C} {g : B ‚ü∂ C} [HasPullback f g] :
    (Œì ‚ü∂ pullback f g) ‚âÉ
    (fst : Œì ‚ü∂ A) √ó (snd : Œì ‚ü∂ B) √ó' (fst ‚â´ f = snd ‚â´ g) where
  toFun h := ‚ü®h ‚â´ pullback.fst f g, h ‚â´ pullback.snd f g, by simp[pullback.condition]‚ü©
  invFun x := pullback.lift x.1 x.2.1 x.2.2
  left_inv _ := pullback.hom_ext (by simp) (by simp)
  right_inv := by rintro ‚ü®_,_,_‚ü©; congr!; simp; simp

end CategoryTheory.Limits

namespace CategoryTheory.IsPullback

variable {C : Type*} [Category C]

@[simp]
lemma lift_fst_snd {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}
    (pb : IsPullback fst snd f g) w : pb.lift fst snd w = ùüô _ := by
  apply pb.hom_ext <;> simp

end CategoryTheory.IsPullback

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [SmallCategory C] {F G : C·µí·µñ ‚•§ Type u‚ÇÅ}
  (app : ‚àÄ {X : C}, (yoneda.obj X ‚ü∂ F) ‚Üí (yoneda.obj X ‚ü∂ G))
  (naturality : ‚àÄ {X Y : C} (f : X ‚ü∂ Y) (Œ± : yoneda.obj Y ‚ü∂ F),
    app (yoneda.map f ‚â´ Œ±) = yoneda.map f ‚â´ app Œ±)

variable (F) in
/--
  A presheaf `F` on a small category `C` is isomorphic to
  the hom-presheaf `Hom(y(‚Ä¢),F)`.
-/
def yonedaIso : yoneda.op ‚ãô yoneda.obj F ‚âÖ F :=
  NatIso.ofComponents (fun _ => Equiv.toIso yonedaEquiv)
    (fun f => by ext : 1; dsimp; rw [yonedaEquiv_naturality'])

def yonedaIsoMap : yoneda.op ‚ãô yoneda.obj F ‚ü∂ yoneda.op ‚ãô yoneda.obj G where
  app _ := app
  naturality _ _ _ := by ext : 1; apply naturality

/-- Build natural transformations between presheaves on a small category
  by defining their action when precomposing by a morphism with
  representable domain -/
def NatTrans.yonedaMk : F ‚ü∂ G :=
  (yonedaIso F).inv ‚â´ yonedaIsoMap app naturality ‚â´ (yonedaIso G).hom

theorem NatTrans.yonedaMk_app {X : C} (Œ± : yoneda.obj X ‚ü∂ F) :
    Œ± ‚â´ yonedaMk app naturality = app Œ± := by
  rw [‚Üê yonedaEquiv.apply_eq_iff_eq, yonedaEquiv_comp]
  simp [yonedaMk, yonedaIso, yonedaIsoMap]

namespace Functor

theorem precomp_heq_of_heq_id {A B : Type u} {C : Type*} [Category.{v} A] [Category.{v} B] [Category C]
    (hAB : A = B) (h0 : HEq (inferInstance : Category A) (inferInstance : Category B)) {F : A ‚•§ B}
    (h : HEq F (ùü≠ B)) (G : B ‚•§ C) : HEq (F ‚ãô G) G := by
  subst hAB
  subst h0
  subst h
  rfl

theorem comp_heq_of_heq_id {A B : Type u} {C : Type*} [Category.{v} A] [Category.{v} B] [Category C]
    (hAB : A = B) (h0 : HEq (inferInstance : Category A) (inferInstance : Category B))
    {F : B ‚•§ A}
    (h : HEq F (ùü≠ B)) (G : C ‚•§ B) : HEq (G ‚ãô F) G := by
  subst hAB
  subst h0
  subst h
  rfl

end Functor

end CategoryTheory
