import Mathlib.CategoryTheory.Limits.Yoneda
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.CategoryTheory.Limits.Preserves.Finite
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq

/-!
  This file builds API for showing that a presheaf diagram is a pullback
  by its universal property among representable cones.
-/

universe u v uâ‚ vâ‚ uâ‚‚ vâ‚‚

namespace CategoryTheory

namespace Limits

universe uâ‚ƒ vâ‚ƒ
variable {J : Type uâ‚} [Category.{vâ‚} J] {K : Type uâ‚‚} [Category.{vâ‚‚} K]
variable {C : Type uâ‚ƒ} [Category.{vâ‚ƒ} C]

open CategoryTheory
open Functor

/-- A `c : RepCone F` is:
* an object `c.pt` and
* a natural transformation `c.Ï€ : yoneda.obj c.pt âŸ¶ F`
from the constant `yoneda.obj c.pt` functor to `F`.
-/
structure RepCone (F : J â¥¤ Cáµ’áµ– â¥¤ Type vâ‚ƒ) where
  /-- An object of `C` -/
  pt : C
  /-- A natural transformation from the constant functor at `X` to `F` -/
  Ï€ : (const J).obj (yoneda.obj pt) âŸ¶ F

namespace RepCone

variable {F : J â¥¤ Cáµ’áµ– â¥¤ Type vâ‚ƒ}

@[reducible] def cone (s : RepCone F) : Limits.Cone F where
  pt := yoneda.obj s.pt
  Ï€ := s.Ï€

end RepCone

variable {F : J â¥¤ Cáµ’áµ– â¥¤ Type vâ‚ƒ}

structure RepIsLimit (t : Cone F) where
  lift : âˆ€ s : RepCone F, s.cone.pt âŸ¶ t.pt
  fac : âˆ€ (s : RepCone F) (j : J),
    lift s â‰« t.Ï€.app j = (s.cone).Ï€.app j := by aesop_cat
  /-- It is the unique such map to do this -/
  uniq : âˆ€ (s : RepCone F) (m : s.cone.pt âŸ¶ t.pt)
    (_ : âˆ€ j : J, m â‰« t.Ï€.app j = s.cone.Ï€.app j), m = lift s := by
    aesop_cat

def repConeOfConeMap (s : Cone F) (c : C) (x' : yoneda.obj c âŸ¶ s.pt) : RepCone F :=
    { pt := c
      Ï€ := {app := Î» j â†¦ x' â‰« s.Ï€.app j}}

namespace RepIsLimit

variable {t : Cone F} (P : RepIsLimit t) {s : Cone F}

def lift' (c : C) (x' : yoneda.obj c âŸ¶ s.pt) : yoneda.obj c âŸ¶ t.pt :=
  P.lift $ repConeOfConeMap s c x'

@[simp] lemma lift'_naturality {s : Cone F} {c d : C}
    (f : c âŸ¶ d) (x' : yoneda.obj d âŸ¶ s.pt) :
    lift' P c (yoneda.map f â‰« x') = yoneda.map f â‰« lift' P d x' := by
  apply Eq.symm
  apply P.uniq (repConeOfConeMap s c (yoneda.map f â‰« x')) (yoneda.map f â‰« lift' P d x')
  intro j
  have h := P.fac (repConeOfConeMap s d x') j
  dsimp[repConeOfConeMap]
  dsimp[repConeOfConeMap] at h
  rw[Category.assoc, Category.assoc, â† h]
  rfl

def lift''_app (s : Cone F) (c : C) :
    s.pt.obj (Opposite.op c) â†’ t.pt.obj (Opposite.op c) :=
  yonedaEquiv âˆ˜ lift' P c âˆ˜ yonedaEquiv.symm

theorem lift''_app_naturality {c d : C} (f : c âŸ¶ d) :
    s.pt.map (f.op) â‰« lift''_app P s c
      = lift''_app P s d â‰« t.pt.map (Opposite.op f) := by
  ext x
  simp[lift''_app, lift']
  rw[yonedaEquiv_naturality']
  have h := lift'_naturality P f (yonedaEquiv.symm x)
  simp[lift'] at h
  simp
  rw[â† h, yonedaEquiv_symm_naturality_left]

variable (s)

def lift'' : s.pt âŸ¶ t.pt := {
  app := Î» c â†¦ lift''_app P s c.unop
  naturality := by
    intros
    apply lift''_app_naturality
  }

theorem fac_ext (j : J) (c : C) (x) :
    (lift'' P s â‰« t.Ï€.app j).app (Opposite.op c) x
    = (s.Ï€.app j).app (Opposite.op c) x := by
  dsimp [lift'',lift', lift''_app, â† yonedaEquiv_comp]
  rw [P.fac (repConeOfConeMap s c (yonedaEquiv.symm x)) j]
  simp [repConeOfConeMap, yonedaEquiv_comp, Equiv.apply_symm_apply yonedaEquiv x]

theorem uniq_ext (m : s.pt âŸ¶ t.pt)
    (hm : âˆ€ (j : J), m â‰« t.Ï€.app j = s.Ï€.app j) (c : C) (x) :
    m.app (Opposite.op c) x
    = (P.lift'' s).app (Opposite.op c) x := by
  let x' := yonedaEquiv.symm x
  have hj : (âˆ€ (j : J), (x' â‰« m) â‰« t.Ï€.app j = x' â‰« s.Ï€.app j) := by simp[hm]
  have h := P.uniq (repConeOfConeMap s c x') (x' â‰« m) hj
  dsimp [lift'', lift''_app, lift']
  rw [â† h, yonedaEquiv_comp, Equiv.apply_symm_apply yonedaEquiv x]

def IsLimit {t : Cone F} (P : RepIsLimit t) : IsLimit t where
  lift := lift'' P
  fac := Î» s j â†¦ by
    ext c x
    apply fac_ext
  uniq := Î» s m hm â†¦ by
    ext c x
    apply uniq_ext P s m hm

end RepIsLimit

abbrev RepPullbackCone {X Y Z : Cáµ’áµ– â¥¤ Type vâ‚ƒ} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :=
  RepCone (cospan f g)

namespace RepPullbackCone

variable {W X Y Z : Cáµ’áµ– â¥¤ Type vâ‚ƒ}
  {f : X âŸ¶ Z} {g : Y âŸ¶ Z} (t : RepPullbackCone f g)

def mk (W : C) (fst : yoneda.obj W âŸ¶ X) (snd : yoneda.obj W âŸ¶ Y)
    (h : fst â‰« f = snd â‰« g) :
    RepPullbackCone f g :=
  repConeOfConeMap (PullbackCone.mk fst snd h) W (ðŸ™ _)

def pullbackCone : PullbackCone f g where
  pt := yoneda.obj t.pt
  Ï€  := t.Ï€

/-- The first projection of a pullback cone. -/
abbrev fst : yoneda.obj t.pt âŸ¶ X :=
  t.pullbackCone.fst

/-- The second projection of a pullback cone. -/
abbrev snd : yoneda.obj t.pt âŸ¶ Y :=
  t.pullbackCone.snd

@[reassoc]
theorem condition : t.fst â‰« f = t.snd â‰« g :=
  t.pullbackCone.condition

open WalkingSpan.Hom WalkingCospan.Hom WidePullbackShape.Hom WidePushoutShape.Hom Limits.PullbackCone

def repIsLimitAux (t : PullbackCone f g) (lift : âˆ€ s : RepPullbackCone f g, yoneda.obj s.pt âŸ¶ t.pt)
    (fac_left : âˆ€ s : RepPullbackCone f g, lift s â‰« t.fst = s.fst)
    (fac_right : âˆ€ s : RepPullbackCone f g, lift s â‰« t.snd = s.snd)
    (uniq : âˆ€ (s : RepPullbackCone f g) (m : yoneda.obj s.pt âŸ¶ t.pt)
      (_ : âˆ€ j : WalkingCospan, m â‰« t.Ï€.app j = s.Ï€.app j), m = lift s) : RepIsLimit t :=
  { lift
    fac := fun s j => Option.casesOn j (by
        rw [â† s.cone.w inl, â† t.w inl, â† Category.assoc]
        congr
        exact fac_left s)
      fun j' => WalkingPair.casesOn j' (fac_left s) (fac_right s)
    uniq := uniq }

/-- To show that a `PullbackCone` in a presheaf category constructed using `PullbackCone.mk` is a limit cone,
  it suffices to show its universal property among representable cones.
-/
def RepIsLimit.mk {fst : W âŸ¶ X} {snd : W âŸ¶ Y} (eq : fst â‰« f = snd â‰« g)
    (lift : âˆ€ s : RepPullbackCone f g, yoneda.obj s.pt âŸ¶ W)
    (fac_left : âˆ€ s : RepPullbackCone f g, lift s â‰« fst = s.fst)
    (fac_right : âˆ€ s : RepPullbackCone f g, lift s â‰« snd = s.snd)
    (uniq :
      âˆ€ (s : RepPullbackCone f g) (m : yoneda.obj s.pt âŸ¶ W)
      (_ : m â‰« fst = s.fst) (_ : m â‰« snd = s.snd),
        m = lift s) :
    IsLimit (PullbackCone.mk fst snd eq) :=
  RepIsLimit.IsLimit $
  repIsLimitAux _ lift fac_left fac_right fun s m w =>
  uniq s m (w WalkingCospan.left) (w WalkingCospan.right)

theorem is_pullback {fst : W âŸ¶ X} {snd : W âŸ¶ Y} (eq : fst â‰« f = snd â‰« g)
    (lift : âˆ€ s : RepPullbackCone f g, yoneda.obj s.pt âŸ¶ W)
    (fac_left : âˆ€ s : RepPullbackCone f g, lift s â‰« fst = s.fst)
    (fac_right : âˆ€ s : RepPullbackCone f g, lift s â‰« snd = s.snd)
    (uniq :
      âˆ€ (s : RepPullbackCone f g) (m : yoneda.obj s.pt âŸ¶ W)
      (_ : m â‰« fst = s.fst) (_ : m â‰« snd = s.snd),
        m = lift s) :
    IsPullback fst snd f g :=
  IsPullback.of_isLimit' âŸ¨ eq âŸ© (RepIsLimit.mk eq lift fac_left fac_right uniq)

end RepPullbackCone


end Limits
