import GroupoidModel.ForMathlib

universe w v u vâ‚ uâ‚ vâ‚‚ uâ‚‚ vâ‚ƒ uâ‚ƒ

namespace CategoryTheory
namespace Grpd

open Limits

/-- The chosen terminal object in `Grpd`. -/
abbrev chosenTerminal : Grpd.{u,u} := Grpd.of (Discrete.{u} PUnit)

/-- The chosen terminal object in `Grpd` is terminal. -/
def chosenTerminalIsTerminal : IsTerminal chosenTerminal :=
  IsTerminal.ofUniqueHom (fun _ â†¦ (Functor.const _).obj âŸ¨âŸ¨âŸ©âŸ©) fun _ _ â†¦ rfl

/-- The chosen product of categories `C Ã— D` yields a product cone in `Grpd`. -/
def prodCone (C D : Grpd.{u,u}) : BinaryFan C D :=
  .mk (P := .of (C Ã— D)) (Prod.fst _ _) (Prod.snd _ _)

/-- The product cone in `Grpd` is indeed a product. -/
def isLimitProdCone (X Y : Grpd) : IsLimit (prodCone X Y) := BinaryFan.isLimitMk
  (fun S => S.fst.prod' S.snd) (fun _ => rfl) (fun _ => rfl) (fun A B h1 h2 =>
    Functor.hext
      (fun x â†¦ Prod.ext (by dsimp; rw [â† h1]; rfl)
      (by dsimp; rw [â† h2]; rfl))
      (fun _ _ _ â†¦ by dsimp; rw [â† h1, â† h2]; rfl))

instance : CartesianMonoidalCategory Grpd :=
  .ofChosenFiniteProducts
    { cone := asEmptyCone chosenTerminal
      isLimit := chosenTerminalIsTerminal }
    (fun X Y => {
      cone := prodCone X Y
      isLimit := isLimitProdCone X Y })

/-- The identity in the category of groupoids equals the identity functor.-/
theorem id_eq_id (X : Grpd) : ğŸ™ X = ğŸ­ X := rfl

-- NOTE this is currently called `Grpd.hom_to_functor` in mathlib,
-- but this naming is inconsistent with that of `Cat`
/-- Composition in the category of groupoids equals functor composition.-/
theorem comp_eq_comp {X Y Z : Grpd} (F : X âŸ¶ Y) (G : Y âŸ¶ Z) : F â‰« G = F â‹™ G := rfl

theorem eqToHom_obj
  {C1 C2 : Grpd.{v,u}} (x : C1) (eq : C1 = C2) :
    (eqToHom eq).obj x = cast (congrArg Bundled.Î± eq) x := by
  cases eq
  simp [id_eq_id]

section
variable {Î“ : Type uâ‚‚} [Category.{vâ‚‚} Î“] {A : Î“ â¥¤ Grpd.{vâ‚,uâ‚}}

@[simp] theorem map_id_obj {x : Î“} {a : A.obj x} :
    (A.map (ğŸ™ x)).obj a = a := by
  have : A.map (ğŸ™ x) = ğŸ™ (A.obj x) := by simp
  exact Functor.congr_obj this a

@[simp] theorem map_id_map
    {x : Î“} {a b : A.obj x} {f : a âŸ¶ b} :
    (A.map (ğŸ™ x)).map f = eqToHom Grpd.map_id_obj
      â‰« f â‰« eqToHom Grpd.map_id_obj.symm := by
  have : A.map (ğŸ™ x) = ğŸ™ (A.obj x) := by simp
  exact Functor.congr_hom this f

@[simp] theorem map_comp_obj
    {x y z : Î“} {f : x âŸ¶ y} {g : y âŸ¶ z} {a : A.obj x} :
    (A.map (f â‰« g)).obj a = (A.map g).obj ((A.map f).obj a) := by
  have : A.map (f â‰« g) = A.map f â‹™ A.map g := by
    simp [Grpd.comp_eq_comp]
  have h := Functor.congr_obj this a
  simp only [Functor.comp_obj] at h
  exact h

@[simp] theorem map_comp_map
    {x y z : Î“} {f : x âŸ¶ y} {g : y âŸ¶ z} {a b : A.obj x} {Ï† : a âŸ¶ b} :
    (A.map (f â‰« g)).map Ï†
    = eqToHom Grpd.map_comp_obj â‰« (A.map g).map ((A.map f).map Ï†)
    â‰« eqToHom Grpd.map_comp_obj.symm := by
  have : A.map (f â‰« g) = A.map f â‰« A.map g := by simp
  exact Functor.congr_hom this Ï†

theorem map_comp_map'
    {x y z : Î“} {f : x âŸ¶ y} {g : y âŸ¶ z} {a b : A.obj x} {Ï† : a âŸ¶ b} :
    (A.map g).map ((A.map f).map Ï†)
    = eqToHom Grpd.map_comp_obj.symm â‰« (A.map (f â‰« g)).map Ï† â‰« eqToHom Grpd.map_comp_obj
    := by
  simp [Grpd.map_comp_map]
end

@[simp] theorem id_obj {C : Grpd} (X : C) :
    (ğŸ™ C : C â¥¤ C).obj X = X :=
  rfl

@[simp] theorem comp_obj {C D E : Grpd} (F : C âŸ¶ D) (G : D âŸ¶ E)
    (X : C) : (F â‰« G).obj X = G.obj (F.obj X) :=
  rfl

variable {Î“ : Type u} [Category.{v} Î“] (F : Î“ â¥¤ Grpd.{vâ‚,uâ‚})

theorem map_eqToHom_obj {x y : Î“} (h : x = y) (t) :
    (F.map (eqToHom h)).obj t = cast (by rw [h]) t := by
  subst h
  simp

/-- This is the proof of equality used in the eqToHom in `Grpd.eqToHom_hom` -/
theorem eqToHom_hom_aux {C1 C2 : Grpd.{v,u}} (x y: C1) (eq : C1 = C2) :
    (x âŸ¶ y) = ((eqToHom eq).obj x âŸ¶ (eqToHom eq).obj y) := by
  cases eq
  simp[CategoryStruct.id]

/-- This is the turns the hom part of eqToHom functors into a cast-/
theorem eqToHom_hom {C1 C2 : Grpd.{v,u}} {x y: C1} (f : x âŸ¶ y) (eq : C1 = C2) :
    (eqToHom eq).map f = (cast (Grpd.eqToHom_hom_aux x y eq) f) := by
  cases eq
  simp only [eqToHom_refl, id_eq_id, Functor.id_map, cast_eq]

@[simp] theorem map_eqToHom_map {x y : Î“} (h : x = y) {t s} (f : t âŸ¶ s) :
    (F.map (eqToHom h)).map f =
    eqToHom (Functor.congr_obj (eqToHom_map _ _) t)
    â‰« cast (Grpd.eqToHom_hom_aux t s (by rw [h])) f
    â‰« eqToHom (Eq.symm (Functor.congr_obj (eqToHom_map _ _) s)) := by
  have h1 : F.map (eqToHom h) = eqToHom (by rw [h]) := eqToHom_map _ _
  rw [Functor.congr_hom h1, Grpd.eqToHom_hom]

@[simp] theorem eqToHom_app {C : Type uâ‚} [Category.{vâ‚} C]
    {D : Type uâ‚‚} [Category.{vâ‚‚} D] (F G : C â¥¤ D) (h : F = G) (X : C) :
    (eqToHom h).app X = eqToHom (by subst h; rfl) := by
  subst h
  simp

end Grpd
end CategoryTheory
