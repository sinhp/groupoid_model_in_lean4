import GroupoidModel.ForMathlib
import Mathlib.CategoryTheory.Widesubcategory
import GroupoidModel.ForMathlib.CategoryTheory.Functor.Iso

universe v u vâ‚ uâ‚ vâ‚‚ uâ‚‚ vâ‚ƒ uâ‚ƒ

/-!
## Strict (meta-theoretic) 1-pullbacks of categories
* Due to universe level complications, it is convenient to have
  a version of pullbacks of categories that is agnostic to universe levels,
  providing a universal property for cones with apex of any universe size.
  This is essentially the same as asking for a pullback in `Cat.{max â‹¯, max â‹¯}`,
  (where all the categories are adjusted by `ULift`).
* However, since Lean cannot express quantification of universe variables,
  we state `CategoryTheory.Functor.IsPullback` via being isomorphic
  to a chosen pullback
  `CategoryTheory.Functor.IsPullback.Chosen`.

## Main definitions
* `CategoryTheory.Functor.IsPullback.Chosen` a construction of the
  strict 1-pullback of categories as a (non-full)
  subcategory of the product.
* `CategoryTheory.Functor.IsPullback` being a pullback of a functor
  is to be isomorphic (functors to and from that compose to identity)
  to the chosen pullback `CategoryTheory.Functor.IsPullback.Chosen`.
* Pullback pasting, in `pasteHoriz` and `ofRight` and `ofRight'`.
-/

namespace CategoryTheory.Functor

namespace IsPullback

section east_south

variable {Egypt : Type*} [Category Egypt]
  {Chad : Type*} [Category Chad]
  {Sudan : Type*} [Category Sudan]
  (east : Egypt â¥¤ Sudan) (south : Chad â¥¤ Sudan)

/--
  The chosen pullback category `Chosen` is a wide subcategory
  of this intermediate category `ChosenObjects`,
  which itself is a full subcategory of the product `Egypt Ã— Chad`.
  Objects in this full subcategory are those whose two components are
  sent to equal objects in the base category `Sudan`.
-/
def ChosenObjects := ObjectProperty.FullSubcategory (
  fun p : Egypt Ã— Chad => east.obj p.1 = south.obj p.2 )

namespace ChosenObjects

instance : Category (ChosenObjects east south) :=
  inferInstanceAs (Category (ObjectProperty.FullSubcategory _))

end ChosenObjects

/--
  Morphisms in `Chosen` are morphisms from `ChosenObjects`
  whose two components are sent to equal maps in the base category `Sudan`.
-/
def morphismProperty : MorphismProperty (ChosenObjects east south) :=
  fun {x y} f => east.map f.1
    = eqToHom x.property â‰« south.map f.2 â‰« eqToHom y.property.symm

instance : MorphismProperty.IsMultiplicative (morphismProperty east south) where
  id_mem x := by
    simp [morphismProperty, ChosenObjects, ObjectProperty.FullSubcategory.id_def]
  comp_mem f g hf hg := by
    simp only [morphismProperty, ChosenObjects,
      ObjectProperty.FullSubcategory.comp_def, comp_eqToHom_iff] at *
    simp [hg, hf]

/--
  The chosen pullback category `Chosen` is a wide subcategory
  of `ChosenObjects`,
  consisting of pairs of maps from `Egypt` and `Chad`
  that are equal in the base category `Sudan`.
-/
def Chosen := WideSubcategory (morphismProperty east south)

instance : Category (Chosen east south) :=
  inferInstanceAs (Category (WideSubcategory _))

end east_south

namespace Chosen

section
/-
       north
  Chosen ----> Egypt
    |          |
  west         |east
    |          |
    V          V
  Chad ----> Sudan
       south
-/
variable {Egypt : Type*} [Category Egypt]
  {Chad : Type*} [Category Chad]
  {Sudan : Type*} [Category Sudan]
  {east : Egypt â¥¤ Sudan} {south : Chad â¥¤ Sudan}

def north : Chosen east south â¥¤ Egypt :=
  wideSubcategoryInclusion _ â‹™ ObjectProperty.Î¹ _ â‹™ Prod.fst _ _

def west : Chosen east south â¥¤ Chad :=
  wideSubcategoryInclusion _ â‹™ ObjectProperty.Î¹ _ â‹™ Prod.snd _ _

theorem comm_sq : @north _ _ _ _ _ _ east south â‹™ east = west â‹™ south := by
  fapply Functor.ext
  Â· intro x
    exact x.obj.property
  Â· intro x y f
    exact f.property

end

variable {Egypt Chad Sudan : Type*}
  [Category Egypt] [Category Chad] [Category Sudan]
  {east : Egypt â¥¤ Sudan} {south : Chad â¥¤ Sudan}

variable {C : Type u} [Category.{v} C] (Cn : C â¥¤ Egypt) (Cw : C â¥¤ Chad)
    (hC : Cn â‹™ east = Cw â‹™ south)

/--
The universal lift of the chosen pullback `Chosen`.
-/
def lift : C â¥¤ Chosen east south where
  obj x := âŸ¨ âŸ¨ Cn.obj x , Cw.obj x âŸ© , congr_obj hC x âŸ©
  map f := âŸ¨ âŸ¨ Cn.map f , Cw.map f âŸ© , congr_hom hC f âŸ©

/--
The universal lift of the chosen pullback `Chosen` commutes with projections.
-/
theorem fac_left : lift Cn Cw hC â‹™ north = Cn :=
  rfl

/--
The universal lift of the chosen pullback `Chosen` commutes with projections.
-/
theorem fac_right : lift Cn Cw hC â‹™ west = Cw :=
  rfl

/--
Lifts of the chosen pullback `Chosen` are unique.
-/
theorem hom_ext {l0 l1 : C â¥¤ Chosen east south} (hnorth : l0 â‹™ north = l1 â‹™ north)
   (hwest : l0 â‹™ west = l1 â‹™ west) : l0 = l1 := by
  fapply Functor.ext
  Â· intro x
    apply WideSubcategory.ext
    apply ObjectProperty.FullSubcategory.ext
    apply Prod.ext
    Â· exact congr_obj hnorth x
    Â· exact congr_obj hwest x
  Â· intro x y f
    apply (wideSubcategory.faithful _).map_injective
    apply (ObjectProperty.faithful_Î¹ _).map_injective
    apply prod.hom_ext
    Â· convert congr_hom hnorth f
      simp only [Functor.map_comp, eqToHom_map]
      simp [north]
    Â· convert congr_hom hwest f
      simp only [Functor.map_comp, eqToHom_map]
      simp [west]

end Chosen

end IsPullback


open IsPullback

/--
A pullback structure on a type `Libya` over a pair of functors
`east` and `south` with the same codomain consists of an isomorphism
with the chosen pullback `CategoryTheory.Functor.IsPullback.Chosen east south`.
This is a strict isomorphism in the sense of having functors back and forth
that compose to *equal* the identity,
but since the universe levels are variable these are not the same it does not fit
into mathlib's definition of an isomorphism.
-/
structure IsPullback {Libya Egypt Chad Sudan : Type*}
    [Category Libya] [Category Egypt] [Category Chad] [Category Sudan]
    (north : Libya â¥¤ Egypt) (west : Libya â¥¤ Chad)
    (east : Egypt â¥¤ Sudan) (south : Chad â¥¤ Sudan) where
  (comm_sq : north â‹™ east = west â‹™ south)
  (toChosen : Libya â¥¤ Chosen east south)
  (fromChosen : Chosen east south â¥¤ Libya)
  (to_from_id : toChosen â‹™ fromChosen = ğŸ­ _)
  (from_to_id : fromChosen â‹™ toChosen = ğŸ­ _)
  (from_north : fromChosen â‹™ north = Chosen.north)
  (from_west : fromChosen â‹™ west = Chosen.west)

namespace IsPullback

section
variable {Libya Egypt Chad Sudan : Type*} [Category Libya]
  [Category Egypt] [Category Chad] [Category Sudan]
  (north : Libya â¥¤ Egypt) (west : Libya â¥¤ Chad)
  (east : Egypt â¥¤ Sudan) (south : Chad â¥¤ Sudan)

section
variable {Libya'} [Category Libya']
  (h : IsPullback north west east south)

/--
We can construct a pullback by only providing an isomorphism to the chosen pullback.
-/
def ofIso (to' : Libya' â¥¤ Libya)
    (from' : Libya â¥¤ Libya')
    (htf : to' â‹™ from' = ğŸ­ _) (hft: from' â‹™ to' = ğŸ­ _) :
    IsPullback (to' â‹™ north) (to' â‹™ west) east south where
  comm_sq := by rw [Functor.assoc, h.comm_sq, Functor.assoc]
  toChosen := to' â‹™ h.toChosen
  fromChosen := h.fromChosen â‹™ from'
  to_from_id := calc to' â‹™ (h.toChosen â‹™ h.fromChosen) â‹™ from'
    _ = to' â‹™ from' := by rw [h.to_from_id, Functor.id_comp]
    _ = ğŸ­ _ := htf
  from_to_id := calc h.fromChosen â‹™ (from' â‹™ to') â‹™ h.toChosen
    _ = h.fromChosen â‹™ h.toChosen := by rw [hft, Functor.id_comp]
    _ = ğŸ­ _ := h.from_to_id
  from_north := calc h.fromChosen â‹™ (from' â‹™ to') â‹™ north
    _ = h.fromChosen â‹™ north := by rw [hft, Functor.id_comp]
    _ = Chosen.north := h.from_north
  from_west := calc h.fromChosen â‹™ (from' â‹™ to') â‹™ west
    _ = h.fromChosen â‹™ west := by rw [hft, Functor.id_comp]
    _ = Chosen.west := h.from_west

end

def Chosen.isPullback : IsPullback (@Chosen.north _ _ _ _ _ _ east south)
    Chosen.west east south where
  comm_sq := Chosen.comm_sq
  toChosen := ğŸ­ _
  fromChosen := ğŸ­ _
  to_from_id := rfl
  from_to_id := rfl
  from_north := rfl
  from_west := rfl

/--
We can construct a pullback by only providing an isomorphism to the chosen pullback.
-/
def ofIsoChosen (toChosen : Libya â¥¤ Chosen east south)
    (fromChosen : Chosen east south â¥¤ Libya)
    (htf : toChosen â‹™ fromChosen = ğŸ­ _) (hft: fromChosen â‹™ toChosen = ğŸ­ _) :
    IsPullback (toChosen â‹™ Chosen.north) (toChosen â‹™ Chosen.west) east south :=
  ofIso _ _ _ _ (Chosen.isPullback east south) toChosen fromChosen htf hft

variable {north} {east} {south} {west} (P : IsPullback north west east south)
/--
Commuting conditions between a general pullback `P` and the chosen pullback.
-/
theorem toChosen_north : P.toChosen â‹™ Chosen.north = north := by
  simp [â† P.from_north, â† Functor.assoc, P.to_from_id, Functor.id_comp]

/--
Commuting conditions between a general pullback `P` and the chosen pullback.
-/
theorem toChosen_west : P.toChosen â‹™ Chosen.west = west := by
  simp [â† P.from_west, â† Functor.assoc, P.to_from_id, Functor.id_comp]

variable {C : Type u} [Category.{v} C] (Cn : C â¥¤ Egypt) (Cw : C â¥¤ Chad)
    (hC : Cn â‹™ east = Cw â‹™ south)

/--
The universal lift of the pullback `P`.
-/
def lift : C â¥¤ Libya := Chosen.lift Cn Cw hC â‹™ P.fromChosen

/--
Commuting conditions for the universal lift of the pullback `P`.
-/
theorem fac_left : lift P Cn Cw hC â‹™ north = Cn := by
  simp [lift, Functor.assoc, P.from_north, Chosen.fac_left]


/--
Commuting conditions for the universal lift of the pullback `P`.
-/
theorem fac_right : lift P Cn Cw hC â‹™ west = Cw := by
  simp [lift, Functor.assoc, P.from_west, Chosen.fac_right]

include P in
/--
Uniqueness of universal lifts for the pullback `P`.
-/
theorem hom_ext {l0 l1 : C â¥¤ Libya} (hnorth : l0 â‹™ north = l1 â‹™ north)
    (hwest : l0 â‹™ west = l1 â‹™ west) : l0 = l1 :=
  calc l0
    _ = l0 â‹™ P.toChosen â‹™ P.fromChosen := by
      rw [P.to_from_id, Functor.comp_id]
    _ = l1 â‹™ P.toChosen â‹™ P.fromChosen := by
      dsimp only [â† Functor.assoc]
      congr 1
      apply Chosen.hom_ext
      Â· simp [Functor.assoc, toChosen_north, hnorth]
      Â· simp [Functor.assoc, toChosen_west, hwest]
    _ = l1 := by rw [P.to_from_id, Functor.comp_id]

/--
Uniqueness of universal lifts for the pullback `P`.
-/
theorem lift_uniq {l : C â¥¤ Libya} (hnorth : l â‹™ north = Cn)
    (hwest : l â‹™ west = Cw) : l = lift P Cn Cw hC := by
  apply hom_ext P
  Â· simp [hnorth, fac_left]
  Â· simp [hwest, fac_right]
end

section
variable {Libya : Type u} {Egypt : Type uâ‚} {Chad : Type uâ‚‚} {Sudan : Type uâ‚ƒ}
  [Category.{v} Libya] [Category.{vâ‚} Egypt]
  [Category.{vâ‚‚} Chad] [Category.{vâ‚ƒ} Sudan]
  (north : Libya â¥¤ Egypt) (west : Libya â¥¤ Chad)
  (east : Egypt â¥¤ Sudan) (south : Chad â¥¤ Sudan)
  (comm_sq : north â‹™ east = west â‹™ south)

variable (lChosen : âˆ€ {C : Type (max uâ‚ uâ‚‚)} [Category.{max vâ‚ vâ‚‚} C]
  (Cn : C â¥¤ Egypt) (Cw : C â¥¤ Chad)
  (hC : Cn â‹™ east = Cw â‹™ south),
  (lift : C â¥¤ Libya) Ã—'
  (lift â‹™ north = Cn) âˆ§
  (lift â‹™ west = Cw) âˆ§
  (âˆ€ {l0 l1 : C â¥¤ Libya}, l0 â‹™ north = l1 â‹™ north â†’
    l0 â‹™ west = l1 â‹™ west â†’ l0 = l1))

variable (lLibya : âˆ€ {C : Type u} [Category.{v} C]
  (Cn : C â¥¤ Egypt) (Cw : C â¥¤ Chad)
  (hC : Cn â‹™ east = Cw â‹™ south),
  (lift : C â¥¤ Libya) Ã—'
  (lift â‹™ north = Cn) âˆ§
  (lift â‹™ west = Cw) âˆ§
  (âˆ€ {l0 l1 : C â¥¤ Libya}, l0 â‹™ north = l1 â‹™ north â†’
    l0 â‹™ west = l1 â‹™ west â†’ l0 = l1))
/--
  To define a pullback structure on a category,
  rather than showing a category is isomorphic to the chosen pullback,
  we can instead show its universal property.

  Unfortunately, because we cannot quantify over universe level `u`
  we cannot state the general universal property as a hypothesis.
  Instead, one should on an ad-hoc basis state and prove a more general universal property
  (i.e. using a fresh universe variable)
  and then specialize it to these two choices of universe levels.
  For an example of this see `Category.Functor.IsPullback.pasteHoriz`.

  Note that when we *use* the universal property of a pullback
  this problem does not arise.
  See `CategoryTheory.Functor.IsPullback.lift` etc.
-/
def ofUniversal : IsPullback north west east south := {
  comm_sq := comm_sq
  toChosen := Chosen.lift north west comm_sq
  fromChosen := (lChosen Chosen.north Chosen.west Chosen.comm_sq).1
  to_from_id := by
    apply (lLibya north west comm_sq).2.2.2
    Â· rw [Functor.assoc, (lChosen _ _ Chosen.comm_sq).2.1,
        Chosen.fac_left, Functor.id_comp]
    Â· rw [Functor.assoc, (lChosen _ _ Chosen.comm_sq).2.2.1,
        Chosen.fac_right, Functor.id_comp]
  from_to_id := by
    apply Chosen.hom_ext
    Â· rw [Functor.assoc, Chosen.fac_left, Functor.id_comp,
        (lChosen _ _ Chosen.comm_sq).2.1]
    Â· rw [Functor.assoc, Chosen.fac_right, Functor.id_comp,
        (lChosen _ _ Chosen.comm_sq).2.2.1]
  from_north := (lChosen _ _ Chosen.comm_sq).2.1
  from_west := (lChosen _ _ Chosen.comm_sq).2.2.1
}


end

section

variable {Libya Egypt Chad Sudan : Type*} [Category Libya]
  [Category Egypt] [Category Chad] [Category Sudan]
  (north : Libya â¥¤ Egypt) (west : Libya â¥¤ Chad)
  (east : Egypt â¥¤ Sudan) (south : Chad â¥¤ Sudan)
  (pb : IsPullback north west east south)

variable {north} in
lemma Iso.inv_comp_eq_comp_inv {north'} (lib : Iso Chad Libya) (egy : Iso Sudan Egypt)
(hnorth : north' â‹™ egy.hom = lib.hom â‹™ north): lib.inv â‹™ north' = north â‹™ egy.inv
    â†” north' â‹™ egy.hom = lib.hom â‹™ north := by
    rw [lib.inv_comp_eq, â† Functor.assoc, egy.eq_comp_inv, hnorth]

variable {Libya' Egypt' Chad' Sudan' : Type*} [Category Libya']
  [Category Egypt'] [Category Chad'] [Category Sudan']
  (north' : Libya' â¥¤ Egypt') (west' : Libya' â¥¤ Chad')
  (east' : Egypt' â¥¤ Sudan') (south' : Chad' â¥¤ Sudan')
  (lib : Iso Libya' Libya) (egy : Iso Egypt' Egypt)
  (cha : Iso Chad' Chad) (sud : Iso Sudan' Sudan)
  (hnorth : north' â‹™ egy.hom = lib.hom â‹™ north) (hwest : lib.hom â‹™ west = west' â‹™ cha.hom)
  (heast : egy.hom â‹™ east = east' â‹™ sud.hom) (hsouth : south' â‹™ sud.hom = cha.hom â‹™ south)

include hnorth in

include north west east south pb north' west' east' south' lib egy cha
  sud hnorth hwest heast hsouth in
theorem ofIso'_comm_sq : north' â‹™ east' = west' â‹™ south' :=
  calc north' â‹™ east'
  _ = lib.hom â‹™ north â‹™ egy.inv â‹™ east' := by rw [egy.eq_comp_inv.mpr hnorth]; rfl
  _ = lib.hom â‹™ (north â‹™ east) â‹™ sud.inv := by
    rw [egy.eq_inv_comp.mpr heast]; simp [Functor.comp_id, Functor.assoc]
  _ = lib.hom â‹™ (west â‹™ south) â‹™ sud.inv := by rw [pb.comm_sq]
  _ = west' â‹™ (cha.hom â‹™ south) â‹™ sud.inv := by
    rw [lib.eq_inv_comp.mpr hwest]; simp [Functor.id_comp, â† Functor.assoc]
  _ = west' â‹™ south' := by rw [sud.eq_comp_inv.mpr hsouth]

def ofIso'Lift {C : Type*} [Category C] (Cn : C â¥¤ Egypt') (Cw : C â¥¤ Chad')
    (hC : Cn â‹™ east' = Cw â‹™ south') : C â¥¤ Libya' :=
  pb.lift (Cn â‹™ egy.hom) (Cw â‹™ cha.hom) (by simp [Functor.assoc, heast, â† hsouth, hC])
  â‹™ lib.inv

def ofIso'Universal {C : Type*} [Category C]
    (Cn : C â¥¤ Egypt') (Cw : C â¥¤ Chad') (hC : Cn â‹™ east' = Cw â‹™ south')
    : (lift : C â¥¤ Libya') Ã—' lift â‹™ north' = Cn âˆ§ lift â‹™ west' = Cw âˆ§
    âˆ€ {l0 l1 : C â¥¤ Libya'}, l0 â‹™ north' = l1 â‹™ north' â†’ l0 â‹™ west' = l1 â‹™ west'
    â†’ l0 = l1 :=
  âŸ¨ ofIso'Lift north west east south pb east' south' lib egy cha sud heast hsouth Cn Cw hC,
    by rw [ofIso'Lift, Functor.assoc, (Iso.inv_comp_eq_comp_inv lib egy hnorth).mpr hnorth,
        â† Functor.assoc, pb.fac_left, Functor.assoc, egy.hom_inv_id, Functor.comp_id],
    by rw [ofIso'Lift, Functor.assoc, (Iso.inv_comp_eq_comp_inv lib cha hwest.symm).mpr hwest.symm,
        â† Functor.assoc, pb.fac_right, Functor.assoc, cha.hom_inv_id, Functor.comp_id],
    by
      intro l0 l1 hn hw
      have : l0 â‹™ lib.hom = l1 â‹™ lib.hom := by
        apply pb.hom_ext
        Â· rw [Functor.assoc, â† hnorth, â† Functor.assoc, hn, Functor.assoc, hnorth, Functor.assoc]
        Â· rw [Functor.assoc, hwest, â† Functor.assoc, hw, Functor.assoc, â† hwest, Functor.assoc]
      calc l0
        _ = l0 â‹™ lib.hom â‹™ lib.inv := by aesop_cat
        _ = l1 â‹™ lib.hom â‹™ lib.inv := by rw [â† Functor.assoc, this, Functor.assoc]
        _ = l1 := by aesop_cat
  âŸ©

/--
Libya' --------------------------> Egypt'
  |    \âˆ¨                       v/   |
  |        Libya -------->Egypt      |
  |          |              |        |
  |          |              |        |
  |          |              |        |
  |          v              v        |
  |        Chad ---------> Sudan     |
  v      /^                     ^\   v
Chad' ---------------------------> Sudan

If the inner square is a pullback and all corners are isomorphic to the outer square,
then the outer square is also a pullback.
-/
def ofIso' : IsPullback north' west' east' south' :=
  ofUniversal north' west' east' south'
  (ofIso'_comm_sq north west east south pb north' west' east' south' lib egy cha
    sud hnorth hwest heast hsouth)
  (fun Cn Cw hC => ofIso'Universal north west east south pb north' west' east' south' lib egy cha
      sud hnorth hwest heast hsouth Cn Cw hC)
  (fun Cn Cw hC => ofIso'Universal north west east south pb north' west' east' south' lib egy cha
      sud hnorth hwest heast hsouth Cn Cw hC)

end

namespace Paste
variable {Algeria Libya Egypt Niger Chad Sudan : Type*} [Category Algeria]
  [Category Libya] [Category Egypt] [Category Niger] [Category Chad] [Category Sudan]

section no_rth

/-
           no           rth
  Algeria -----> Libya ----> Egypt
    |              |          |
  west         sah |          | east
    |              |          |
    V              V          V
  Niger   -----> Chad  ----> Sudan
           so           uth
-/
variable
  {no : Algeria â¥¤ Libya} {rth : Libya â¥¤ Egypt}
  {west : Algeria â¥¤ Niger} {sah : Libya â¥¤ Chad} {east : Egypt â¥¤ Sudan}
  {so : Niger â¥¤ Chad} {uth : Chad â¥¤ Sudan}
  (wsah : no â‹™ sah = west â‹™ so) (esah : rth â‹™ east = sah â‹™ uth)

include esah wsah in
theorem outer_comm_sq : no â‹™ rth â‹™ east = west â‹™ so â‹™ uth := by
  rw [esah, â† Functor.assoc, wsah, Functor.assoc]

section horiz

variable (esah_pb : IsPullback rth sah east uth) (wsah_pb : IsPullback no west sah so)

namespace horiz

variable {C : Type u} [Category.{v} C] (Cn : C â¥¤ Egypt) (Cw : C â¥¤ Niger)
  (hC : Cn â‹™ east = Cw â‹™ so â‹™ uth)

def lift : C â¥¤ Algeria :=
  wsah_pb.lift (esah_pb.lift Cn (Cw â‹™ so) hC) Cw (esah_pb.fac_right _ _ _)

def universal : (lift : C â¥¤ Algeria) Ã—'
    lift â‹™ no â‹™ rth = Cn âˆ§
    lift â‹™ west = Cw âˆ§
    âˆ€ {l0 l1 : C â¥¤ Algeria}, l0 â‹™ no â‹™ rth = l1 â‹™ no â‹™ rth â†’
    l0 â‹™ west = l1 â‹™ west â†’ l0 = l1 :=
  âŸ¨ lift esah_pb wsah_pb Cn Cw hC,
    by rw [lift, â† Functor.assoc, wsah_pb.fac_left, esah_pb.fac_left],
    wsah_pb.fac_right _ _ _,
    by
      intro l0 l1 hnorth hwest
      apply wsah_pb.hom_ext
      Â· apply esah_pb.hom_ext
        Â· exact hnorth
        Â· conv => right; rw [Functor.assoc, wsah, â† Functor.assoc]
          conv => left; rw [Functor.assoc, wsah, â† Functor.assoc, hwest]
      Â· exact hwest
  âŸ©

end horiz

/--
Pullback pasting =>.
The outer square is a pullback when the two inner squares
are both pullbacks.
           no           rth
  Algeria -----> Libya ----> Egypt
    |              |          |
  west         sah |          | east
    |              |          |
    V              V          V
  Niger   -----> Chad  ----> Sudan
           so           uth
-/
def horiz : IsPullback (no â‹™ rth) west east (so â‹™ uth) :=
  IsPullback.ofUniversal (no â‹™ rth) west east (so â‹™ uth)
    (outer_comm_sq wsah esah)
    (fun _ _ hC => horiz.universal wsah esah_pb wsah_pb _ _ hC)
    (fun _ _ hC => horiz.universal wsah esah_pb wsah_pb _ _ hC)

end horiz

section ofRight

/-
           no           rth
  Algeria -----> Libya ----> Egypt
    |              |          |
  west         sah |          | east
    |              |          |
    V              V          V
  Niger   -----> Chad  ----> Sudan
           so           uth
-/
variable (esah_pb : IsPullback rth sah east uth)
  (outer_pb : IsPullback (no â‹™ rth) west east (so â‹™ uth))

namespace ofRight

  variable {C : Type u} [Category.{v} C] (Cn : C â¥¤ Libya) (Cw : C â¥¤ Niger)
  (hC : Cn â‹™ sah = Cw â‹™ so)

  def hCLeft : (Cn â‹™ rth) â‹™ east = Cw â‹™ so â‹™ uth :=
    by calc
      (Cn â‹™ rth) â‹™ east = Cn â‹™ (sah â‹™ uth) := by rw [Functor.assoc, esah]
      _ = (Cn â‹™ sah) â‹™ uth := by rw[â† Functor.assoc]
      _ = (Cw â‹™ so) â‹™ uth := by rw [hC]
      _ = Cw â‹™ so â‹™ uth := by rw [Functor.assoc]

  def lift : C â¥¤ Algeria :=
    outer_pb.lift (Cn â‹™ rth) Cw (hCLeft esah Cn Cw hC)

  def universal : (lift : C â¥¤ Algeria) Ã—'
    lift â‹™ no = Cn âˆ§ lift â‹™ west = Cw âˆ§
    âˆ€ {l0 l1 : C â¥¤ Algeria}, l0 â‹™ no = l1 â‹™ no â†’ l0 â‹™ west = l1 â‹™ west â†’ l0 = l1 :=
  âŸ¨ lift esah_pb.comm_sq outer_pb Cn Cw hC,
  by constructor
     . apply esah_pb.hom_ext
       . exact outer_pb.fac_left _ _ _
       . rw [Functor.assoc, wsah, â† Functor.assoc, hC]
         rw! [outer_pb.fac_right (Cnâ‹™rth) Cw (hCLeft esah Cn Cw hC)]
     . constructor
       . exact outer_pb.fac_right (Cnâ‹™rth) Cw (hCLeft esah Cn Cw hC)
       . intro l0 l1 hln hlw
         apply outer_pb.hom_ext
         . rw[â† Functor.assoc, â† Functor.assoc, hln]
         . exact hlw
    âŸ©

end ofRight

/--
Pullback pasting <=.
The left square is a pullback when the right and outer squares
are both pullbacks.
           no           rth
  Algeria -----> Libya ----> Egypt
    |              |          |
  west         sah |          | east
    |              |          |
    V              V          V
  Niger   -----> Chad  ----> Sudan
           so           uth
-/
def ofRight : IsPullback no west sah so :=
  IsPullback.ofUniversal no west sah so
  wsah
  (fun Cn Cw hC => ofRight.universal wsah esah esah_pb outer_pb Cn Cw hC)
  (fun Cn Cw hC => ofRight.universal wsah esah esah_pb outer_pb Cn Cw hC)



end ofRight

end no_rth

section north
/-
               north
    âŒ-------------------------Â¬
    |                  rth    V
  Algeria        Libya ----> Egypt
    |              |          |
  west         sah |          | east
    |              |          |
    V              V          V
  Niger   -----> Chad  ----> Sudan
           so           uth
-/
variable {north : Algeria â¥¤ Egypt} {rth : Libya â¥¤ Egypt}
  {west : Algeria â¥¤ Niger} {sah : Libya â¥¤ Chad} {east : Egypt â¥¤ Sudan}
  {so : Niger â¥¤ Chad} {uth : Chad â¥¤ Sudan}
  (outer : north â‹™ east = west â‹™ so â‹™ uth) (esah : rth â‹™ east = sah â‹™ uth)

section ofRight'

variable (esah_pb : IsPullback rth sah east uth)
  (outer_pb : IsPullback north west east (so â‹™ uth))

namespace ofRight'

variable {C : Type u} [Category.{v} C] (Cn : C â¥¤ Libya) (Cw : C â¥¤ Niger)
  (hC : Cn â‹™ sah = Cw â‹™ so)

def universal : (lift : C â¥¤ Algeria) Ã—'
  lift â‹™ esah_pb.lift north (west â‹™ so) outer = Cn âˆ§
  lift â‹™ west = Cw âˆ§
  âˆ€ {l0 l1 : C â¥¤ Algeria},
    l0 â‹™ esah_pb.lift north (west â‹™ so) outer = l1 â‹™ esah_pb.lift north (west â‹™ so) outer â†’
      l0 â‹™ west = l1 â‹™ west â†’ l0 = l1 :=
  âŸ¨ lift outer_pb (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC),
  by constructor
     . apply esah_pb.hom_ext
       . calc
          (outer_pb.lift (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC) â‹™ esah_pb.lift north (west â‹™ so) outer) â‹™ rth =
            outer_pb.lift (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC) â‹™ esah_pb.lift north (west â‹™ so) outer â‹™ rth := by rw[Functor.assoc]
          _ = outer_pb.lift (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC) â‹™ north := by rw[esah_pb.fac_left north (west â‹™ so) outer]
          _ = Cn â‹™ rth := by rw[outer_pb.fac_left (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC)]
       . calc
          (outer_pb.lift (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC) â‹™ esah_pb.lift north (west â‹™ so) outer) â‹™ sah =
            outer_pb.lift (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC) â‹™ esah_pb.lift north (west â‹™ so) outer â‹™ sah := by rw[Functor.assoc]
          _ = outer_pb.lift (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC) â‹™ (west â‹™ so) := by rw[esah_pb.fac_right north (west â‹™ so) outer]
          _ = Cw â‹™ so := by rw[â† Functor.assoc, outer_pb.fac_right (Cn â‹™ rth) Cw (ofRight.hCLeft esah Cn Cw hC)]
          _ = Cn â‹™ sah := by rw[hC]
     . constructor
       . exact outer_pb.fac_right (Cnâ‹™rth) Cw (ofRight.hCLeft esah Cn Cw hC)
       . intro l0 l1 hll hlw
         apply outer_pb.hom_ext
         . calc
            l0 â‹™ north = l0 â‹™ (esah_pb.lift north (west â‹™ so) outer â‹™ rth) :=
              by rw [esah_pb.fac_left north (west â‹™ so) outer ]
            _ = (l0 â‹™ esah_pb.lift north (west â‹™ so) outer) â‹™ rth := by rw [Functor.assoc]
            _ = (l1 â‹™ esah_pb.lift north (west â‹™ so) outer) â‹™ rth := by rw [hll]
            _ = l1 â‹™ north := by rw [Functor.assoc, esah_pb.fac_left north (west â‹™ so) outer]
         . exact hlw
    âŸ©

end ofRight'

/--
Pullback pasting <=,
where the map `Algeria â¥¤ Libya` is generated by the universal property of the right square
and the top map `north : Algberia â¥¤ Egypt`.
The left square is a pullback when the right and outer squares
are both pullbacks.
               north
    âŒ-------------------------Â¬
    |                  rth    V
  Algeria        Libya ----> Egypt
    |              |          |
  west         sah |          | east
    |              |          |
    V              V          V
  Niger   -----> Chad  ----> Sudan
           so           uth
-/
def ofRight' {north : Algeria â¥¤ Egypt} {rth : Libya â¥¤ Egypt}
  {west : Algeria â¥¤ Niger} {sah : Libya â¥¤ Chad} {east : Egypt â¥¤ Sudan}
  {so : Niger â¥¤ Chad} {uth : Chad â¥¤ Sudan}
  (outer : north â‹™ east = west â‹™ so â‹™ uth)
  (outer_pb : IsPullback north west east (so â‹™ uth))
  (esah : rth â‹™ east = sah â‹™ uth)
  (esah_pb : IsPullback rth sah east uth) :
  IsPullback (esah_pb.lift north (west â‹™ so) outer) west sah so :=
  IsPullback.ofUniversal (esah_pb.lift north (west â‹™ so) outer) west sah so
  (esah_pb.fac_right _ _ _)
  (fun Cn Cw hC => ofRight'.universal outer esah esah_pb outer_pb Cn Cw hC)
  (fun Cn Cw hC => ofRight'.universal outer esah esah_pb outer_pb Cn Cw hC)



end ofRight'
end north

end Paste


end IsPullback

end Functor
end CategoryTheory

namespace CategoryTheory.Cat

open Functor Limits

section
variable {Libya Egypt Chad Sudan : Type u} [Category.{v} Libya]
  [Category.{v} Egypt] [Category.{v} Chad] [Category.{v} Sudan]
  {north : Libya â¥¤ Egypt} {west : Libya â¥¤ Chad}
  {east : Egypt â¥¤ Sudan} {south : Chad â¥¤ Sudan}
  {comm_sq : north â‹™ east = west â‹™ south}
  (h : Functor.IsPullback north west east south)
  (s : Limits.PullbackCone (homOf east) (homOf south))

def lift : s.pt âŸ¶ of Libya := h.lift s.fst s.snd s.condition

def fac_left : lift h s â‰« (homOf north) = s.fst :=
  h.fac_left _ _ _

def fac_right : lift h s â‰« (homOf west) = s.snd :=
  h.fac_right _ _ _

def uniq (m : s.pt âŸ¶ of Libya) (hl : m â‰« homOf north = s.fst)
    (hr : m â‰« homOf west = s.snd) : m = lift h s := by
  apply h.hom_ext
  Â· convert (fac_left h s).symm
  Â· convert (fac_right h s).symm

variable (comm_sq) in
def isPullback : IsPullback (homOf north) (homOf west) (homOf east)
    (homOf south) :=
  IsPullback.of_isLimit (PullbackCone.IsLimit.mk
    comm_sq (lift h) (fac_left _) (fac_right _) (uniq _))

end

end CategoryTheory.Cat
