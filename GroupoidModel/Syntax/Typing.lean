import GroupoidModel.Syntax.Autosubst

/-! ## Typing rules

In this file we specify typing judgments of the type theory
as `Prop`-valued relations. -/

section Notation -- TODO make notation local

declare_syntax_cat judgment
syntax:50 term:51 : judgment
syntax:50 term:51 " â‰¡ " term:51 : judgment
syntax:50 term:51 " : " term:51 : judgment
syntax:50 term:51 " â‰¡ " term:51 " : " term:51 : judgment

end Notation

/-- The maximum `l` for which `Î“ âŠ¢[l] ğ’¥` makes sense.
When set to `0`, types cannot be quantified over at all. -/
-- TODO: this should be a parameter,
-- but adding an `optParam` to all judgments is super noisy.
-- If only we had parameterized modules..
def univMax : Nat := 4

/-- An *axiom environment* is a (possibly infinite) set
of closed term constants indexed by a type of names `Ï‡`.
`Ï‡` is in general larger than necessary
and not all names correspond to constants.
We record the universe level and type of each constant.

We do not support type constants directly:
they are just term constants in a universe.
This does mean we cannot have type constants at level `univMax`.

We do *not* use `Axioms` for definitions;
the native Lean `Environment` is used instead. -/
abbrev Axioms (Ï‡ : Type*) := Ï‡ â†’ Option { Al : Expr Ï‡ Ã— Nat // Al.1.isClosed âˆ§ Al.2 â‰¤ univMax }

/-- A typing context consisting of type expressions and their universe levels. -/
abbrev Ctx (Ï‡ : Type*) := List (Expr Ï‡ Ã— Nat)

variable {Ï‡ : Type*} (E : Axioms Ï‡)

/-- `Lookup Î“ i A l` means that `A = A'[â†‘â±âºÂ¹]` where `Î“[i] = (A', l)`.
Together with `âŠ¢ Î“`, this implies `Î“ âŠ¢[l] .bvar i : A`. -/
inductive Lookup : Ctx Ï‡ â†’ Nat â†’ Expr Ï‡ â†’ Nat â†’ Prop where
  | zero (Î“ A l) : Lookup ((A,l) :: Î“) 0 (A.subst Expr.wk) l
  | succ {Î“ A i l} (B l') : Lookup Î“ i A l â†’ Lookup ((B,l') :: Î“) (i+1) (A.subst Expr.wk) l

/-- Judgment syntax not parameterized by an environment.
Used locally to define typing rules without repeating `E âˆ£ Î“`. -/
local syntax:25 term:51 " âŠ¢[" term:51 "] " judgment:50 : term

set_option hygiene false in
macro_rules
  | `($Î“ âŠ¢[$l:term] $A:term) => `(WfTp $Î“ $l $A)
  | `($Î“ âŠ¢[$l:term] $A:term â‰¡ $B:term) => `(EqTp $Î“ $l $A $B)
  | `($Î“ âŠ¢[$l:term] $t:term : $A:term) => `(WfTm $Î“ $l $A $t)
  | `($Î“ âŠ¢[$l:term] $t:term â‰¡ $u:term : $A:term) => `(EqTm $Î“ $l $A $t $u)

mutual
/-- All types in the given context are well-formed. -/
inductive WfCtx : Ctx Ï‡ â†’ Prop
  | nil : WfCtx []
  | snoc {Î“ A l} :
    WfCtx Î“ â†’
    Î“ âŠ¢[l] A â†’
    WfCtx ((A,l) :: Î“)

/-- The type is well-formed at the specified universe level.

Many of the inference rules have redundant premises ("presuppositions");
these rules are postfixed with a prime (').
This makes it easier to push syntactic metatheory through.
After proving inversion lemmas,
we define more efficient rules with fewer premises,
named the same but without the prime.
This is not just for usability:
it also means the Lean kernel is checking smaller derivation trees.

Convention on order of implicit parameters:
contexts, types, terms, de Bruijn indices, universe levels. -/
inductive WfTp : Ctx Ï‡ â†’ Nat â†’ Expr Ï‡ â†’ Prop
  -- Type formers
  | pi' {Î“ A B l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] .pi l l' A B

  | sigma' {Î“ A B l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] .sigma l l' A B

  | Id' {Î“ A t u l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l] u : A â†’
    Î“ âŠ¢[l] .Id l A t u

  | univ {Î“ l} :
    WfCtx Î“ â†’
    l < univMax â†’
    Î“ âŠ¢[l+1] .univ l

  | el {Î“ A l} :
    Î“ âŠ¢[l+1] A : .univ l â†’
    Î“ âŠ¢[l] .el A

/-- The two types are equal at the specified universe level. -/
inductive EqTp : Ctx Ï‡ â†’ Nat â†’ Expr Ï‡ â†’ Expr Ï‡ â†’ Prop
  -- Congruences
  | cong_pi' {Î“ A A' B B' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A' â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] .pi l l' A B â‰¡ .pi l l' A' B'

  | cong_sigma' {Î“ A A' B B' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A' â†’
    Î“ âŠ¢[l] A â‰¡ A'â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] .sigma l l' A B â‰¡ .sigma l l' A' B'

  | cong_Id {Î“ A A' t t' u u' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] u â‰¡ u' : A â†’
    Î“ âŠ¢[l] .Id l A t u â‰¡ .Id l A' t' u'

  | cong_el {Î“ A A' l} :
    Î“ âŠ¢[l+1] A â‰¡ A' : .univ l â†’
    Î“ âŠ¢[l] .el A â‰¡ .el A'

  -- Reductions
  | el_code {Î“ A l} :
    l < univMax â†’
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] .el (.code A) â‰¡ A

  -- Reflexive-symmetric-transitive closure
  | refl_tp {Î“ A l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A â‰¡ A

  | symm_tp {Î“ A A' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] A' â‰¡ A

  | trans_tp {Î“ A A' A'' l} :
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] A' â‰¡ A'' â†’
    Î“ âŠ¢[l] A â‰¡ A''

/-- The term has the specified type at the specified universe level.

Note: the type is the _first_ `Expr` argument. -/
inductive WfTm : Ctx Ï‡ â†’ Nat â†’ Expr Ï‡ â†’ Expr Ï‡ â†’ Prop
  -- Term formers
  | ax {Î“ c Al} :
    WfCtx Î“ â†’
    E c = some Al â†’
    Î“ âŠ¢[Al.val.2] Al.val.1 â†’
    Î“ âŠ¢[Al.val.2] .ax c Al.val.1 : Al.val.1

  | bvar {Î“ A i l} :
    WfCtx Î“ â†’
    Lookup Î“ i A l â†’
    Î“ âŠ¢[l] .bvar i : A

  | lam' {Î“ A B t l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] t : B â†’
    Î“ âŠ¢[max l l'] .lam l l' A t : .pi l l' A B

  | app' {Î“ A B f a l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] f : .pi l l' A B â†’
    Î“ âŠ¢[l] a : A â†’
    Î“ âŠ¢[l'] .app l l' B f a : B.subst a.toSb

  | pair' {Î“ A B t u l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l'] u : B.subst t.toSb â†’
    Î“ âŠ¢[max l l'] .pair l l' B t u : .sigma l l' A B

  | fst' {Î“ A B p l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] p : .sigma l l' A B â†’
    Î“ âŠ¢[l] .fst l l' A B p : A

  | snd' {Î“ A B p l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] p : .sigma l l' A B â†’
    Î“ âŠ¢[l'] .snd l l' A B p : B.subst (Expr.fst l l' A B p).toSb

  | refl' {Î“ A t l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l] .refl l t : .Id l A t t

  | idRec' {Î“ A M t r u h l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t : A â†’
    (.Id l (A.subst Expr.wk) (t.subst Expr.wk) (.bvar 0), l) :: (A, l) :: Î“ âŠ¢[l'] M â†’
    Î“ âŠ¢[l'] r : M.subst (.snoc t.toSb <| .refl l t) â†’
    Î“ âŠ¢[l] u : A â†’
    Î“ âŠ¢[l] h : .Id l A t u â†’
    Î“ âŠ¢[l'] .idRec l l' t M r u h : M.subst (.snoc u.toSb h)

  | code {Î“ A l} :
    l < univMax â†’
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l+1] .code A : .univ l

  -- Conversion
  | conv {Î“ A A' t l} :
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] t : A'

/-- The two terms are equal at the specified type and universe level.

Note: the type is the _first_ `Expr` argument in order to make `gcongr` work.
We still pretty-print it last following convention. -/
inductive EqTm : Ctx Ï‡ â†’ Nat â†’ Expr Ï‡ â†’ Expr Ï‡ â†’ Expr Ï‡ â†’ Prop
  -- Congruences
  | cong_lam' {Î“ A A' B t t' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A' â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] t â‰¡ t' : B â†’
    Î“ âŠ¢[max l l'] .lam l l' A t â‰¡ .lam l l' A' t' : .pi l l' A B

  | cong_app' {Î“ A B B' f f' a a' l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] f â‰¡ f' : .pi l l' A B â†’
    Î“ âŠ¢[l] a â‰¡ a' : A â†’
    Î“ âŠ¢[l'] .app l l' B f a â‰¡ .app l l' B' f' a' : B.subst a.toSb

  | cong_pair' {Î“ A B B' t t' u u' l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l'] u â‰¡ u' : B.subst t.toSb â†’
    Î“ âŠ¢[max l l'] .pair l l' B t u â‰¡ .pair l l' B' t' u' : .sigma l l' A B

  | cong_fst' {Î“ A A' B B' p p' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] p â‰¡ p' : .sigma l l' A B â†’
    Î“ âŠ¢[l] .fst l l' A B p â‰¡ .fst l l' A' B' p' : A

  | cong_snd' {Î“ A A' B B' p p' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    (A,l) :: Î“ âŠ¢[l'] B â‰¡ B' â†’
    Î“ âŠ¢[max l l'] p â‰¡ p' : .sigma l l' A B â†’
    Î“ âŠ¢[l'] .snd l l' A B p â‰¡ .snd l l' A' B' p' : B.subst (Expr.fst l l' A B p).toSb

  | cong_refl' {Î“ A t t' l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] .refl l t â‰¡ .refl l t' : .Id l A t t

  | cong_idRec' {Î“ A M M' t t' r r' u u' h h' l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    (.Id l (A.subst Expr.wk) (t.subst Expr.wk) (.bvar 0), l) :: (A, l) :: Î“ âŠ¢[l'] M â‰¡ M' â†’
    Î“ âŠ¢[l'] r â‰¡ r' : M.subst (.snoc t.toSb <| .refl l t) â†’
    Î“ âŠ¢[l] u â‰¡ u' : A â†’
    Î“ âŠ¢[l] h â‰¡ h' : .Id l A t u â†’
    Î“ âŠ¢[l'] .idRec l l' t M r u h â‰¡ .idRec l l' t' M' r' u' h' : M.subst (.snoc u.toSb h)

  | cong_code {Î“ A A' l} :
    l < univMax â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l+1] .code A â‰¡ .code A' : .univ l

  -- Reductions
  | app_lam' {Î“ A B t u l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    (A,l) :: Î“ âŠ¢[l'] t : B â†’
    Î“ âŠ¢[l] u : A â†’
    Î“ âŠ¢[l'] .app l l' B (.lam l l' A t) u â‰¡ t.subst u.toSb : B.subst u.toSb

  | fst_pair' {Î“} {A B t u} {l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l'] u : B.subst t.toSb â†’
    Î“ âŠ¢[l] .fst l l' A B (.pair l l' B t u) â‰¡ t : A

  | snd_pair' {Î“} {A B t u} {l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l'] u : B.subst t.toSb â†’
    Î“ âŠ¢[l'] .snd l l' A B (.pair l l' B t u) â‰¡ u : B.subst t.toSb

  | idRec_refl' {Î“ A M t r l l'} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t : A â†’
    (.Id l (A.subst Expr.wk) (t.subst Expr.wk) (.bvar 0), l) :: (A, l) :: Î“ âŠ¢[l'] M â†’
    Î“ âŠ¢[l'] r : M.subst (.snoc t.toSb <| .refl l t) â†’
    Î“ âŠ¢[l'] .idRec l l' t M r t (.refl l t) â‰¡ r : M.subst (.snoc t.toSb <| .refl l t)

  -- Expansions
  | lam_app' {Î“ A B f l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] f : .pi l l' A B â†’
    Î“ âŠ¢[max l l'] f â‰¡
      .lam l l' A (.app l l' (B.subst (Expr.up Expr.wk)) (f.subst Expr.wk) (.bvar 0)) :
      .pi l l' A B

  | pair_fst_snd' {Î“ A B p l l'} :
    Î“ âŠ¢[l] A â†’
    (A,l) :: Î“ âŠ¢[l'] B â†’
    Î“ âŠ¢[max l l'] p : .sigma l l' A B â†’
    Î“ âŠ¢[max l l'] p â‰¡ .pair l l' B (.fst l l' A B p) (.snd l l' A B p) : .sigma l l' A B

  | code_el {Î“ a l} :
    Î“ âŠ¢[l+1] a : .univ l â†’
    Î“ âŠ¢[l+1] a â‰¡ .code (.el a) : .univ l

  -- Conversion
  | conv_eq {Î“ A A' t t' l} :
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] A â‰¡ A' â†’
    Î“ âŠ¢[l] t â‰¡ t' : A'

  -- Reflexive-symmetric-transitive closure
  | refl_tm {Î“ A t l} :
    Î“ âŠ¢[l] t : A â†’
    Î“ âŠ¢[l] t â‰¡ t : A

  | symm_tm' {Î“ A t t' l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t' â‰¡ t : A

  | trans_tm' {Î“ A t t' t'' l} :
    Î“ âŠ¢[l] A â†’
    Î“ âŠ¢[l] t â‰¡ t' : A â†’
    Î“ âŠ¢[l] t' â‰¡ t'' : A â†’
    Î“ âŠ¢[l] t â‰¡ t'' : A
end

/-! ## Pretty-printers -/

-- FIXME: hovering over this syntax doesn't show docstrings for `WfTp` et al.
syntax:25 term:51 " âˆ£ " term:51 " âŠ¢[" term:51 "] " judgment:50 : term

macro_rules
  | `($E âˆ£ $Î“ âŠ¢[$l:term] $A:term) => ``(WfTp $E $Î“ $l $A)
  | `($E âˆ£ $Î“ âŠ¢[$l:term] $A:term â‰¡ $B:term) => ``(EqTp $E $Î“ $l $A $B)
  | `($E âˆ£ $Î“ âŠ¢[$l:term] $t:term : $A:term) => ``(WfTm $E $Î“ $l $A $t)
  | `($E âˆ£ $Î“ âŠ¢[$l:term] $t:term â‰¡ $u:term : $A:term) => ``(EqTm $E $Î“ $l $A $t $u)

section PrettyPrinting
open Lean PrettyPrinter

@[app_unexpander WfTp]
def WfTp.unexpand : Unexpander
  | `($_ $E $Î“ $l $A) => `($E âˆ£ $Î“ âŠ¢[$l] $A:term)
  | _ => throw ()

@[app_unexpander EqTp]
def EqTp.unexpand : Unexpander
  | `($_ $E $Î“ $l $A $A') => `($E âˆ£ $Î“ âŠ¢[$l] $A:term â‰¡ $A')
  | _ => throw ()

@[app_unexpander WfTm]
def WfTm.unexpand : Unexpander
  | `($_ $E $Î“ $l $A $t) => `($E âˆ£ $Î“ âŠ¢[$l] $t:term : $A)
  | _ => throw ()

@[app_unexpander EqTm]
def EqTm.unexpand : Unexpander
  | `($_ $E $Î“ $l $A $t $t') => `($E âˆ£ $Î“ âŠ¢[$l] $t:term â‰¡ $t' : $A)
  | _ => throw ()

end PrettyPrinting

/-! ## Well-formed axiom environments -/

/-- The given axiom environment is well-formed.

Unlike contexts that change via substitutions,
there is usually one fixed axiom environment that definitions 'live' over. -/
/- FIXME: Can't make inversion (`E | Î“ âŠ¢[l] ğ’¥ â‡ E.Wf`) true
by making `Axioms.Wf` mutual with typing
(that's not strictly positive),
but we could redefine `E âˆ£ Î“ âŠ¢[l] ğ’¥` to mean `E.Wf âˆ§ E âˆ£ Î“ âŠ¢[l] ğ’¥`.
We'd need to rederive all typing rules for the latter,
and this should be done using custom automation
(do NOT write a million lemmas by hand). -/
abbrev Axioms.Wf (E : Axioms Ï‡) :=
  âˆ€ â¦ƒc pâ¦„, E c = some p â†’ E âˆ£ [] âŠ¢[p.val.2] p.val.1
