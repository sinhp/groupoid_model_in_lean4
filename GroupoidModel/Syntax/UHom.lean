import GroupoidModel.Syntax.NaturalModel
import GroupoidModel.ForMathlib
import Mathlib.CategoryTheory.Limits.Shapes.StrictInitial
import GroupoidModel.ForPoly
import GroupoidModel.Tactic.PrintSorries

/-! Morphisms of natural models, and Russell-universe embeddings. -/

universe v u

noncomputable section

open CategoryTheory Limits Opposite MonoidalCategory

namespace NaturalModel

variable {Ctx : Type u} [SmallCategory Ctx]

macro "by>" s:tacticSeq : term => `(by as_aux_lemma => $s)

structure Hom (M N : NaturalModel Ctx) where
  mapTm : M.Tm âŸ¶ N.Tm
  mapTy : M.Ty âŸ¶ N.Ty
  pb : IsPullback mapTm M.tp N.tp mapTy

def Hom.id (M : NaturalModel Ctx) : Hom M M where
  mapTm := ğŸ™ _
  mapTy := ğŸ™ _
  pb := IsPullback.of_id_fst

def Hom.comp {M N O : NaturalModel Ctx} (Î± : Hom M N) (Î² : Hom N O) : Hom M O where
  mapTm := Î±.mapTm â‰« Î².mapTm
  mapTy := Î±.mapTy â‰« Î².mapTy
  pb := Î±.pb.paste_horiz Î².pb

def Hom.comp_assoc {M N O P : NaturalModel Ctx} (Î± : Hom M N) (Î² : Hom N O) (Î³ : Hom O P) :
    comp (comp Î± Î²) Î³ = comp Î± (comp Î² Î³) := by
  simp [comp]

/-- Morphism into the representable natural transformation `M`
from the pullback of `M` along a type. -/
protected def pullbackHom (M : NaturalModel Ctx) {Î“ : Ctx} (A : y(Î“) âŸ¶ M.Ty) :
    Hom (M.pullback A) M where
  mapTm := M.var A
  mapTy := A
  pb := M.disp_pullback A

/-- Given `M : NaturalModel`, a semantic type `A : y(Î“) âŸ¶ M.Ty`,
and a substitution `Ïƒ : Î” âŸ¶ Î“`, construct a Hom for the substitution `A[Ïƒ]`.
-/
def Hom.subst (M : NaturalModel Ctx)
    {Î“ Î” : Ctx} (A : y(Î“) âŸ¶ M.Ty) (Ïƒ : Î” âŸ¶ Î“) :
    Hom (M.pullback (ym(Ïƒ) â‰« A)) (M.pullback A) :=
  let AÏƒ := ym(Ïƒ) â‰« A
  { mapTm :=
    (M.disp_pullback A).lift (M.var AÏƒ) ym(M.disp AÏƒ â‰« Ïƒ) (by aesop_cat)
    mapTy := ym(Ïƒ)
    pb := by
      convert IsPullback.of_right' (M.disp_pullback AÏƒ) (M.disp_pullback A)
      simp }

def Hom.cartesianNatTrans {M N : NaturalModel Ctx} (h : Hom M N) :
    M.Ptp âŸ¶ N.Ptp :=
  M.uvPolyTp.cartesianNatTrans N.uvPolyTp h.mapTy h.mapTm h.pb.flip

@[simp] def Hom.extIsoExt {M N : NaturalModel Ctx} (h : Hom M N)
    {Î“} (A : y(Î“) âŸ¶ M.Ty) : y(N.ext (A â‰« h.mapTy)) â‰… y(M.ext A) :=
  IsPullback.isoIsPullback N.Tm y(Î“) (N.disp_pullback (A â‰« h.mapTy))
  (IsPullback.paste_horiz (M.disp_pullback A) h.pb)

@[reassoc]
theorem Hom.mk_comp_cartesianNatTrans {M N : NaturalModel Ctx} (h : Hom M N)
    {Î“ X} (A : y(Î“) âŸ¶ M.Ty) (B : y(M.ext A) âŸ¶ X) :
    PtpEquiv.mk M A B â‰« h.cartesianNatTrans.app X =
    PtpEquiv.mk N (A â‰« h.mapTy) ((h.extIsoExt A).hom â‰« B) := by
  simp [PtpEquiv.mk]
  have := UvPoly.Equiv.mk'_comp_cartesianNatTrans_app M.uvPolyTp (P' := N.uvPolyTp)
    A _ _ _ (M.disp_pullback _).flip B h.mapTm h.mapTy h.pb.flip
  refine this.trans ?_
  simp [UvPoly.Equiv.mk']; congr 1
  rw [â† Category.assoc]; congr 1
  generalize_proofs _ h1
  apply h1.hom_ext <;> simp

/- We have a 'nice', specific terminal object in `Ctx`,
and this instance allows use to use it directly
rather than through an isomorphism with `Limits.terminal`.
`ChosenTerminal` would suffice but is not defined in mathlib,
so we use `ChosenFiniteProducts`. -/
variable [CartesianMonoidalCategory Ctx]

-- Should be in mathlib?
def isTerminal_yUnit : IsTerminal y(ğŸ™_ Ctx) :=
  (IsTerminal.ofUnique (ğŸ™_ Ctx)).isTerminalObj yoneda (ğŸ™_ Ctx)

/-- A Russell universe embedding is a hom of natural models `M âŸ¶ N`
such that types in `M` correspond to terms of a universe `U` in `N`.

These don't form a category since `UHom.id M` is essentially `Type : Type` in `M`.

Note this doesn't need to extend `Hom` as none of its fields are used;
it's just convenient to pack up the data. -/
structure UHom (M N : NaturalModel Ctx) extends Hom M N where
  U : y(ğŸ™_ Ctx) âŸ¶ N.Ty
  asTm : M.Ty âŸ¶ N.Tm
  U_pb : IsPullback
            /- m.Ty -/           asTm /- N.Tm -/
    (isTerminal_yUnit.from M.Ty)         N.tp
             /- âŠ¤ -/               U  /- N.Ty -/

def UHom.ofTyIsoExt
    {M N : NaturalModel Ctx}
    (H : Hom M N) {U : y(ğŸ™_ Ctx) âŸ¶ N.Ty} (i : M.Ty â‰… y(N.ext U)) :
    UHom M N where
  __ := H
  U := U
  asTm := i.hom â‰« N.var U
  U_pb := by
    convert IsPullback.of_iso_isPullback (N.disp_pullback _) i
    apply isTerminal_yUnit.hom_ext

def UHom.comp {M N O : NaturalModel Ctx} (Î± : UHom M N) (Î² : UHom N O) : UHom M O where
  __ := Hom.comp Î±.toHom Î².toHom
  U := Î±.U â‰« Î².mapTy
  asTm := Î±.asTm â‰« Î².mapTm
  U_pb := Î±.U_pb.paste_horiz Î².pb

def UHom.comp_assoc {M N O P : NaturalModel Ctx} (Î± : UHom M N) (Î² : UHom N O) (Î³ : UHom O P) :
    comp (comp Î± Î²) Î³ = comp Î± (comp Î² Î³) := by
  simp [comp, Hom.comp]

def UHom.wkU {M N : NaturalModel Ctx} (Î“ : Ctx) (Î± : UHom M N) : y(Î“) âŸ¶ N.Ty :=
  isTerminal_yUnit.from y(Î“) â‰« Î±.U

@[reassoc (attr := simp)]
theorem UHom.comp_wkU {M N : NaturalModel Ctx} {Î” Î“ : Ctx} (Î± : UHom M N) (f : y(Î”) âŸ¶ y(Î“)) :
    f â‰« Î±.wkU Î“ = Î±.wkU Î” := by
  simp [wkU]

/- Sanity check:
construct a `UHom` into a natural model with a Tarski universe. -/
def UHom.ofTarskiU (M : NaturalModel Ctx) (U : y(ğŸ™_ Ctx) âŸ¶ M.Ty) (El : y(M.ext U) âŸ¶ M.Ty) :
    UHom (M.pullback El) M where
  __ := M.pullbackHom El
  U
  asTm := M.var U
  U_pb := (M.disp_pullback U).of_iso
      (Iso.refl _)
      (Iso.refl _)
      (Iso.refl _)
      (Iso.refl _)
      (by simp) (isTerminal_yUnit.hom_ext ..)
      (by simp) (by simp)

/-! ## Universe embeddings -/

variable (Ctx) in
/-- A sequence of Russell universe embeddings. -/
structure UHomSeq [CartesianMonoidalCategory Ctx] where
  /-- Number of embeddings in the sequence,
  or one less than the number of models in the sequence. -/
  length : Nat
  objs (i : Nat) (h : i < length + 1) : NaturalModel Ctx
  homSucc' (i : Nat) (h : i < length) : UHom (objs i <| by omega) (objs (i + 1) <| by omega)

namespace UHomSeq

variable (s : UHomSeq Ctx)

instance : GetElem (UHomSeq Ctx) Nat (NaturalModel Ctx) (fun s i => i < s.length + 1) where
  getElem s i h := s.objs i h

def homSucc (i : Nat) (h : i < s.length := by get_elem_tactic) : UHom s[i] s[i+1] :=
  s.homSucc' i h

/-- Composition of embeddings between `i` and `j` in the chain. -/
def hom (s : UHomSeq Ctx) (i j : Nat) (ij : i < j := by omega)
    (jlen : j < s.length + 1 := by get_elem_tactic) : UHom s[i] s[j] :=
  if h : i + 1 = j then
    h â–¸ s.homSucc i
  else
    (s.homSucc i).comp <| s.hom (i+1) j
termination_by s.length - i

/- It is useful to be able to talk about the underlying sequence of Homs in a UHomSeq.
  For such a sequence, we can loosen the condition i < j to i <= j
  without creating Type in Type.
  This is helpful for defining `s[i] â†’ s[max i j]` for Pi and Sigma below.
-/
def homOfLe (i j : Nat) (ij : i <= j := by omega)
    (jlen : j < s.length + 1 := by get_elem_tactic) : Hom s[i] s[j] :=
  if h : i = j then h â–¸ Hom.id s[i]
  else
    (s.hom i j (by omega) _).toHom

/-- For `i â‰¤ j` and a term `t` at level `j`,
if the type of `t` is lifted from level `i`,
then `t` is a lift of a term at level `i`. -/
def unlift (i j) (ij : i â‰¤ j := by omega) (jlen : j < s.length + 1 := by get_elem_tactic)
    {Î“} (A : Î“ âŸ¶ (s[i]'(ij.trans_lt jlen)).Ty) (t : Î“ âŸ¶ s[j].Tm)
    (eq : t â‰« s[j].tp = A â‰« (s.homOfLe i j).mapTy) :
    Î“ âŸ¶ (s[i]'(ij.trans_lt jlen)).Tm :=
  (s.homOfLe i j).pb.lift t A eq

@[simp]
theorem unlift_tp {i j ij jlen Î“ A}
    {t : y(Î“) âŸ¶ _} (eq : t â‰« s[j].tp = A â‰« (s.homOfLe i j).mapTy) :
    s.unlift i j ij jlen A t eq â‰« (s[i]'(ij.trans_lt jlen)).tp = A := by
  simp [unlift]

@[simp]
theorem unlift_lift {i j ij jlen Î“ A}
    {t : y(Î“) âŸ¶ _} (eq : t â‰« s[j].tp = A â‰« (s.homOfLe i j).mapTy) :
    s.unlift i j ij jlen A t eq â‰« (s.homOfLe i j).mapTm = t := by
  simp [unlift]

def unliftVar (i j) (ij : i â‰¤ j := by omega) (jlen : j < s.length + 1 := by get_elem_tactic)
    {Î“} (A : y(Î“) âŸ¶ (s[i]'(ij.trans_lt jlen)).Ty)
    {A' : y(Î“) âŸ¶ s[j].Ty} (eq : A â‰« (s.homOfLe i j).mapTy = A') :
    y(s[j].ext A') âŸ¶ (s[i]'(ij.trans_lt jlen)).Tm :=
  s.unlift i j ij jlen (ym(s[j].disp _) â‰« A) (s[j].var _) (by simp [eq])

@[simp]
theorem unliftVar_tp {i j ij jlen Î“ A} {A' : y(Î“) âŸ¶ s[j].Ty}
    (eq : A â‰« (s.homOfLe i j).mapTy = A') :
    s.unliftVar i j ij jlen A eq â‰« (s[i]'(ij.trans_lt jlen)).tp = ym(s[j].disp _) â‰« A := by
  simp [unliftVar]

theorem substCons_unliftVar {i j ij jlen Î“ A} {A' : y(Î“) âŸ¶ s[j].Ty}
    {eq : A â‰« (s.homOfLe i j).mapTy = A'}
    {Î”} {Ïƒ : Î” âŸ¶ Î“} {t : y(Î”) âŸ¶ _}
    (t_tp : t â‰« (s[i]'(ij.trans_lt jlen)).tp = ym(Ïƒ) â‰« A) :
    ym(s[j].substCons Ïƒ A' (t â‰« (s.homOfLe i j ij jlen).mapTm) (by
      simp [*]
      conv_lhs => rhs; apply (s.homOfLe i j).pb.w
      subst A'; rw [â† Category.assoc, â† Category.assoc, â† t_tp]))
    â‰« s.unliftVar i j ij jlen A eq = t := by
  simp [unlift, unliftVar]; apply (s.homOfLe i j).pb.hom_ext <;> simp [*]

/--
If `s` is a sequence of universe homomorphisms then for `i â‰¤ j` we get a polynomial endofunctor
natural transformation `s[i].Ptp âŸ¶ s[j].Ptp`.
-/
def cartesianNatTrans (i j : Nat)
    (ij : i â‰¤ j := by get_elem_tactic) (jlen : j < s.length + 1 := by get_elem_tactic) :
    s[i].Ptp âŸ¶ s[j].Ptp :=
  (s.homOfLe i j).cartesianNatTrans

@[reassoc]
theorem mk_comp_cartesianNatTrans {i j ij jlen} {Î“ X} (A : y(Î“) âŸ¶ s[i].Ty)
    (B : y(s[i].ext A) âŸ¶ X) :
    PtpEquiv.mk s[i] A B â‰« (s.cartesianNatTrans i j).app X =
    PtpEquiv.mk s[j] (A â‰« (s.homOfLe i j).mapTy)
    (ym(substCons _ (s[j].disp _) _ (s.unliftVar i j ij jlen A rfl) (by simp)) â‰« B) := by
  convert Hom.mk_comp_cartesianNatTrans _ _ _
  apply (IsPullback.paste_horiz (s[i].disp_pullback _) (s.homOfLe i j).pb).hom_ext
  Â· simp [unliftVar]
  Â· simp

theorem cartesianNatTrans_fstProj {i j ij jlen X} :
    (s.cartesianNatTrans i j ij jlen).app X â‰« s[j].uvPolyTp.fstProj X =
    s[i].uvPolyTp.fstProj X â‰« (s.homOfLe i j ij jlen).mapTy := by
  unfold cartesianNatTrans
  apply UvPoly.cartesianNatTrans_fstProj

/--
This is one side of the commutative square
```
s[i0].Ptp.obj s[j0].Tm âŸ¶ s[i1].Ptp.obj s[j1].Tm
  |                           |
  |                           |
  |                           |
  |                           |
  V                           V
s[i0].Ptp.obj s[j0].Tm âŸ¶ s[i1].Ptp.obj s[j0].Tm
```
Given `i0 â‰¤ i1` and `j0 â‰¤ j1`
-/
def cartesianNatTransTm (i0 i1 j0 j1 : Nat)
    (ii : i0 â‰¤ i1 := by get_elem_tactic) (ilen : i1 < s.length + 1 := by get_elem_tactic)
    (jj : j0 â‰¤ j1 := by get_elem_tactic) (jlen : j1 < s.length + 1 := by get_elem_tactic)
    : s[i0].Ptp.obj s[j0].Tm âŸ¶ s[i1].Ptp.obj s[j1].Tm :=
  (s.cartesianNatTrans i0 i1).app s[j0].Tm â‰«
  s[i1].Ptp.map (s.homOfLe j0 j1).mapTm

theorem mk_comp_cartesianNatTransTm {i0 i1 j0 j1 ii ilen jj jlen}
    {Î“ X} (A : y(Î“) âŸ¶ s[i0].Ty) (B : y(s[i0].ext A) âŸ¶ s[j0].Tm)
    : PtpEquiv.mk s[i0] A B â‰« s.cartesianNatTransTm i0 i1 j0 j1 ii ilen jj jlen =
      PtpEquiv.mk s[i1] (A â‰« (s.homOfLe i0 i1).mapTy)
        (ym(substCons _ (s[i1].disp _) _ (s.unliftVar i0 i1 ii ilen A rfl) (by simp))
          â‰« B â‰« (s.homOfLe j0 j1).mapTm) := by
  simp [cartesianNatTransTm, mk_comp_cartesianNatTrans_assoc, PtpEquiv.mk_map]

theorem cartesianNatTransTm_fstProj {i0 i1 j0 j1 ii ilen jj jlen} :
    s.cartesianNatTransTm i0 i1 j0 j1 ii ilen jj jlen â‰« s[i1].uvPolyTp.fstProj s[j1].Tm =
    s[i0].uvPolyTp.fstProj s[j0].Tm â‰« (s.homOfLe i0 i1).mapTy := by
  simp [cartesianNatTransTm]
  slice_lhs 2 3 => apply UvPoly.map_fstProj
  apply cartesianNatTrans_fstProj

def cartesianNatTransTy (i0 i1 j0 j1 : Nat)
    (i0len : i0 â‰¤ i1 := by get_elem_tactic) (i1len : i1 < s.length + 1 := by get_elem_tactic)
    (j0len : j0 â‰¤ j1 := by get_elem_tactic) (j1len : j1 < s.length + 1 := by get_elem_tactic)
    : s[i0].Ptp.obj s[j0].Ty âŸ¶ s[i1].Ptp.obj s[j1].Ty :=
  (s.cartesianNatTrans i0 i1).app s[j0].Ty â‰«
  s[i1].Ptp.map (s.homOfLe j0 j1).mapTy

theorem mk_comp_cartesianNatTransTy {i0 i1 j0 j1 ii ilen jj jlen}
    {Î“ X} (A : y(Î“) âŸ¶ s[i0].Ty) (B : y(s[i0].ext A) âŸ¶ s[j0].Ty)
    : PtpEquiv.mk s[i0] A B â‰« s.cartesianNatTransTy i0 i1 j0 j1 ii ilen jj jlen =
      PtpEquiv.mk s[i1] (A â‰« (s.homOfLe i0 i1).mapTy)
        (ym(substCons _ (s[i1].disp _) _ (s.unliftVar i0 i1 ii ilen A rfl) (by simp))
          â‰« B â‰« (s.homOfLe j0 j1).mapTy) := by
  simp [cartesianNatTransTy, mk_comp_cartesianNatTrans_assoc, PtpEquiv.mk_map]

theorem cartesianNatTransTy_fstProj {i0 i1 j0 j1 ii ilen jj jlen} :
    s.cartesianNatTransTy i0 i1 j0 j1 ii ilen jj jlen â‰« s[i1].uvPolyTp.fstProj s[j1].Ty =
    s[i0].uvPolyTp.fstProj s[j0].Ty â‰« (s.homOfLe i0 i1).mapTy := by
  simp only [cartesianNatTransTy]
  slice_lhs 2 3 => apply UvPoly.map_fstProj
  apply cartesianNatTrans_fstProj

theorem hom_comp_trans (s : UHomSeq Ctx) (i j k : Nat) (ij : i < j) (jk : j < k)
    (klen : k < s.length + 1) :
    (s.hom i j ij).comp (s.hom j k jk) = s.hom i k (ij.trans jk) := by
  conv_rhs => unfold hom
  conv in s.hom i j _ => unfold hom
  split_ifs
  all_goals try omega
  . rename_i h _
    cases h
    simp
  . rw [UHom.comp_assoc, hom_comp_trans]
termination_by s.length - i

/--
TODO: Consider generalising to just UHom?
Convert a map into the `i`th type classifier into a a term of the
`i+1`th term classifier, that is a term of the `i`th universe.
It is defined by composition with the first projection of the pullback square
               v
     s[i].Ty ----> s[i+1].Tm
     ^    |          |
  A /     |   p.b.   |
   /      |          |
  /       V          V
y(Î“) ---> 1 -----> s[i+1].Ty
              U_i
-/
def code {Î“ : Ctx} {i : Nat} (s : UHomSeq Ctx) (ilen : i < s.length) (A : y(Î“) âŸ¶ s[i].Ty) :
    y(Î“) âŸ¶ s[i+1].Tm :=
  A â‰« (s.homSucc i).asTm

@[simp]
theorem code_tp {Î“ : Ctx} {i : Nat} (s : UHomSeq Ctx) (ilen : i < s.length) (A : y(Î“) âŸ¶ s[i].Ty) :
    s.code ilen A â‰« s[i+1].tp = (s.homSucc i).wkU Î“ := by
  simp [code, (s.homSucc i).U_pb.w, UHom.wkU]

@[reassoc]
theorem comp_code {Î” Î“ : Ctx} {i : Nat} (s : UHomSeq Ctx) (ilen : i < s.length)
    (Ïƒ : y(Î”) âŸ¶ y(Î“)) (A : y(Î“) âŸ¶ s[i].Ty) :
    Ïƒ â‰« s.code ilen A = s.code ilen (Ïƒ â‰« A) := by
  simp [code]

/--
TODO: Consider generalising to just UHom?
Convert a a term of the `i`th universe (it is a `i+1` level term) into
a map into the `i`th type classifier.
It is the unique map into the pullback
             a
y(Î“) -----------------Â¬
â€–  -->          v     V
â€–    s[i].Ty ----> s[i+1].Tm
â€–         |          |
â€–         |   p.b.   |
â€–         |          |
â€–         V          V
y(Î“) ---> 1 -----> s[i+1].Ty
              U_i
-/
def el (s : UHomSeq Ctx) {Î“ : Ctx} {i : Nat} (ilen : i < s.length)
    (a : y(Î“) âŸ¶ s[i+1].Tm) (a_tp : a â‰« s[i+1].tp = (s.homSucc i).wkU Î“) :
    y(Î“) âŸ¶ s[i].Ty :=
  (s.homSucc i).U_pb.lift a (isTerminal_yUnit.from y(Î“)) (by rw [a_tp, UHom.wkU])

@[reassoc]
theorem comp_el (s : UHomSeq Ctx) {Î” Î“ : Ctx} {i : Nat} (ilen : i < s.length)
    (Ïƒ : y(Î”) âŸ¶ y(Î“)) (a : y(Î“) âŸ¶ s[i+1].Tm) (a_tp : a â‰« s[i+1].tp = (s.homSucc i).wkU Î“) :
    Ïƒ â‰« s.el ilen a a_tp = s.el ilen (Ïƒ â‰« a) (by simp [a_tp]) :=
  (s.homSucc i).U_pb.hom_ext (by simp [el]) (by simp)

@[simp]
lemma el_code {Î“ : Ctx} {i : Nat} (s : UHomSeq Ctx) (ilen : i < s.length) (A : y(Î“) âŸ¶ s[i].Ty) :
    el s ilen (code s ilen A) (code_tp _ _ _) = A :=
  (s.homSucc i).U_pb.hom_ext (by simp [el, code]) (by simp)

@[simp]
lemma code_el (s : UHomSeq Ctx) {Î“ : Ctx} {i : Nat} (ilen : i < s.length)
    (a : y(Î“) âŸ¶ s[i+1].Tm) (a_tp : a â‰« s[i+1].tp = (s.homSucc i).wkU Î“) :
    code s ilen (el s ilen a a_tp) = a := by
  simp [code, el]

-- Sadly, we have to spell out `ilen` and `jlen` due to
-- https://leanprover.zulipchat.com/#narrow/stream/270676-lean4/topic/Optional.20implicit.20argument
variable {i j : Nat} (ilen : i < s.length + 1) (jlen : j < s.length + 1)

/-! ## Pi -/

/-- The data of type and term formers at each universe `s[i].tp`.

This data is universe-monomorphic,
but we can use it to construct universe-polymorphic formation
in a model-independent manner.
For example, universe-monomorphic `Pi`
```
Î“ âŠ¢áµ¢ A type  Î“.A âŠ¢áµ¢ B type
--------------------------
Î“ âŠ¢áµ¢ Î A. B type
```
can be extended to
```
Î“ âŠ¢áµ¢ A type  Î“.A âŠ¢â±¼ B type
--------------------------
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ Î A. B type
``` -/
protected class PiSeq (s : UHomSeq Ctx) where
  nmPi (i : Nat) (ilen : i < s.length + 1 := by get_elem_tactic) : NaturalModel.Pi s[i]

section Pi
open PiSeq

variable [s.PiSeq]

def Pi : s[i].Ptp.obj s[j].Ty âŸ¶ s[max i j].Ty :=
  s.cartesianNatTransTy i (max i j) j (max i j) â‰« (nmPi (max i j)).Pi

def lam : s[i].Ptp.obj s[j].Tm âŸ¶ s[max i j].Tm :=
  s.cartesianNatTransTm i (max i j) j (max i j) â‰« (nmPi (max i j)).lam

def Pi_pb :
    IsPullback (s.lam ilen jlen) (s[i].Ptp.map s[j].tp) s[max i j].tp (s.Pi ilen jlen) := by
  have p1 : NatTrans.IsCartesian (s.cartesianNatTrans i (max i j)) := by
   dsimp only [UHomSeq.cartesianNatTrans]
   apply UvPoly.isCartesian_cartesianNatTrans
  let pbB : IsPullback
      (s[max i j].Ptp.map (s.homOfLe j (max i j)).mapTm)
      (s[max i j].Ptp.map s[j].tp)
      (s[max i j].Ptp.map s[max i j].tp)
      (s[max i j].Ptp.map (s.homOfLe j (max i j)).mapTy) :=
    CategoryTheory.UvPoly.preservesPullbacks s[max i j].uvPolyTp _ _ _ _
    (s.homOfLe j (max i j)).pb
  have q := IsPullback.paste_horiz pbB (nmPi (max i j)).Pi_pullback
  apply CategoryTheory.IsPullback.paste_horiz (p1 s[j].tp).flip q

/--
```
Î“ âŠ¢áµ¢ A  Î“.A âŠ¢â±¼ B
-----------------
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ Î A. B
``` -/
def mkPi {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty) : y(Î“) âŸ¶ s[max i j].Ty :=
  PtpEquiv.mk s[i] A B â‰« s.Pi ilen jlen

theorem comp_mkPi {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (ÏƒA) (eq : ym(Ïƒ) â‰« A = ÏƒA)
    (B : y(s[i].ext A) âŸ¶ s[j].Ty) :
    ym(Ïƒ) â‰« s.mkPi ilen jlen A B = s.mkPi ilen jlen ÏƒA (ym(s[i].substWk Ïƒ A _ eq) â‰« B) := by
  simp [mkPi, â† Category.assoc, PtpEquiv.mk_comp_left (eq := eq)]

/--
```
Î“ âŠ¢áµ¢ A  Î“.A âŠ¢â±¼ t : B
-------------------------
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ Î»A. t : Î A. B
``` -/
def mkLam {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (t : y(s[i].ext A) âŸ¶ s[j].Tm) : y(Î“) âŸ¶ s[max i j].Tm :=
  PtpEquiv.mk s[i] A t â‰« s.lam ilen jlen

@[simp]
theorem mkLam_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (t : y(s[i].ext A) âŸ¶ s[j].Tm) (t_tp : t â‰« s[j].tp = B) :
    s.mkLam ilen jlen A t â‰« s[max i j].tp = s.mkPi ilen jlen A B := by
  simp [mkLam, mkPi, (s.Pi_pb ilen jlen).w, PtpEquiv.mk_map_assoc, t_tp]

theorem comp_mkLam {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (ÏƒA) (eq : ym(Ïƒ) â‰« A = ÏƒA) (t : y(s[i].ext A) âŸ¶ s[j].Tm) :
    ym(Ïƒ) â‰« s.mkLam ilen jlen A t = s.mkLam ilen jlen ÏƒA (ym(s[i].substWk Ïƒ A _ eq) â‰« t) := by
  simp [mkLam, â† Category.assoc, PtpEquiv.mk_comp_left (eq := eq)]


/--
```
Î“ âŠ¢áµ¢ A  Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ f : Î A. B
-----------------------------
Î“.A âŠ¢â±¼ unlam f : B
``` -/
def unLam {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B) :
    y(s[i].ext A) âŸ¶ s[j].Tm := by
  let total : y(Î“) âŸ¶ s[i].Ptp.obj s[j].Tm :=
    (s.Pi_pb ilen jlen).lift f (PtpEquiv.mk s[i] A B) f_tp
  refine PtpEquiv.snd s[i] total _ ?_
  have eq : total â‰« s[i].Ptp.map s[j].tp = PtpEquiv.mk s[i] A B :=
    (s.Pi_pb ilen jlen).lift_snd ..
  apply_fun PtpEquiv.fst s[i] at eq
  rw [PtpEquiv.fst_comp_right] at eq
  simpa using eq

@[simp]
theorem unLam_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B) :
    s.unLam ilen jlen A B f f_tp â‰« s[j].tp = B := by
  rw [unLam, â† PtpEquiv.snd_comp_right]
  convert PtpEquiv.snd_mk s[i] A B using 2; simp

theorem comp_unLam {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (ÏƒA) (eq : ym(Ïƒ) â‰« A = ÏƒA) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B) :
    ym(s[i].substWk Ïƒ A _ eq) â‰« s.unLam ilen jlen A B f f_tp =
      s.unLam ilen jlen ÏƒA (ym(s[i].substWk Ïƒ A _ eq) â‰« B)
        (ym(Ïƒ) â‰« f) (by simp [eq, f_tp, comp_mkPi]) := by
  simp [unLam]
  rw [â† PtpEquiv.snd_comp_left]
  simp [PtpEquiv.snd, UvPoly.Equiv.snd'_eq]
  simp only [â† Category.assoc]; congr 2
  apply pullback.hom_ext <;> simp; congr 1
  apply (s.Pi_pb ilen jlen).hom_ext <;> simp
  rw [PtpEquiv.mk_comp_left]

/--
```
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ f : Î A. B  Î“ âŠ¢áµ¢ a : A
---------------------------------
Î“ âŠ¢â±¼ f a : B[id.a]
``` -/
def mkApp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B)
    (a : y(Î“) âŸ¶ s[i].Tm) (a_tp : a â‰« s[i].tp = A) : y(Î“) âŸ¶ s[j].Tm :=
  ym(s[i].sec A a a_tp) â‰« s.unLam ilen jlen A B f f_tp

@[simp]
theorem mkApp_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B)
    (a : y(Î“) âŸ¶ s[i].Tm) (a_tp : a â‰« s[i].tp = A) :
    s.mkApp ilen jlen A B f f_tp a a_tp â‰« s[j].tp = ym(s[i].sec A a a_tp) â‰« B := by
  simp [mkApp]

theorem comp_mkApp {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (ÏƒA) (eq : ym(Ïƒ) â‰« A = ÏƒA) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B)
    (a : y(Î“) âŸ¶ s[i].Tm) (a_tp : a â‰« s[i].tp = A) :
    ym(Ïƒ) â‰« s.mkApp ilen jlen A B f f_tp a a_tp =
      s.mkApp ilen jlen ÏƒA (ym(s[i].substWk Ïƒ A _ eq) â‰« B)
        (ym(Ïƒ) â‰« f) (by simp [f_tp, comp_mkPi (eq := eq)])
        (ym(Ïƒ) â‰« a) (by simp [a_tp, eq]) := by
  unfold mkApp; rw [â† Functor.map_comp_assoc,
    comp_sec (eq := eq), Functor.map_comp_assoc, comp_unLam (eq := eq)]

@[simp]
theorem mkLam_unLam {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B) :
    s.mkLam ilen jlen A (s.unLam ilen jlen A B f f_tp) = f := by
  let total : y(Î“) âŸ¶ s[i].Ptp.obj s[j].Tm :=
    (s.Pi_pb ilen jlen).lift f (PtpEquiv.mk s[i] A B) f_tp
  simp [mkLam, unLam]
  have : PtpEquiv.fst s[i] total = A := by
    simp [total, PtpEquiv.fst, UvPoly.Equiv.fst]
    rw [â† s[i].uvPolyTp.map_fstProj s[j].tp]
    slice_lhs 1 2 => apply (s.Pi_pb ilen jlen).lift_snd
    apply PtpEquiv.fst_mk
  slice_lhs 1 1 => equals total =>
    apply PtpEquiv.ext _ (A := A) (by simp) (by simp [this]) (by simp [total])
  apply (s.Pi_pb ilen jlen).lift_fst

@[simp]
theorem unLam_mkLam {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (t : y(s[i].ext A) âŸ¶ s[j].Tm) (t_tp : t â‰« s[j].tp = B)
    (lam_tp : s.mkLam ilen jlen A t â‰« s[max i j].tp = s.mkPi ilen jlen A B) :
    s.unLam ilen jlen A B (s.mkLam ilen jlen A t) lam_tp = t := by
  simp [mkLam, unLam]
  convert PtpEquiv.snd_mk s[i] A t using 2
  apply (s.Pi_pb ilen jlen).hom_ext <;> simp
  rw [PtpEquiv.mk_comp_right, t_tp]

/--
```
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ f : Î A. B
--------------------------------------
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ Î»A. f[â†‘] vâ‚€ : Î A. B
```
-/
def etaExpand {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B) :
    y(Î“) âŸ¶ s[max i j].Tm :=
  s.mkLam ilen jlen A <|
    s.mkApp ilen jlen
      (ym(s[i].disp A) â‰« A) (ym(s[i].substWk ..) â‰« B) (ym(s[i].disp A) â‰« f)
        (by simp [f_tp, comp_mkPi])
      (s[i].var A) (s[i].var_tp A)

theorem etaExpand_eq {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (f : y(Î“) âŸ¶ s[max i j].Tm) (f_tp : f â‰« s[max i j].tp = s.mkPi ilen jlen A B) :
    s.etaExpand ilen jlen A B f f_tp = f := by
  simp [etaExpand]
  convert s.mkLam_unLam ilen jlen A B f f_tp using 2
  simp [mkApp]; rw [â† comp_unLam (f_tp := f_tp), â† Category.assoc]
  conv_rhs => rw [â† Category.id_comp (s.unLam ..)]
  congr 2
  apply (s[i].disp_pullback A).hom_ext <;> simp
  simp [substWk]

/--
```
Î“ âŠ¢áµ¢ A  Î“.A âŠ¢â±¼ t : B  Î“ âŠ¢áµ¢ a : A
--------------------------------
Î“.A âŠ¢â±¼ (Î»A. t) a â‰¡ t[a] : B[a]
``` -/
@[simp]
theorem mkApp_mkLam {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (t : y(s[i].ext A) âŸ¶ s[j].Tm) (t_tp : t â‰« s[j].tp = B)
    (lam_tp : s.mkLam ilen jlen A t â‰« s[max i j].tp = s.mkPi ilen jlen A B)
    (a : y(Î“) âŸ¶ s[i].Tm) (a_tp : a â‰« s[i].tp = A) :
    s.mkApp ilen jlen A B (s.mkLam ilen jlen A t) lam_tp a a_tp = ym(s[i].sec A a a_tp) â‰« t := by
  rw [mkApp, unLam_mkLam]
  assumption

end Pi

/-! ## Sigma -/

class SigSeq (s : UHomSeq Ctx) where
  nmSig (i : Nat) (ilen : i < s.length + 1 := by get_elem_tactic) : NaturalModel.Sigma s[i]

section Sigma
open SigSeq
variable [s.SigSeq]

def Sig : s[i].Ptp.obj s[j].Ty âŸ¶ s[max i j].Ty :=
  s.cartesianNatTransTy i (max i j) j (max i j) â‰« (nmSig (max i j)).Sig

def pair : UvPoly.compDom s[i].uvPolyTp s[j].uvPolyTp âŸ¶ s[max i j].Tm :=
  let l : s[i].uvPolyTp.compDom s[j].uvPolyTp âŸ¶ s[max i j].uvPolyTp.compDom s[max i j].uvPolyTp :=
    UvPoly.compDomMap
      (s.homOfLe i (max i j)).mapTm
      (s.homOfLe j (max i j)).mapTm
      (s.homOfLe i (max i j)).mapTy
      (s.homOfLe j (max i j)).mapTy
      (s.homOfLe i (max i j)).pb.flip
      (s.homOfLe j (max i j)).pb.flip
  l â‰« (nmSig (max i j)).pair

def Sig_pb : IsPullback
    (s.pair ilen jlen)
  (s[i].uvPolyTp.comp s[j].uvPolyTp).p s[max i j].tp
    (s.Sig ilen jlen) :=
  (UvPoly.compDomMap_isPullback ..).paste_horiz (nmSig (max i j)).Sig_pullback

/--
```
Î“ âŠ¢áµ¢ A  Î“.A âŠ¢â±¼ B
-----------------
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ Î£A. B
``` -/
def mkSig {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty) :
    y(Î“) âŸ¶ s[max i j].Ty :=
  PtpEquiv.mk s[i] A B â‰« s.Sig ilen jlen

theorem comp_mkSig {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty) :
    ym(Ïƒ) â‰« s.mkSig ilen jlen A B =
      s.mkSig ilen jlen (ym(Ïƒ) â‰« A) (ym(s[i].substWk Ïƒ A) â‰« B) := by
  simp [mkSig, â† Category.assoc, PtpEquiv.mk_comp_left]

/--
```
Î“ âŠ¢áµ¢ t : A  Î“ âŠ¢â±¼ u : B[t]
--------------------------
Î“ âŠ¢â‚˜â‚â‚“â‚áµ¢,â±¼â‚ âŸ¨t, uâŸ© : Î£A. B
``` -/
def mkPair {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (t : y(Î“) âŸ¶ s[i].Tm) (t_tp : t â‰« s[i].tp = A)
    (u : y(Î“) âŸ¶ s[j].Tm) (u_tp : u â‰« s[j].tp = ym(s[i].sec A t t_tp) â‰« B) :
    y(Î“) âŸ¶ s[max i j].Tm :=
  compDomEquiv.mk t t_tp B u u_tp â‰« s.pair ilen jlen

theorem comp_mkPair {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (t : y(Î“) âŸ¶ s[i].Tm) (t_tp : t â‰« s[i].tp = A)
    (u : y(Î“) âŸ¶ s[j].Tm) (u_tp : u â‰« s[j].tp = ym(s[i].sec A t t_tp) â‰« B) :
    ym(Ïƒ) â‰« s.mkPair ilen jlen A B t t_tp u u_tp =
      s.mkPair ilen jlen (ym(Ïƒ) â‰« A) (ym(s[i].substWk Ïƒ A) â‰« B)
        (ym(Ïƒ) â‰« t) (by simp [t_tp])
        (ym(Ïƒ) â‰« u) (by simp [u_tp, comp_sec_functor_map_assoc]) := by
  simp only [â† Category.assoc, mkPair]; rw [compDomEquiv.comp_mk]

@[simp]
theorem mkPair_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (t : y(Î“) âŸ¶ s[i].Tm) (t_tp : t â‰« s[i].tp = A)
    (u : y(Î“) âŸ¶ s[j].Tm) (u_tp : u â‰« s[j].tp = ym(s[i].sec A t t_tp) â‰« B) :
    s.mkPair ilen jlen A B t t_tp u u_tp â‰« s[max i j].tp = s.mkSig ilen jlen A B := by
  simp [mkPair, mkSig, (s.Sig_pb ilen jlen).w, compDomEquiv.mk]

def mkFst {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (p : y(Î“) âŸ¶ s[max i j].Tm) (p_tp : p â‰« s[max i j].tp = s.mkSig ilen jlen A B) :
    y(Î“) âŸ¶ s[i].Tm :=
  compDomEquiv.fst ((s.Sig_pb ilen jlen).lift p (PtpEquiv.mk _ A B) p_tp)

@[simp]
theorem mkFst_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (p : y(Î“) âŸ¶ s[max i j].Tm) (p_tp : p â‰« s[max i j].tp = s.mkSig ilen jlen A B) :
    s.mkFst ilen jlen A B p p_tp â‰« s[i].tp = A := by simp [mkFst, compDomEquiv.fst_tp]

@[simp]
theorem mkFst_mkPair {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (t : y(Î“) âŸ¶ s[i].Tm) (t_tp : t â‰« s[i].tp = A)
    (u : y(Î“) âŸ¶ s[j].Tm) (u_tp : u â‰« s[j].tp = ym(s[i].sec A t t_tp) â‰« B) :
    s.mkFst ilen jlen A B (s.mkPair ilen jlen A B t t_tp u u_tp) (by simp) = t := by
  simp [mkFst, mkPair]
  convert compDomEquiv.fst_mk t t_tp B u u_tp using 2
  apply (s.Sig_pb ilen jlen).hom_ext <;> [simp; simp [compDomEquiv.mk]]

theorem comp_mkFst {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (p : y(Î“) âŸ¶ s[max i j].Tm) (p_tp : p â‰« s[max i j].tp = s.mkSig ilen jlen A B) :
    ym(Ïƒ) â‰« s.mkFst ilen jlen A B p p_tp =
      s.mkFst ilen jlen (ym(Ïƒ) â‰« A) (ym(s[i].substWk Ïƒ A) â‰« B) (ym(Ïƒ) â‰« p)
        (by simp [p_tp, comp_mkSig]) := by
  simp [mkFst]
  rw [compDomEquiv.comp_fst]; congr 1
  apply (s.Sig_pb ilen jlen).hom_ext <;> simp
  rw [PtpEquiv.mk_comp_left]

def mkSnd {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (p : y(Î“) âŸ¶ s[max i j].Tm) (p_tp : p â‰« s[max i j].tp = s.mkSig ilen jlen A B) :
    y(Î“) âŸ¶ s[j].Tm :=
  compDomEquiv.snd ((s.Sig_pb ilen jlen).lift p (PtpEquiv.mk _ A B) p_tp)

@[simp]
theorem mkSnd_mkPair {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (t : y(Î“) âŸ¶ s[i].Tm) (t_tp : t â‰« s[i].tp = A)
    (u : y(Î“) âŸ¶ s[j].Tm) (u_tp : u â‰« s[j].tp = ym(s[i].sec A t t_tp) â‰« B) :
    s.mkSnd ilen jlen A B (s.mkPair ilen jlen A B t t_tp u u_tp) (by simp) = u := by
  simp [mkSnd, mkPair]
  convert compDomEquiv.snd_mk t t_tp B u u_tp using 2
  apply (s.Sig_pb ilen jlen).hom_ext <;> [simp; simp [compDomEquiv.mk]]

protected theorem dependent_eq {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (p : y(Î“) âŸ¶ s[max i j].Tm) (p_tp : p â‰« s[max i j].tp = s.mkSig ilen jlen A B) :
    compDomEquiv.dependent ((s.Sig_pb ilen jlen).lift p (PtpEquiv.mk s[i] A B) p_tp) A
      (by simp [compDomEquiv.fst_tp]) = B := by
  simp [compDomEquiv.dependent, -UvPoly.comp_p]
  convert PtpEquiv.snd_mk s[i] A B using 2
  simp

@[simp]
theorem mkSnd_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (p : y(Î“) âŸ¶ s[max i j].Tm) (p_tp : p â‰« s[max i j].tp = s.mkSig ilen jlen A B) :
    s.mkSnd ilen jlen A B p p_tp â‰« s[j].tp =
      ym(s[i].sec A (s.mkFst ilen jlen A B p p_tp) (by simp)) â‰« B := by
  generalize_proofs h
  simp [mkSnd, compDomEquiv.snd_tp (eq := h), s.dependent_eq]; rfl

theorem comp_mkSnd {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (p : y(Î“) âŸ¶ s[max i j].Tm) (p_tp : p â‰« s[max i j].tp = s.mkSig ilen jlen A B) :
    ym(Ïƒ) â‰« s.mkSnd ilen jlen A B p p_tp =
      s.mkSnd ilen jlen (ym(Ïƒ) â‰« A) (ym(s[i].substWk Ïƒ A) â‰« B) (ym(Ïƒ) â‰« p)
        (by simp [p_tp, comp_mkSig]) := by
  simp [mkSnd, compDomEquiv.comp_snd]; congr 1
  apply (s.Sig_pb ilen jlen).hom_ext <;> simp
  rw [PtpEquiv.mk_comp_left]

@[simp]
theorem mkPair_mkFst_mkSnd {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (B : y(s[i].ext A) âŸ¶ s[j].Ty)
    (p : y(Î“) âŸ¶ s[max i j].Tm) (p_tp : p â‰« s[max i j].tp = s.mkSig ilen jlen A B) :
    s.mkPair ilen jlen A B
      (s.mkFst ilen jlen A B p p_tp) (by simp)
      (s.mkSnd ilen jlen A B p p_tp) (by simp) = p := by
  simp [mkFst, mkSnd, mkPair]
  have := compDomEquiv.eta ((s.Sig_pb ilen jlen).lift p (PtpEquiv.mk _ A B) p_tp)
    (eq := by rw [â† mkFst.eq_def, mkFst_tp])
  conv at this => enter [1, 3]; apply s.dependent_eq
  simp [this]

end Sigma

/-! ## Identity types -/

/--
```
Î“ âŠ¢áµ¢ A  Î“ âŠ¢áµ¢ a0, a1 : A
-----------------------
Î“ âŠ¢áµ¢ Id(A, a0, a1)
``` -/
def mkId {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty) (a0 a1 : y(Î“) âŸ¶ s[i].Tm)
    (a0_tp : a0 â‰« s[i].tp = A) (a1_tp : a1 â‰« s[i].tp = A) :
    y(Î“) âŸ¶ s[i].Ty :=
  sorry

theorem comp_mkId {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (ÏƒA) (eq : ym(Ïƒ) â‰« A = ÏƒA)
    (a0 a1 : y(Î“) âŸ¶ s[i].Tm)
    (a0_tp : a0 â‰« s[i].tp = A) (a1_tp : a1 â‰« s[i].tp = A) :
    ym(Ïƒ) â‰« s.mkId ilen A a0 a1 a0_tp a1_tp =
      s.mkId ilen ÏƒA (ym(Ïƒ) â‰« a0) (ym(Ïƒ) â‰« a1)
        (by simp [eq, a0_tp]) (by simp [eq, a1_tp]) := by
  sorry

/--
```
Î“ âŠ¢áµ¢ t : A
-----------------------
Î“ âŠ¢áµ¢ refl(t) : Id(A, t, t)
``` -/
def mkRefl {Î“ : Ctx} (t : y(Î“) âŸ¶ s[i].Tm) : y(Î“) âŸ¶ s[i].Tm :=
  sorry

theorem comp_mkRefl {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (t : y(Î“) âŸ¶ s[i].Tm) :
    ym(Ïƒ) â‰« s.mkRefl ilen t = s.mkRefl ilen (ym(Ïƒ) â‰« t) := by
  sorry

@[simp]
theorem mkRefl_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty)
    (t : y(Î“) âŸ¶ s[i].Tm) (t_tp : t â‰« s[i].tp = A) :
    s.mkRefl ilen t â‰« s[i].tp = s.mkId ilen A t t t_tp t_tp := by
  sorry

/--
```
Î“ âŠ¢áµ¢ t : A
-----------------------
Î“ âŠ¢áµ¢ idRec(t) : Id(A, t, t)
``` -/
def mkIdRec {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty)
    (t : y(Î“) âŸ¶ s[i].Tm) (t_tp : t â‰« s[i].tp = A)
    (B : y(s[i].ext A) âŸ¶ s[i].Ty)
    (B_eq : s.mkId ilen (ym(s[i].disp A) â‰« A)
      (ym(s[i].disp A) â‰« t) (s[i].var A) (by> simp [*]) (var_tp ..) = B)
    (M : y(s[i].ext B) âŸ¶ s[j].Ty)
    (r : y(Î“) âŸ¶ s[j].Tm) (r_tp : r â‰« s[j].tp =
      ym(substCons _ (s[i].sec A t t_tp) _ (s.mkRefl ilen t)
        (by> simp [comp_mkId, t_tp, â† B_eq])) â‰« M)
    (u : y(Î“) âŸ¶ s[i].Tm) (u_tp : u â‰« s[i].tp = A)
    (h : y(Î“) âŸ¶ s[i].Tm) (h_tp : h â‰« s[i].tp = s.mkId ilen A t u t_tp u_tp) :
    y(Î“) âŸ¶ s[j].Tm :=
  sorry

theorem comp_mkIdRec {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (A : y(Î“) âŸ¶ s[i].Ty) (ÏƒA) (ÏƒA_eq : ym(Ïƒ) â‰« A = ÏƒA)
    (t t_tp B B_eq ÏƒB) (ÏƒB_eq : ym(s[i].substWk Ïƒ _ _ ÏƒA_eq) â‰« B = ÏƒB)
    (M) (r : y(Î“) âŸ¶ (s[j]'jlen).Tm) (r_tp u u_tp h h_tp) :
    ym(Ïƒ) â‰« s.mkIdRec ilen jlen A t t_tp B B_eq M r r_tp u u_tp h h_tp =
    s.mkIdRec ilen jlen ÏƒA (ym(Ïƒ) â‰« t) (by> simp [t_tp, â† ÏƒA_eq])
      ÏƒB (by>
        simp [â† ÏƒB_eq, â† B_eq]
        rw [comp_mkId]; congr! 1
        Â· rw [â† Functor.map_comp_assoc, â† Functor.map_comp_assoc, substWk_disp]
        Â· simp
        Â· rw [â† Functor.map_comp_assoc, substWk_disp]; simp [ÏƒA_eq])
      (ym((s[i]'ilen).substWk (s[i].substWk Ïƒ _ _ ÏƒA_eq) _ _ ÏƒB_eq) â‰« M)
      (ym(Ïƒ) â‰« r) (by
        simp [*]
        simp only [â† Functor.map_comp_assoc]; congr! 2
        simp [comp_substCons, comp_sec, substWk, comp_mkRefl])
      (ym(Ïƒ) â‰« u) (by> simp [*])
      (ym(Ïƒ) â‰« h) (by> simp [*, comp_mkId]) := by
  sorry

@[simp]
theorem mkIdRec_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty)
    (t t_tp B B_eq M) (r : y(Î“) âŸ¶ s[j].Tm) (r_tp u u_tp h h_tp) :
    s.mkIdRec ilen jlen A t t_tp B B_eq M r r_tp u u_tp h h_tp â‰« s[j].tp =
      ym(substCons _ (s[i].sec _ u u_tp) _ h (by> simp [h_tp, comp_mkId, â† B_eq])) â‰« M := by
  sorry

@[simp]
theorem mkIdRec_mkRefl {Î“ : Ctx} (A : y(Î“) âŸ¶ s[i].Ty)
    (t t_tp B B_eq M) (r : y(Î“) âŸ¶ s[j].Tm) (r_tp) :
    s.mkIdRec ilen jlen A t t_tp B B_eq M r r_tp t t_tp
      (s.mkRefl ilen t) (s.mkRefl_tp ilen _ t t_tp) = r := by
  sorry
