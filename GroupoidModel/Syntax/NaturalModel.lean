import SEq.Tactic.DepRewrite
import Poly.ForMathlib.CategoryTheory.LocallyCartesianClosed.Presheaf
import Poly.UvPoly.UPFan
import Mathlib.CategoryTheory.Limits.Shapes.KernelPair

import GroupoidModel.ForPoly
import GroupoidModel.ForMathlib.Tactic.CategoryTheory.FunctorMap
import GroupoidModel.ForMathlib.CategoryTheory.Yoneda
import GroupoidModel.ForMathlib.CategoryTheory.RepPullbackCone
import GroupoidModel.ForMathlib.CategoryTheory.WeakPullback

universe v u

noncomputable section

open CategoryTheory Limits Opposite

namespace NaturalModel

/-- A natural model with support for dependent types (and nothing more).
The data is a natural transformation with representable fibers,
stored as a choice of representative for each fiber. -/
structure Universe (Ctx : Type u) [Category Ctx] where
  Tm : Psh Ctx
  Ty : Psh Ctx
  tp : Tm âŸ¶ Ty
  ext {Î“ : Ctx} (A : y(Î“) âŸ¶ Ty) : Ctx
  disp {Î“ : Ctx} (A : y(Î“) âŸ¶ Ty) : ext A âŸ¶ Î“
  var {Î“ : Ctx} (A : y(Î“) âŸ¶ Ty) : y(ext A) âŸ¶ Tm
  disp_pullback {Î“ : Ctx} (A : y(Î“) âŸ¶ Ty) :
    IsPullback (var A) ym(disp A) tp A

namespace Universe

variable {Ctx : Type u} [SmallCategory Ctx] (M : Universe Ctx)

@[simps! hom inv]
def pullbackIsoExt {Î“ : Ctx} (A : y(Î“) âŸ¶ M.Ty) :
    pullback A M.tp â‰… yoneda.obj (M.ext A) :=
  -- The use of `IsPullback.flip` suggests an inconsistency in convention.
  IsPullback.isoPullback (M.disp_pullback A).flip |>.symm

/-! ## Pullback of representable natural transformation -/

/-- Pull a natural model back along a type. -/
protected def pullback {Î“ : Ctx} (A : y(Î“) âŸ¶ M.Ty) : Universe Ctx where
  Tm := y(M.ext A)
  Ty := y(Î“)
  tp := ym(M.disp A)
  ext := fun B => M.ext (B â‰« A)
  disp := fun B => M.disp (B â‰« A)
  var := fun B =>
    (M.disp_pullback A).lift (M.var (B â‰« A)) (ym(M.disp (B â‰« A)) â‰« B) (M.disp_pullback (B â‰« A)).w
  disp_pullback := fun B =>
    IsPullback.of_right' (M.disp_pullback (B â‰« A)) (M.disp_pullback A)

/--
  Given the pullback square on the right,
  with a natural model structure on `tp : Tm âŸ¶ Ty`
  giving the outer pullback square.

  Î“.A -.-.- var -.-,-> E ------ toTm ------> Tm
   |                   |                      |
   |                   |                      |
 M.disp                Ï€                     tp
   |                   |                      |
   V                   V                      V
  Î“ ------- A -------> U ------ toTy ------> Ty

  construct a natural model structure on `Ï€ : E âŸ¶ U`,
  by pullback pasting.
-/
def ofIsPullback {U E : Psh Ctx} {Ï€ : E âŸ¶ U}
    {toTy : U âŸ¶ M.Ty} {toTm : E âŸ¶ M.Tm}
    (pb : IsPullback toTm Ï€ M.tp toTy) :
    Universe Ctx where
  Ty := U
  Tm := E
  tp := Ï€
  ext A := M.ext (A â‰« toTy)
  disp A := M.disp (A â‰« toTy)
  var A := pb.lift
    (M.var (A â‰« toTy))
    (ym(M.disp (A â‰« toTy)) â‰« A)
    (M.disp_pullback (A â‰« toTy)).w
  disp_pullback A :=
    IsPullback.of_right'
      (M.disp_pullback (A â‰« toTy))
      pb

/-! ## Substitutions -/

/--
```
Î” âŠ¢ Ïƒ : Î“  Î“ âŠ¢ A type  Î” âŠ¢ t : A[Ïƒ]
-----------------------------------
Î” âŠ¢ Ïƒ.t : Î“.A
```
 ------ Î” ------ t --------Â¬
 |      â†“ substCons         â†“
 |   M.ext A ---var A---> M.Tm
 |      |                  |
 Ïƒ      |                  |
 |    disp A              M.tp
 |      |                  |
 |      V                  V
  ---> Î“ ------ A -----> M.Ty
-/
def substCons {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty)
    (t : y(Î”) âŸ¶ M.Tm) (t_tp : t â‰« M.tp = ym(Ïƒ) â‰« A) :
    Î” âŸ¶ M.ext A :=
  uy((M.disp_pullback A).lift t ym(Ïƒ) t_tp)

@[functor_map (attr := reassoc (attr := simp))]
theorem substCons_disp {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty) (t : y(Î”) âŸ¶ M.Tm)
    (tTp : t â‰« M.tp = ym(Ïƒ) â‰« A) :
    M.substCons Ïƒ A t tTp â‰« M.disp A = Ïƒ := by
  apply Yoneda.fullyFaithful.map_injective
  simp [substCons]

@[reassoc (attr := simp)]
theorem substCons_var {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty) (t : y(Î”) âŸ¶ M.Tm)
    (aTp : t â‰« M.tp = ym(Ïƒ) â‰« A) :
    ym(M.substCons Ïƒ A t aTp) â‰« M.var A = t := by
  simp [substCons]

@[simp]
theorem comp_substCons {Î˜ Î” Î“ : Ctx} (Ï„ : Î˜ âŸ¶ Î”) (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty) (t : y(Î”) âŸ¶ M.Tm)
    (aTp : t â‰« M.tp = ym(Ïƒ) â‰« A) :
    Ï„ â‰« M.substCons Ïƒ A t aTp = M.substCons (Ï„ â‰« Ïƒ) A (ym(Ï„) â‰« t) (by simp [*]) := by
  apply Yoneda.fullyFaithful.map_injective
  apply (M.disp_pullback A).hom_ext
  Â· simp
  Â· simp

/--
```
Î” âŠ¢ Ïƒ : Î“.A
------------
Î” âŠ¢ â†‘âˆ˜Ïƒ : Î“
```
-/
def substFst {Î” Î“ : Ctx} {A : y(Î“) âŸ¶ M.Ty} (Ïƒ : Î” âŸ¶ M.ext A) : Î” âŸ¶ Î“ :=
  Ïƒ â‰« M.disp A

/--
```
Î” âŠ¢ Ïƒ : Î“.A
-------------------
Î” âŠ¢ vâ‚€[Ïƒ] : A[â†‘âˆ˜Ïƒ]
```
-/
def substSnd {Î” Î“ : Ctx} {A : y(Î“) âŸ¶ M.Ty} (Ïƒ : Î” âŸ¶ M.ext A) : y(Î”) âŸ¶ M.Tm :=
  ym(Ïƒ) â‰« M.var A

theorem substSnd_tp {Î” Î“ : Ctx} {A : y(Î“) âŸ¶ M.Ty} (Ïƒ : Î” âŸ¶ M.ext A) :
    M.substSnd Ïƒ â‰« M.tp = ym(M.substFst Ïƒ) â‰« A := by
  simp [substSnd, substFst]; rw [(M.disp_pullback _).w]

@[reassoc (attr := simp)]
theorem var_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ M.Ty) : M.var A â‰« M.tp = ym(M.disp A) â‰« A := by
  simp [(M.disp_pullback A).w]

/--
Weaken a substitution.
```
Î” âŠ¢ Ïƒ : Î“  Î“ âŠ¢ A type  A' = A[Ïƒ]
------------------------------------
Î”.A' âŠ¢ â†‘â‰«Ïƒ : Î“  Î”.A' âŠ¢ vâ‚€ : A[â†‘â‰«Ïƒ]
------------------------------------
Î”.A' âŠ¢ (â†‘â‰«Ïƒ).vâ‚€ : Î“.A
```
-/
def substWk {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty)
    (A' := ym(Ïƒ) â‰« A) (eq : ym(Ïƒ) â‰« A = A' := by rfl) : M.ext A' âŸ¶ M.ext A :=
  M.substCons (M.disp _ â‰« Ïƒ) A (M.var _) (by simp [eq])

@[functor_map (attr := reassoc)]
theorem substWk_disp {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty) (A' eq) :
    M.substWk Ïƒ A A' eq â‰« M.disp A = M.disp A' â‰« Ïƒ := by
  simp [substWk]

@[reassoc (attr := simp)]
theorem substWk_var {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty) (A' eq) :
    ym(M.substWk Ïƒ A A' eq) â‰« M.var A = M.var A' := by
  simp [substWk]

/-- `sec` is the section of `disp A` corresponding to `a`.

  ===== Î“ ------ a --------Â¬
 â€–      â†“ sec             V
 â€–   M.ext A -----------> M.Tm
 â€–      |                  |
 â€–      |                  |
 â€–    disp A              M.tp
 â€–      |                  |
 â€–      V                  V
  ===== Î“ ------ A -----> M.Ty -/
def sec {Î“ : Ctx} (A : y(Î“) âŸ¶ M.Ty) (a : y(Î“) âŸ¶ M.Tm) (a_tp : a â‰« M.tp = A) : Î“ âŸ¶ M.ext A :=
  M.substCons (ğŸ™ Î“) A a (by simp [a_tp])

@[functor_map (attr := reassoc (attr := simp))]
theorem sec_disp {Î“ : Ctx} (A : y(Î“) âŸ¶ M.Ty) (a : y(Î“) âŸ¶ M.Tm) (a_tp : a â‰« M.tp = A) :
    M.sec A a a_tp â‰« M.disp A = ğŸ™ _ := by
  simp [sec]

@[reassoc (attr := simp)]
theorem sec_var {Î“ : Ctx} (A : y(Î“) âŸ¶ M.Ty) (a : y(Î“) âŸ¶ M.Tm) (a_tp : a â‰« M.tp = A) :
    ym(M.sec A a a_tp) â‰« M.var A = a := by
  simp [sec]

@[functor_map (attr := reassoc)]
theorem comp_sec {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty) (ÏƒA) (eq : ym(Ïƒ) â‰« A = ÏƒA)
    (a : y(Î“) âŸ¶ M.Tm) (a_tp : a â‰« M.tp = A) :
    Ïƒ â‰« M.sec A a a_tp = M.sec ÏƒA (ym(Ïƒ) â‰« a) (by simp [eq, a_tp]) â‰« M.substWk Ïƒ A _ eq := by
  apply Yoneda.fullyFaithful.map_injective
  apply (M.disp_pullback _).hom_ext <;>
    simp [sec, substWk_disp_functor_map]

/-! ## Polynomial functor on `tp`

Specializations of results from the `Poly` package to natural models. -/

@[simps] def uvPolyTp : UvPoly M.Tm M.Ty := âŸ¨M.tp, inferInstanceâŸ©
def Ptp : Psh Ctx â¥¤ Psh Ctx := M.uvPolyTp.functor

namespace PtpEquiv

variable {Î“ : Ctx} {X : Psh Ctx}

-- TODO: possibly want to remove M.uvPolyTp.equiv
-- and directly define `fst`, `snd`, etc.
/--
A map `(AB : y(Î“) âŸ¶ M.Ptp.obj X)` is equivalent to a pair of maps
`A : y(Î“) âŸ¶ M.Ty` and `B : y(M.ext (fst M AB)) âŸ¶ X`,
thought of as a dependent pair `A : Type` and `B : A âŸ¶ Type`.
`PtpEquiv.fst` is the `A` in this pair.
-/
def fst (AB : y(Î“) âŸ¶ M.Ptp.obj X) : y(Î“) âŸ¶ M.Ty :=
  UvPoly.Equiv.fst M.uvPolyTp X AB

/--
A map `(AB : y(Î“) âŸ¶ M.Ptp.obj X)` is equivalent to a pair of maps
`A : y(Î“) âŸ¶ M.Ty` and `B : y(M.ext (fst M AB)) âŸ¶ X`,
thought of as a dependent pair `A : Type` and `B : A âŸ¶ Type`
`PtpEquiv.snd` is the `B` in this pair.
-/
def snd (AB : y(Î“) âŸ¶ M.Ptp.obj X) (A := fst M AB) (eq : fst M AB = A := by rfl) : y(M.ext A) âŸ¶ X :=
  UvPoly.Equiv.snd' M.uvPolyTp X AB (by rw [â† fst, eq]; exact (M.disp_pullback _).flip)

/--
A map `(AB : y(Î“) âŸ¶ M.Ptp.obj X)` is equivalent to a pair of maps
`A : y(Î“) âŸ¶ M.Ty` and `B : y(M.ext (fst M AB)) âŸ¶ X`,
thought of as a dependent pair `A : Type` and `B : A âŸ¶ Type`
`PtpEquiv.mk` constructs such a map `AB` from such a pair `A` and `B`.
-/
def mk (A : y(Î“) âŸ¶ M.Ty) (B : y(M.ext A) âŸ¶ X) : y(Î“) âŸ¶ M.Ptp.obj X :=
  UvPoly.Equiv.mk' M.uvPolyTp X A (M.disp_pullback _).flip B

@[simp]
lemma fst_mk (A : y(Î“) âŸ¶ M.Ty) (B : y(M.ext A) âŸ¶ X) :
    fst M (mk M A B) = A := by
  simp [fst, mk]

@[simp]
lemma snd_mk (A : y(Î“) âŸ¶ M.Ty) (B : y(M.ext A) âŸ¶ X) :
    snd M (mk M A B) _ (fst_mk ..) = B := by
  dsimp only [snd, mk]
  rw! [UvPoly.Equiv.snd'_mk']

section
variable {Î” : Ctx} {Ïƒ : Î” âŸ¶ Î“} {AB : y(Î“) âŸ¶ M.Ptp.obj X}

theorem fst_comp_left (Ïƒ : y(Î”) âŸ¶ y(Î“)) : fst M (Ïƒ â‰« AB) = Ïƒ â‰« fst M AB :=
  UvPoly.Equiv.fst_comp_left ..

theorem fst_comp_right {Y} (Ïƒ : X âŸ¶ Y) : fst M (AB â‰« M.Ptp.map Ïƒ) = fst M AB :=
  UvPoly.Equiv.fst_comp_right ..

theorem snd_comp_right {Y} (Ïƒ : X âŸ¶ Y) {A} (eq : fst M AB = A) :
    snd M (AB â‰« M.Ptp.map Ïƒ) _ (fst_comp_right M Ïƒ â–¸ eq) = snd M AB _ eq â‰« Ïƒ := by
  simp only [snd, Ptp]
  rw [UvPoly.Equiv.snd'_comp_right M.uvPolyTp X Y Ïƒ AB]

theorem snd_comp_left {A} (eqA : fst M AB = A) {ÏƒA} (eqÏƒ : ym(Ïƒ) â‰« A = ÏƒA) :
    snd M (ym(Ïƒ) â‰« AB) ÏƒA (by simp [fst_comp_left, eqA, eqÏƒ]) =
    ym(M.substWk Ïƒ _ _ eqÏƒ) â‰« snd M AB _ eqA := by
  have H1 : IsPullback ym(M.disp A) (M.var A) (UvPoly.Equiv.fst M.uvPolyTp X AB) M.uvPolyTp.p := by
    rw [â† fst, eqA]; exact (M.disp_pullback _).flip
  have H2 : IsPullback ym(M.disp ÏƒA) (M.var ÏƒA)
    (ym(Ïƒ) â‰« UvPoly.Equiv.fst M.uvPolyTp X AB) M.uvPolyTp.p := by
    rw [â† fst, eqA, eqÏƒ]; exact (M.disp_pullback _).flip
  convert UvPoly.Equiv.snd'_comp_left M.uvPolyTp X AB H1 _ H2
  apply H1.hom_ext <;> simp [â† Functor.map_comp, substWk]

theorem mk_comp_left {Î” Î“ : Ctx} (M : Universe Ctx) (Ïƒ : Î” âŸ¶ Î“)
    {X : Psh Ctx} (A : y(Î“) âŸ¶ M.Ty) (ÏƒA) (eq : ym(Ïƒ) â‰« A = ÏƒA) (B : y(M.ext A) âŸ¶ X) :
    ym(Ïƒ) â‰« PtpEquiv.mk M A B = PtpEquiv.mk M ÏƒA (ym(M.substWk Ïƒ A _ eq) â‰« B) := by
  dsimp [PtpEquiv.mk]
  have h := UvPoly.Equiv.mk'_comp_left M.uvPolyTp X A (M.disp_pullback A).flip B ym(Ïƒ)
    ÏƒA eq (M.disp_pullback ÏƒA).flip
  convert h
  apply (M.disp_pullback _).hom_ext
  Â· simp
  Â· simp [â† Functor.map_comp, substWk_disp]

theorem mk_comp_right {Î“ : Ctx} (M : Universe Ctx)
    {X Y : Psh Ctx} (Ïƒ : X âŸ¶ Y) (A : y(Î“) âŸ¶ M.Ty) (B : y(M.ext A) âŸ¶ X) :
    PtpEquiv.mk M A B â‰« M.Ptp.map Ïƒ = PtpEquiv.mk M A (B â‰« Ïƒ) :=
  UvPoly.Equiv.mk'_comp_right M.uvPolyTp X Y Ïƒ A (M.disp_pullback A).flip B

theorem ext {AB AB' : y(Î“) âŸ¶ M.Ptp.obj X}
    (A := fst M AB) (eq : fst M AB = A := by rfl)
    (h1 : fst M AB = fst M AB')
    (h2 : snd M AB A eq = snd M AB' A (h1 â–¸ eq))
    : AB = AB' := UvPoly.Equiv.ext' _ _ _ h1 h2

theorem eta (AB : y(Î“) âŸ¶ M.Ptp.obj X) : mk M (fst M AB) (snd M AB) = AB :=
  .symm <| ext _ _ rfl (by simp) (by simp)

def lift {Y} (mk : âˆ€ {Î“} {A : y(Î“) âŸ¶ M.Ty}, (y(M.ext A) âŸ¶ X) â†’ (y(Î“) âŸ¶ Y))
    (comp_mk : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty) {ÏƒA} (eq) (B : y(M.ext A) âŸ¶ X),
      ym(Ïƒ) â‰« mk B = mk (A := ÏƒA) (ym(M.substWk Ïƒ A ÏƒA eq) â‰« B))
    : M.Ptp.obj X âŸ¶ Y where
  app Î“' A := yonedaEquiv (mk (snd M (yonedaEquiv.symm A)))
  naturality A B Ïƒ := by
    ext C; dsimp
    set ÏƒC := (M.Ptp.obj X).map Ïƒ C
    set C' := yonedaEquiv.symm C
    set ÏƒC' := yonedaEquiv.symm ÏƒC
    have : ÏƒC' = ym(Ïƒ.unop) â‰« C' := (yonedaEquiv_symm_naturality_left _ _ _).symm
    rw [yonedaEquiv_naturality', comp_mk Ïƒ.unop (fst M C') (ÏƒA := fst M ÏƒC') _ (snd M C'),
      â† snd_comp_left]
    Â· congr! 3
    Â· rw [â† fst_comp_left, this]

theorem comp_lift {Y} (mk comp_mk) {Î“} (ab : y(Î“) âŸ¶ M.Ptp.obj X)
    (A := fst M ab) (eq : fst M ab = A := by rfl) :
    ab â‰« lift M (Y := Y) mk comp_mk = mk (snd M ab A eq) := by
  cases eq
  obtain âŸ¨ab, rflâŸ© := yonedaEquiv.symm.surjective ab
  apply yonedaEquiv.injective
  trans (lift M mk comp_mk).app âŸ¨Î“âŸ© ab
  Â· simp [yonedaEquiv_symm_naturality_right]
  Â· simp [lift]

end

end PtpEquiv

@[reassoc]
theorem PtpEquiv.mk_map {Î“ : Ctx} {X Y : Psh Ctx}
    (A : y(Î“) âŸ¶ M.Ty) (x : y(M.ext A) âŸ¶ X) (Î± : X âŸ¶ Y) :
    mk M A x â‰« M.Ptp.map Î± = mk M A (x â‰« Î±) := by
  simp [mk, Ptp, UvPoly.Equiv.mk'_comp_right]

/-! ## Polynomial composition `M.tp â–¸ N.tp` -/

-- -- `private` lemma for the equivalence below.
-- private lemma lift_ev {Î“ : Ctx} {N : Universe Ctx}
--     {AB : y(Î“) âŸ¶ M.Ptp.obj N.Ty} {Î± : y(Î“) âŸ¶ M.Tm}
--     (hA : AB â‰« M.uvPolyTp.fstProj N.Ty = Î± â‰« M.tp) :
--     pullback.lift AB Î± hA â‰« (UvPoly.PartialProduct.fan M.uvPolyTp N.Ty).snd =
--       ym(M.sec (Î± â‰« M.tp) Î± rfl) â‰«
--         (M.disp_pullback _).lift (M.var _) ym(M.disp _)
--           (by dsimp; rw [hA, (M.disp_pullback _).w]) â‰«
--         (M.Ptp_equiv AB).2 :=
--   sorry

namespace compDomEquiv
open UvPoly

variable {M N : Universe Ctx} {Î“ Î” : Ctx} (Ïƒ : Î” âŸ¶ Î“)

/-- Universal property of `compDom`, decomposition (part 1).

A map `ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp` is equivalently three maps
`fst, dependent, snd` such that `fst_tp` and `snd_tp`. The map `fst : y(Î“) âŸ¶ M.Tm`
is the `(a : A)` in `(a : A) Ã— (b : B a)`.
-/
def fst (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp) : y(Î“) âŸ¶ M.Tm :=
  ab â‰« pullback.snd N.tp (UvPoly.PartialProduct.fan M.uvPolyTp N.Ty).snd â‰«
    pullback.snd (M.uvPolyTp.fstProj N.Ty) M.uvPolyTp.p

/-- Computation of `comp` (part 1).

`fst_tp` is (part 1) of the computation that
      (Î±, B, Î², h)
     Î“ âŸ¶ compDom
      \        |
       \       | comp
(Î± â‰« tp, B)    |
         \     V
           >  P_tp Ty
Namely the first projection `Î± â‰« tp` agrees.
-/
theorem fst_tp (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp) :
    fst ab â‰« M.tp = PtpEquiv.fst M (ab â‰« (M.uvPolyTp.compP _)) := by
  have : pullback.snd (M.uvPolyTp.fstProj N.Ty) M.tp â‰« M.tp =
    pullback.fst (M.uvPolyTp.fstProj N.Ty) M.tp â‰« M.uvPolyTp.fstProj N.Ty :=
      Eq.symm pullback.condition
  simp [PtpEquiv.fst, fst, this]
  rfl

theorem comp_fst (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp) (Ïƒ : y(Î”) âŸ¶ y(Î“)) :
    Ïƒ â‰« fst ab = fst (Ïƒ â‰« ab) := by simp [fst]

/-- Universal property of `compDom`, decomposition (part 2).

A map `ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp` is equivalently three maps
`fst, dependent, snd` such that `fst_tp` and `snd_tp`.
The map `dependent : y(M.ext (fst N ab â‰« M.tp)) âŸ¶ M.Ty`
is the `B : A âŸ¶ Type` in `(a : A) Ã— (b : B a)`.
Here `A` is implicit, derived by the typing of `fst`, or `(a : A)`.
-/
def dependent (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp)
    (A := fst ab â‰« M.tp) (eq : fst ab â‰« M.tp = A := by rfl) :
    y(M.ext A) âŸ¶ N.Ty :=
  PtpEquiv.snd M (ab â‰« (M.uvPolyTp.compP _)) _ (by rw [â† eq, fst_tp])

theorem comp_dependent (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp)
    {A} (eq1 : fst ab â‰« M.tp = A)
    {ÏƒA} (eq2 : ym(Ïƒ) â‰« A = ÏƒA) :
    ym(substWk M Ïƒ _ _ eq2) â‰« dependent ab A eq1 =
    dependent (ym(Ïƒ) â‰« ab) ÏƒA (by simp [â† comp_fst, eq1, eq2]) := by
  rw [dependent, â† PtpEquiv.snd_comp_left]; rfl

/-- Universal property of `compDom`, decomposition (part 3).

A map `ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp` is equivalently three maps
`fst, dependent, snd` such that `fst_tp` and `snd_tp`.
The map `snd : y(Î“) âŸ¶ M.Tm`
is the `(b : B a)` in `(a : A) Ã— (b : B a)`.
-/
def snd (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp) : y(Î“) âŸ¶ N.Tm :=
  ab â‰« pullback.fst N.tp (PartialProduct.fan M.uvPolyTp N.Ty).snd

theorem comp_snd (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp) (Ïƒ : y(Î”) âŸ¶ y(Î“)) :
    Ïƒ â‰« snd ab = snd (Ïƒ â‰« ab) := by simp [snd]

/-- Universal property of `compDom`, decomposition (part 4).

A map `ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp` is equivalently three maps
`fst, dependent, snd` such that `fst_tp` and `snd_tp`.
The equation `snd_tp` says that the type of `b : B a` agrees with
the expression for `B a` obtained solely from `dependent`, or `B : A âŸ¶ Type`.
-/
theorem snd_tp (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp)
    {A} (eq : fst ab â‰« M.tp = A) :
    snd ab â‰« N.tp = ym(M.sec _ (fst ab) eq) â‰« dependent ab A eq := by
  simp [snd, pullback.condition, dependent, PtpEquiv.snd, Equiv.snd'_eq]
  simp only [â† Category.assoc]; congr! 1
  apply pullback.hom_ext <;> simp [fst, UvPoly.compP]

/-- Universal property of `compDom`, constructing a map into `compDom`. -/
def mk (Î± : y(Î“) âŸ¶ M.Tm) {A} (eq : Î± â‰« M.tp = A) (B : y(M.ext A) âŸ¶ N.Ty) (Î² : y(Î“) âŸ¶ N.Tm)
    (h : Î² â‰« N.tp = ym(M.sec _ Î± eq) â‰« B) : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp := by
  refine pullback.lift Î² (pullback.lift (PtpEquiv.mk _ A B) Î± ?_) ?_
  Â· simp [â† Equiv.fst_eq, â† PtpEquiv.fst.eq_def, eq]
  Â· simp [h]
    conv_lhs => arg 2; exact
      Equiv.snd'_mk' M.uvPolyTp N.Ty A _ B
        |>.symm.trans <| Equiv.snd'_eq M.uvPolyTp N.Ty (PtpEquiv.mk M A B) _
    simp only [â† Category.assoc]; congr! 1
    apply pullback.hom_ext <;> simp

@[simp]
theorem fst_mk (Î± : y(Î“) âŸ¶ M.Tm) {A} (eq : Î± â‰« M.tp = A) (B : y(M.ext A) âŸ¶ N.Ty) (Î² : y(Î“) âŸ¶ N.Tm)
    (h : Î² â‰« N.tp = ym(M.sec _ Î± eq) â‰« B) : fst (mk Î± eq B Î² h) = Î± := by
  simp [mk, fst]

@[simp]
theorem dependent_mk (Î± : y(Î“) âŸ¶ M.Tm) {A} (eq : Î± â‰« M.tp = A)
    (B : y(M.ext A) âŸ¶ N.Ty) (Î² : y(Î“) âŸ¶ N.Tm)
    (h : Î² â‰« N.tp = ym(M.sec _ Î± eq) â‰« B) :
    dependent (mk Î± eq B Î² h) A (by simp [fst_mk, eq]) = B := by
  simp [mk, dependent, UvPoly.compP]
  convert PtpEquiv.snd_mk M A B using 2
  slice_lhs 1 2 => apply pullback.lift_snd
  simp

@[simp]
theorem snd_mk (Î± : y(Î“) âŸ¶ M.Tm) {A} (eq : Î± â‰« M.tp = A) (B : y(M.ext A) âŸ¶ N.Ty) (Î² : y(Î“) âŸ¶ N.Tm)
    (h : Î² â‰« N.tp = ym(M.sec _ Î± eq) â‰« B) : snd (mk Î± eq B Î² h) = Î² := by
  simp [mk, snd]

theorem ext {abâ‚ abâ‚‚ : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp}
    {A} (eq : fst abâ‚ â‰« M.tp = A)
    (h1 : fst abâ‚ = fst abâ‚‚)
    (h2 : dependent abâ‚ A eq = dependent abâ‚‚ A (h1 â–¸ eq))
    (h3 : snd abâ‚ = snd abâ‚‚) : abâ‚ = abâ‚‚ := by
  refine pullback.hom_ext h3 (pullback.hom_ext ?_ h1)
  simp only [dependent, PtpEquiv.snd] at h2
  generalize_proofs _ _ H at h2
  refine Equiv.ext' M.uvPolyTp N.Ty H ?_ h2
  simp [Equiv.fst, pullback.condition]
  simp only [â† Category.assoc]; congr 1

theorem comp_mk
    (Î± : y(Î“) âŸ¶ M.Tm) {A} (e1 : Î± â‰« M.tp = A)
    (B : y(M.ext A) âŸ¶ N.Ty)
    (Î² : y(Î“) âŸ¶ N.Tm)
    (e2 : Î² â‰« N.tp = ym(M.sec A Î± e1) â‰« B)
    (Ïƒ : Î” âŸ¶ Î“) {ÏƒA} (e3 : ym(Ïƒ) â‰« A = ÏƒA) :
    ym(Ïƒ) â‰« mk Î± e1 B Î² e2 =
    mk (ym(Ïƒ) â‰« Î±) (by simp [e1, e3])
      (ym(M.substWk Ïƒ A _ e3) â‰« B) (ym(Ïƒ) â‰« Î²)
      (by simp [e2]; rw [â† Functor.map_comp_assoc, comp_sec]; simp; congr!) := by
  apply ext (A := ÏƒA) (by simp [â† comp_fst, e1, e3]) <;> simp [â† comp_fst, â† comp_snd]
  rw [â† comp_dependent, dependent_mk]

theorem eta (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp)
    {A} (eq : fst ab â‰« M.tp = A) :
    mk (fst ab) eq (dependent ab A eq) (snd ab) (snd_tp ab eq) = ab := by
  symm; apply ext (eq := eq) <;> simp

def lift {Y}
    (mk : âˆ€ {Î“} (Î± : y(Î“) âŸ¶ M.Tm) {A} (eq : Î± â‰« M.tp = A)
      (B : y(M.ext A) âŸ¶ N.Ty) (Î² : y(Î“) âŸ¶ N.Tm),
      Î² â‰« N.tp = ym(M.sec A Î± eq) â‰« B â†’ (y(Î“) âŸ¶ Y))
    (comp_mk : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“)
      (Î± : y(Î“) âŸ¶ M.Tm) {A} (eq : Î± â‰« M.tp = A) {ÏƒA} (eqA : ym(Ïƒ) â‰« A = ÏƒA)
      (B : y(M.ext A) âŸ¶ N.Ty)
      (Î² : y(Î“) âŸ¶ N.Tm)
      (eqB : Î² â‰« N.tp = ym(M.sec A Î± eq) â‰« B),
      ym(Ïƒ) â‰« mk Î± eq B Î² eqB =
      mk (ym(Ïƒ) â‰« Î±) (by simp [eq, eqA])
        (ym(M.substWk Ïƒ A _ eqA) â‰« B) (ym(Ïƒ) â‰« Î²)
        (by simp [eqB]; rw [â† Functor.map_comp_assoc, comp_sec]; simp; congr!))
    : M.uvPolyTp.compDom N.uvPolyTp âŸ¶ Y where
  app Î“' A :=
    have A := yonedaEquiv.symm A
    yonedaEquiv (mk (fst A) rfl (dependent A) (snd A) (snd_tp A rfl))
  naturality A B Ïƒ := by
    ext C; dsimp
    set ÏƒC := (M.uvPolyTp.compDom N.uvPolyTp).map Ïƒ C
    set C' := yonedaEquiv.symm C
    set ÏƒC' := yonedaEquiv.symm ÏƒC
    have : ÏƒC' = ym(Ïƒ.unop) â‰« C' := (yonedaEquiv_symm_naturality_left _ _ _).symm
    have : fst ÏƒC' = ym(Ïƒ.unop) â‰« fst C' := by simp [this, comp_fst]
    have : ym(Ïƒ.unop) â‰« fst C' â‰« M.tp = fst ÏƒC' â‰« M.tp := by simp [this]
    rw [yonedaEquiv_naturality', comp_mk Ïƒ.unop (ÏƒA := fst ÏƒC' â‰« M.tp)]
    congr! 2
    Â· rw [comp_dependent]; congr! 1; assumption
    Â· simp [comp_snd, *]

theorem comp_lift {Y} (mk comp_mk) {Î“} (ab : y(Î“) âŸ¶ M.uvPolyTp.compDom N.uvPolyTp)
    (A := fst ab â‰« M.tp) (eq : fst ab â‰« M.tp = A := by rfl)
    (B := dependent ab A eq) (eqB : dependent ab A eq = B := by rfl) :
    ab â‰« lift (M := M) (N := N) (Y := Y) mk comp_mk =
    mk (fst ab) eq B (snd ab) (eqB â–¸ snd_tp ab eq) := by
  cases eq
  obtain âŸ¨ab, rflâŸ© := yonedaEquiv.symm.surjective ab
  apply yonedaEquiv.injective
  trans (lift mk comp_mk).app âŸ¨Î“âŸ© ab
  Â· simp [yonedaEquiv_symm_naturality_right]
  Â· simp [eqB, lift]

theorem comp_lift_mk {Y} (mk' comp_mk) {Î“}
    (Î± : y(Î“) âŸ¶ M.Tm) {A} (eq : Î± â‰« M.tp = A) (B : y(M.ext A) âŸ¶ N.Ty) (Î² : y(Î“) âŸ¶ N.Tm)
    (h : Î² â‰« N.tp = ym(M.sec _ Î± eq) â‰« B) :
    mk Î± eq B Î² h â‰« lift (M := M) (N := N) (Y := Y) mk' comp_mk =
    mk' Î± eq B Î² h := by rw [comp_lift (A := A) (eq := by simp [eq])]; simp

end compDomEquiv

/-! ## Pi and Sigma types -/

set_option linter.dupNamespace false in
protected structure Pi where
  Pi : M.Ptp.obj M.Ty âŸ¶ M.Ty
  lam : M.Ptp.obj M.Tm âŸ¶ M.Tm
  Pi_pullback : IsPullback lam (M.Ptp.map M.tp) M.tp Pi

protected structure Sigma where
  Sig : M.Ptp.obj M.Ty âŸ¶ M.Ty
  pair : UvPoly.compDom (uvPolyTp M) (uvPolyTp M) âŸ¶ M.Tm
  Sig_pullback : IsPullback pair ((uvPolyTp M).compP (uvPolyTp M)) M.tp Sig

variable {M} in
open compDomEquiv in
def Sigma.mk'
    (Sig : âˆ€ {Î“} {A : y(Î“) âŸ¶ M.Ty}, (y(M.ext A) âŸ¶ M.Ty) â†’ (y(Î“) âŸ¶ M.Ty))
    (comp_Sig : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) (A : y(Î“) âŸ¶ M.Ty) {ÏƒA} (eq) (B : y(M.ext A) âŸ¶ M.Ty),
      ym(Ïƒ) â‰« Sig B = Sig (ym(M.substWk Ïƒ A ÏƒA eq) â‰« B))
    (assoc : âˆ€ {Î“} {A : y(Î“) âŸ¶ M.Ty} (B : y(M.ext A) âŸ¶ M.Ty), M.ext B â‰… M.ext (Sig B))
    (comp_assoc : âˆ€ {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) {A : y(Î“) âŸ¶ M.Ty} {ÏƒA} (eq) (B : y(M.ext A) âŸ¶ M.Ty),
      substWk _ (substWk _ Ïƒ _ _ eq) _ â‰« (assoc B).hom =
      (assoc (ym(substWk M Ïƒ A ÏƒA eq) â‰« B)).hom â‰« substWk M Ïƒ _ _ (comp_Sig ..))
    (assoc_disp : âˆ€ {Î“} {A : y(Î“) âŸ¶ M.Ty} (B : y(M.ext A) âŸ¶ M.Ty),
      (assoc B).hom â‰« M.disp _ = M.disp _ â‰« M.disp _)
    : M.Sigma where
  Sig := PtpEquiv.lift M Sig comp_Sig
  pair := by
    fapply compDomEquiv.lift
    Â· intro Î“ Î± A eq B Î² eqB
      refine ym(?_ â‰« (assoc B).hom) â‰« M.var _
      exact substCons _ (substCons _ (ğŸ™ _) _ Î± (by simp [eq])) _ Î² eqB
    Â· as_aux_lemma =>
      intro Î“ Î” Ïƒ Î± A eq ÏƒA eqA B Î² eqB
      have := comp_assoc Ïƒ eqA B
      replace := congr(ym($this) â‰« M.var _)
      simp at this âŠ¢; rw [â† this]; clear this
      simp only [â† Category.assoc]; congr! 2
      apply (M.disp_pullback _).hom_ext <;> simp
      apply (M.disp_pullback _).hom_ext <;> simp [substWk_disp_functor_map]
  Sig_pullback := by
    fapply RepPullbackCone.is_pullback'
    Â· refine hom_ext_yoneda fun Î“ A => ?_
      rw [reassoc_of% compDomEquiv.comp_lift, â† Category.assoc A, PtpEquiv.comp_lift]
      have := assoc_disp (dependent A _ rfl)
      simp; simp only [â† Functor.map_comp_assoc]; rw [this, comp_Sig]; congr! 1
      case eq => simp [fst_tp]
      rw [comp_dependent, dependent]; congr! 2
      simp [substCons_disp]
    Â· intro s
      let A := PtpEquiv.fst M s.snd
      let B : y(M.ext A) âŸ¶ M.Ty := PtpEquiv.snd M s.snd
      have ptp := s.condition
      simp [PtpEquiv.comp_lift] at ptp; change _ = Sig B at ptp
      let Ïƒ := M.sec (Sig B) _ ptp â‰« (assoc B).inv
      have := assoc_disp B
      rw [â† Iso.eq_inv_comp, eq_comm] at this
      replace : Ïƒ â‰« M.disp B â‰« M.disp A = M.sec (Sig B) .. â‰« _ :=
        (Category.assoc ..).trans congr(M.sec _ _ ptp â‰« $this)
      replace := congr(ym($this)); simp at this
      refine
        let t := compDomEquiv.mk (ym(Ïƒ â‰« M.disp _) â‰« M.var _) ?_ B (ym(Ïƒ) â‰« M.var _) ?_
        âŸ¨t, ?_âŸ©
      Â· simp [reassoc_of% this]
      Â· simp; rw [â† Category.assoc]; congr! 1
        apply (M.disp_pullback A).hom_ext <;> simp [this]
      Â· have ttp : fst t â‰« M.tp = A := by simp [t, reassoc_of% this]
        have t1 : fst t = ym(Ïƒ â‰« M.disp _) â‰« M.var _ := fst_mk ..
        have td : dependent t _ ttp = B := dependent_mk ..
        have t2 : snd t = ym(Ïƒ) â‰« M.var _ := snd_mk ..
        refine âŸ¨?_, ?_, fun m h1 h2 => ?_âŸ©
        Â· rw [comp_lift_mk]
          convert (show ym(M.sec _ _ ptp) â‰« M.var _ = s.fst by simp) using 3
          rw [â† Iso.eq_comp_inv]
          apply Yoneda.yoneda_faithful.1
          apply (M.disp_pullback _).hom_ext <;> simp [Ïƒ]
          apply (M.disp_pullback _).hom_ext <;> simp
          simpa [Ïƒ] using this.symm
        Â· symm; fapply PtpEquiv.ext (A := A)
          Â· rw [â† fst_tp, ttp]
          Â· exact (dependent_mk ..).symm
        Â· have mtp : fst m â‰« M.tp = A := by rw [fst_tp]; unfold A; congr! 1
          have md : dependent m _ mtp = B := by unfold dependent B; congr! 1
          rw [comp_lift (A := A) (eq := mtp) (B := B) (eqB := md)] at h1
          refine let Ïƒ' := _; have h1 : ym(Ïƒ' â‰« _) â‰« _ = _ := h1; ?_
          have H : Ïƒ' â‰« (assoc B).hom = M.sec _ s.fst ptp := by
            apply Yoneda.yoneda_faithful.1
            apply (M.disp_pullback _).hom_ext
            Â· rw [h1]; simp
            Â· simp; rw [â† Functor.map_comp, assoc_disp]; simp [Ïƒ']
          simp [â† Iso.eq_comp_inv, Ïƒ'] at H
          have m1 : fst m = ym(Ïƒ â‰« M.disp B) â‰« M.var A := by
            simpa [Ïƒ] using congr(ym($H â‰« M.disp _) â‰« M.var _)
          symm; fapply compDomEquiv.ext (A := A) (eq := by simp [t, reassoc_of% this])
          Â· simp [m1, t1]
          Â· simp [md, td]
          Â· simpa [Ïƒ, t2] using congr(ym($H.symm) â‰« M.var _)

/--
Universe.IdIntro consists of the following commutative square
       refl
M.Tm ------> M.Tm
 |            |
 |            |
diag         M.tp
 |            |
 |            |
 V            V
 k --------> M.Ty
      Id

where `K` (for "Kernel" of `tp`) is a chosen pullback for the square
       k1
 k ---------> Tm
 |             |
 |             |
 k2            | tp
 |             |
 V             V
Tm ----------> Ty
        tp
and `diag` denotes the diagonal into the pullback `K`.

We require a choice of pullback because,
although all pullbacks exist in presheaf categories,
when constructing a model it is convenient to know
that `K` is some specific construction on-the-nose.
-/
structure IdIntro where
  k : Psh Ctx
  k1 : k âŸ¶ M.Tm
  k2 : k âŸ¶ M.Tm
  isKernelPair : IsKernelPair M.tp k1 k2
  Id : k âŸ¶ M.Ty
  refl : M.Tm âŸ¶ M.Tm
  refl_tp : refl â‰« M.tp =
    (IsPullback.lift isKernelPair (ğŸ™ M.Tm) (ğŸ™ M.Tm) (by simp)) â‰« Id

namespace IdIntro

variable {M} (idIntro : IdIntro M) {Î“ : Ctx}

/-- The introduction rule for identity types.
To minimize the number of arguments, we infer the type from the terms. -/
def mkId (a0 a1 : y(Î“) âŸ¶ M.Tm)
    (a0_tp_eq_a1_tp : a0 â‰« M.tp = a1 â‰« M.tp) :
    y(Î“) âŸ¶ M.Ty :=
  idIntro.isKernelPair.lift a1 a0 (by rw [a0_tp_eq_a1_tp]) â‰« idIntro.Id

theorem comp_mkId {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“)
    (a0 a1 : y(Î“) âŸ¶ M.Tm) (eq : a0 â‰« M.tp = a1 â‰« M.tp) :
    ym(Ïƒ) â‰« mkId idIntro a0 a1 eq =
      mkId idIntro (ym(Ïƒ) â‰« a0) (ym(Ïƒ) â‰« a1) (by simp [eq]) := by
  simp [mkId]; rw [â† Category.assoc]; congr 1
  apply idIntro.isKernelPair.hom_ext <;> simp

def mkRefl (a : y(Î“) âŸ¶ M.Tm) : y(Î“) âŸ¶ M.Tm :=
  a â‰« idIntro.refl

theorem comp_mkRefl {Î” Î“ : Ctx} (Ïƒ : Î” âŸ¶ Î“) (a : y(Î“) âŸ¶ M.Tm) :
    ym(Ïƒ) â‰« idIntro.mkRefl a = idIntro.mkRefl (ym(Ïƒ) â‰« a) :=
  rfl

@[simp]
theorem mkRefl_tp (a : y(Î“) âŸ¶ M.Tm) :
    idIntro.mkRefl a â‰« M.tp = idIntro.mkId a a rfl := by
  simp only [mkRefl, Category.assoc, idIntro.refl_tp, mkId]
  rw [â† Category.assoc]
  congr 1
  apply idIntro.isKernelPair.hom_ext <;> simp

/-- The context appearing in the motive for identity elimination `J`
  Î“ âŠ¢ A
  Î“ âŠ¢ a : A
  Î“.(x:A).(h:Id(A,a,x)) âŠ¢ M
  ...
-/
def motiveCtx (a : y(Î“) âŸ¶ M.Tm) : Ctx :=
  M.ext (idIntro.mkId (ym(M.disp (a â‰« M.tp)) â‰« a) (M.var _) (by simp))

def motiveSubst {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) (a : y(Î“) âŸ¶ M.Tm) :
    motiveCtx idIntro (ym(Ïƒ) â‰« a) âŸ¶ motiveCtx idIntro a := by
  refine substWk _ (substWk _ Ïƒ _ _ (by simp)) _ _ ?_
  simp [comp_mkId]; congr 1; simp only [â† Functor.map_comp_assoc, substWk_disp]

/-- The substitution `(a,refl)` appearing in identity elimination `J`
  `(a,refl) : y(Î“) âŸ¶ y(Î“.(x:A).(h:Id(A,a,x)))`
  so that we can write
  `Î“ âŠ¢ r : M(a,refl)`
-/
def reflSubst (a : y(Î“) âŸ¶ M.Tm) : Î“ âŸ¶ idIntro.motiveCtx a :=
  M.substCons (M.substCons (ğŸ™ Î“) (a â‰« M.tp) a (by simp)) _ (idIntro.mkRefl a) (by
    simp only [mkRefl_tp, mkId, â† Category.assoc]
    congr 1
    apply idIntro.isKernelPair.hom_ext <;> simp)

@[reassoc]
theorem comp_reflSubst' {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“) (a : y(Î“) âŸ¶ M.Tm) :
    ym(Ïƒ) â‰« ym(idIntro.reflSubst a) =
    ym(idIntro.reflSubst (ym(Ïƒ) â‰« a)) â‰« ym(idIntro.motiveSubst Ïƒ a) := by
  apply (M.disp_pullback _).hom_ext <;> simp [reflSubst, motiveSubst, mkRefl]
  apply (M.disp_pullback _).hom_ext <;> simp [substWk]

@[simp, reassoc]
lemma comp_reflSubst (a : y(Î“) âŸ¶ M.Tm) {Î”} (Ïƒ : Î” âŸ¶ Î“) :
    reflSubst idIntro (ym(Ïƒ) â‰« a) â‰« idIntro.motiveSubst Ïƒ a = Ïƒ â‰« reflSubst idIntro a := by
  apply Yoneda.fullyFaithful.map_injective
  simp [Functor.map_comp, comp_reflSubst']

end IdIntro

/-- The full structure interpreting the natural model semantics for identity types
requires an `IdIntro` and an elimination rule `j` which satisfies a typing rule `j_tp`
and a Î²-rule `reflSubst_j`.
There is an equivalent formulation of these extra conditions later in `Id'`
that uses the language of polynomial endofunctors.

Note that the universe/model `N` for the motive `C` is different from the universe `M` that the
identity type lives in.
-/
protected structure Id (N : Universe Ctx) (i : IdIntro M) where
  j {Î“} (a : y(Î“) âŸ¶ M.Tm) (C : y(IdIntro.motiveCtx _ a) âŸ¶ N.Ty) (r : y(Î“) âŸ¶ N.Tm)
    (r_tp : r â‰« N.tp = ym(i.reflSubst a) â‰« C) :
    y(i.motiveCtx a) âŸ¶ N.Tm
  j_tp {Î“} (a : y(Î“) âŸ¶ M.Tm) (C : y(IdIntro.motiveCtx _ a) âŸ¶ N.Ty) (r : y(Î“) âŸ¶ N.Tm)
    (r_tp : r â‰« N.tp = ym(i.reflSubst a) â‰« C) : j a C r r_tp â‰« N.tp = C
  comp_j {Î“ Î”} (Ïƒ : Î” âŸ¶ Î“)
    (a : y(Î“) âŸ¶ M.Tm) (C : y(IdIntro.motiveCtx _ a) âŸ¶ N.Ty) (r : y(Î“) âŸ¶ N.Tm)
    (r_tp : r â‰« N.tp = ym(i.reflSubst a) â‰« C) :
    ym(i.motiveSubst Ïƒ _) â‰« j a C r r_tp =
    j (ym(Ïƒ) â‰« a) (ym(i.motiveSubst Ïƒ _) â‰« C) (ym(Ïƒ) â‰« r) (by
      simp [r_tp, IdIntro.comp_reflSubst'_assoc])
  reflSubst_j {Î“} (a : y(Î“) âŸ¶ M.Tm) (C : y(IdIntro.motiveCtx _ a) âŸ¶ N.Ty) (r : y(Î“) âŸ¶ N.Tm)
    (r_tp : r â‰« N.tp = ym(i.reflSubst a) â‰« C) :
    ym(i.reflSubst a) â‰« j a C r r_tp = r

namespace Id

variable {M} {N : Universe Ctx} {ii : M.IdIntro} (i : M.Id N ii) {Î“ : Ctx} (a : y(Î“) âŸ¶ M.Tm)
  (C : y(ii.motiveCtx a) âŸ¶ N.Ty) (r : y(Î“) âŸ¶ N.Tm)
  (r_tp : r â‰« N.tp = ym(ii.reflSubst a) â‰« C) (b : y(Î“) âŸ¶ M.Tm) (b_tp : b â‰« M.tp = a â‰« M.tp)
  (h : y(Î“) âŸ¶ M.Tm) (h_tp : h â‰« M.tp = ii.isKernelPair.lift b a (by aesop) â‰« ii.Id)

def endPtSubst : Î“ âŸ¶ ii.motiveCtx a :=
  M.substCons (M.substCons (ğŸ™ _) _ b (by aesop)) _ h (by
    simp only [h_tp, IdIntro.mkId, â† Category.assoc]
    congr 1
    apply ii.isKernelPair.hom_ext
    Â· simp
    Â· simp)

/-- The elimination rule for identity types, now with the parameters as explicit terms.
  `Î“ âŠ¢ A` is the type with a term `Î“ âŠ¢ a : A`.
  `Î“ (y : A) (p : Id(A,a,y)) âŠ¢ C` is the motive for the elimination.
  `Î“ âŠ¢ b : A` is a second term in `A` and `Î“ âŠ¢ h : Id(A,a,b)` is a path from `a` to `b`.
  Then `Î“ âŠ¢ mkJ' : C [b/y,h/p]` is a term of the motive with `b` and `h` substituted
-/
def mkJ : y(Î“) âŸ¶ N.Tm :=
  ym(endPtSubst a b b_tp h h_tp) â‰« i.j a C r r_tp

/-- Typing for elimination rule `J` -/
lemma mkJ_tp : i.mkJ a C r r_tp b b_tp h h_tp â‰« N.tp = ym(endPtSubst a b b_tp h h_tp) â‰« C := by
  rw [mkJ, Category.assoc, i.j_tp]

/-- Î² rule for identity types. Substituting `J` with `refl` gives the user-supplied value `r` -/
lemma mkJ_refl : i.mkJ a C r r_tp a rfl (ii.mkRefl a) (by aesop) = r :=
  calc ym(endPtSubst a a _ (ii.mkRefl a) _) â‰« i.j a C r r_tp
    _ = ym(ii.reflSubst a) â‰« i.j a C r r_tp := rfl
    _ = r := by rw [i.reflSubst_j]

end Id

/--
`UniverseBase.IdElimBase` extends the structure `UniverseBase.IdIntro`
with a chosen pullback of `Id`
       i1
 i --------> M.Tm
 |            |
 |            |
i2           M.tp
 |            |
 V            V
 k --------> M.Ty
      Id

Again, we always have a pullback,
but when we construct a natural model,
this may not be definitionally equal to the pullbacks we construct,
for example using context extension.
-/
structure IdElimBase extends IdIntro M where
  i : Psh Ctx
  i1 : i âŸ¶ M.Tm
  i2 : i âŸ¶ k
  i_isPullback : IsPullback i1 i2 M.tp Id

namespace IdElimBase
variable {M} (idElimBase : IdElimBase M)

/-- The comparison map `M.tm âŸ¶ i` induced by the pullback universal property of `i`.

          refl
 M.Tm --------->
           i1
 |   i --------> M.Tm
 |   |            |
diag |            |
 |  i2           M.tp
 |   |            |
 |   V            V
 V   k --------> M.Ty
          Id
-/
def comparison : M.Tm âŸ¶ idElimBase.i :=
  idElimBase.i_isPullback.lift idElimBase.refl
  (IsPullback.lift idElimBase.isKernelPair (ğŸ™ M.Tm) (ğŸ™ M.Tm) (by simp))
  idElimBase.refl_tp

@[simp]
lemma comparison_comp_i1 : idElimBase.comparison â‰« idElimBase.i1 = idElimBase.refl := by
  simp [comparison]

@[simp, reassoc]
lemma comparison_comp_i2_comp_k1 : idElimBase.comparison â‰« idElimBase.i2 â‰« idElimBase.k1 =
    ğŸ™ _ := by
  simp [comparison]

@[simp, reassoc]
lemma comparison_comp_i2_comp_k2 : idElimBase.comparison â‰« idElimBase.i2 â‰« idElimBase.k2 =
    ğŸ™ _ := by
  simp [comparison]

/-- `i` over `Tm` can be informally thought of as the context extension
`(A : Ty).(a b : A).(p : Id(a,b)) ->> (A : Ty) (a : A)`
which is defined by the composition of (maps informally thought of as) context extensions
`(A : Ty).(a b : A).(p : Id(a,b)) ->> (A : Ty).(a b : A) ->> (A : Ty).(a : A)`
This is the signature for a polynomial functor `iUvPoly` on the presheaf category `Psh Ctx`.
-/
@[simps] def iUvPoly : UvPoly idElimBase.i M.Tm := âŸ¨idElimBase.i2 â‰« idElimBase.k2, inferInstanceâŸ©

/-- The functor part of the polynomial endofunctor `iOverUvPoly` -/
abbrev iFunctor : Psh Ctx â¥¤ Psh Ctx := idElimBase.iUvPoly.functor

/-- Consider the comparison map `comparison : Tm âŸ¶ i` in the slice over `Tm`.
Then the contravariant action `UVPoly.verticalNatTrans` of taking `UvPoly` on a slice
results in a natural transformation `P_iOver âŸ¶ P_(ğŸ™ Tm)`
between the polynomial endofunctors `iUvPoly` and `UvPoly.id M.Tm` respectively.
  comparison
Tm ----> i
 \      /
 ğŸ™\    /i2 â‰« k2
   \  /
    VV
    Tm
-/
def verticalNatTrans : idElimBase.iFunctor âŸ¶ (UvPoly.id M.Tm).functor :=
    UvPoly.verticalNatTrans (UvPoly.id M.Tm) idElimBase.iUvPoly
  idElimBase.comparison (by simp [iUvPoly])

section reflCase

variable (i : IdIntro M) {N : Universe Ctx}

variable {Î“ : Ctx} (a : y(Î“) âŸ¶ M.Tm) (r : y(Î“) âŸ¶ N.Tm)

lemma reflCase_aux : IsPullback (ğŸ™ y(Î“)) a a (UvPoly.id M.Tm).p :=
  have : IsIso (UvPoly.id M.Tm).p := by simp; infer_instance
  IsPullback.of_horiz_isIso (by simp)

/-- The variable `r` witnesses the motive for the case `refl`,
This gives a map `(a,r) : Î“ âŸ¶ P_ğŸ™Tm Tm â‰… Tm Ã— Tm` where
```
    fst â‰« r
N.Tm <--   Î“  --------> Tm
    <      â€–            â€–
     \     â€–   (pb)     â€– ğŸ™_Tm
    r \    â€–            â€–
       \   â€–            â€–
        \  Î“  --------> Tm
                 a
```
-/
def reflCase : y(Î“) âŸ¶ (UvPoly.id M.Tm).functor.obj N.Tm :=
  UvPoly.Equiv.mk' (UvPoly.id M.Tm) N.Tm a (R := y(Î“)) (f := ğŸ™ _) (g := a)
  (reflCase_aux a) r
-- TODO: consider generalizing
-- TODO: consider showing UvPoly on identity `(P_ğŸ™_Y X)` is isomorphic to product `Y Ã— X`

end reflCase

open IdElimBase IdIntro

section Equiv

variable {Î“ : Ctx} {X : Psh Ctx}

section
variable (a : y(Î“) âŸ¶ M.Tm)
/-
In the following lemmas we build the following diagram of pullbacks,
where `pullback` is the pullback of `iâ‚‚ â‰« kâ‚‚` along `a` given by `HasPullback`.
  X
  Î›
  |
  | x
  |
 y(Î“.aâ‰«tp.Id(...)) ------> i ------> Tm
  |                        |         |
  |                        | iâ‚‚      V
  |                        |         Ty
  V                        V
 y(Î“.aâ‰«tp) ------------>   k ------> Tm
  |                        |    kâ‚   |
  |                        |kâ‚‚       |tp
  |                        |         |
  |                        V         V
 y(Î“) ---------------->   Tm -----> Ty
               a               tp
-/

def toK : y(M.ext (a â‰« M.tp)) âŸ¶ idElimBase.k :=
  idElimBase.isKernelPair.lift (M.var _) (ym(M.disp _) â‰« a) (by simp)

lemma toK_comp_k1 : idElimBase.toK a â‰« idElimBase.k1 = M.var _ := by simp [toK]

lemma toK_comp_left {Î”} (Ïƒ : Î” âŸ¶ Î“) : toK idElimBase (ym(Ïƒ) â‰« a) =
    ym(M.substWk Ïƒ (a â‰« M.tp)) â‰« toK idElimBase a := by
  dsimp [toK]
  apply idElimBase.isKernelPair.hom_ext
  Â· rw! [Category.assoc]
    simp
  Â· simp only [IsKernelPair.lift_snd, Category.assoc]
    slice_rhs 1 2 => rw [â† Functor.map_comp, substWk_disp]
    rw! [Category.assoc]
    simp

lemma ext_a_tp_isPullback : IsPullback (toK idElimBase a) ym(M.disp _)
    idElimBase.k2 a :=
  IsPullback.of_right' (M.disp_pullback _) idElimBase.isKernelPair

def toI : y(idElimBase.motiveCtx a) âŸ¶ idElimBase.i :=
  idElimBase.i_isPullback.lift (M.var _) (ym(M.disp _) â‰« toK idElimBase a)
  (by rw [(M.disp_pullback _).w]; simp [IdIntro.mkId, toK])

lemma toI_comp_i1 : idElimBase.toI a â‰« idElimBase.i1 = M.var _ := by simp [toI]

lemma toI_comp_i2 : idElimBase.toI a â‰« idElimBase.i2 = ym(M.disp _) â‰« idElimBase.toK a :=
  by simp [toI]

lemma toI_comp_left {Î”} (Ïƒ : Î” âŸ¶ Î“) : toI idElimBase (ym(Ïƒ) â‰« a) =
    ym(idElimBase.motiveSubst Ïƒ a) â‰« toI idElimBase a := by
  dsimp [toI]
  apply idElimBase.i_isPullback.hom_ext
  Â· simp [motiveSubst]
  Â· simp [toK_comp_left, motiveSubst, substWk, substCons]
    rfl

theorem motiveCtx_isPullback :
    IsPullback (toI idElimBase a) ym(M.disp _) idElimBase.i2 (toK idElimBase a) :=
  IsPullback.of_right' (M.disp_pullback _) idElimBase.i_isPullback

theorem motiveCtx_isPullback' :
    IsPullback (toI idElimBase a) (ym(M.disp (idElimBase.mkId (ym(M.disp (a â‰« M.tp)) â‰« a)
      (M.var (a â‰« M.tp)) (by simp))) â‰« ym(M.disp (a â‰« M.tp))) (iUvPoly idElimBase).p a :=
  IsPullback.paste_vert (idElimBase.motiveCtx_isPullback a)
    (idElimBase.ext_a_tp_isPullback a)

def equivMk (x : y(idElimBase.motiveCtx a) âŸ¶ X) : y(Î“) âŸ¶ idElimBase.iFunctor.obj X :=
  UvPoly.Equiv.mk' idElimBase.iUvPoly X a (idElimBase.motiveCtx_isPullback' a).flip x

def equivFst (pair : y(Î“) âŸ¶ idElimBase.iFunctor.obj X) :
    y(Î“) âŸ¶ M.Tm :=
  UvPoly.Equiv.fst idElimBase.iUvPoly X pair

lemma equivFst_comp_left (pair : y(Î“) âŸ¶ idElimBase.iFunctor.obj X)
    {Î”} (Ïƒ : Î” âŸ¶ Î“) :
    idElimBase.equivFst (ym(Ïƒ) â‰« pair) = ym(Ïƒ) â‰« idElimBase.equivFst pair := by
  dsimp [equivFst]
  rw [UvPoly.Equiv.fst_comp_left]

def equivSnd (pair : y(Î“) âŸ¶ idElimBase.iFunctor.obj X) :
    y(idElimBase.motiveCtx (equivFst idElimBase pair)) âŸ¶ X :=
  UvPoly.Equiv.snd' idElimBase.iUvPoly X pair (idElimBase.motiveCtx_isPullback' _).flip

lemma equivSnd_comp_left (pair : y(Î“) âŸ¶ idElimBase.iFunctor.obj X)
    {Î”} (Ïƒ : Î” âŸ¶ Î“) :
    idElimBase.equivSnd (ym(Ïƒ) â‰« pair) =
    ym(idElimBase.motiveSubst Ïƒ _) â‰« idElimBase.equivSnd pair := by
  dsimp only [equivSnd]
  let a := idElimBase.equivFst pair
  have H : IsPullback (idElimBase.toI a)
    (ym(M.disp (idElimBase.mkId (ym(M.disp (a â‰« M.tp)) â‰« a) (M.var (a â‰« M.tp)) _)) â‰«
    ym(M.disp (a â‰« M.tp))) idElimBase.iUvPoly.p
    (UvPoly.Equiv.fst idElimBase.iUvPoly X pair) := (motiveCtx_isPullback' _ _)
  have H' : IsPullback (ym(M.disp
      (idElimBase.mkId (ym(M.disp (idElimBase.equivFst (ym(Ïƒ) â‰« pair) â‰« M.tp)) â‰«
      idElimBase.equivFst (ym(Ïƒ) â‰« pair))
      (M.var (idElimBase.equivFst (ym(Ïƒ) â‰« pair) â‰« M.tp)) _)) â‰«
      ym(M.disp (idElimBase.equivFst (ym(Ïƒ) â‰« pair) â‰« M.tp)))
      (idElimBase.toI (idElimBase.equivFst (ym(Ïƒ) â‰« pair)))
      (ym(Ïƒ) â‰« UvPoly.Equiv.fst idElimBase.iUvPoly X pair)
      idElimBase.iUvPoly.p :=
    (motiveCtx_isPullback' _ _).flip
  rw [UvPoly.Equiv.snd'_comp_left (H := H.flip) (H' := H')]
  Â· congr 1
    have h : idElimBase.toI (idElimBase.equivFst (ym(Ïƒ) â‰« pair)) =
        ym(idElimBase.motiveSubst Ïƒ (idElimBase.equivFst pair)) â‰« idElimBase.toI a :=
      idElimBase.toI_comp_left a Ïƒ
    apply (IsPullback.flip H).hom_ext
    Â· simp only [iUvPoly_p, Category.assoc, IsPullback.lift_fst]
      simp [motiveSubst, substWk, substCons, a]; rfl
    Â· apply idElimBase.i_isPullback.hom_ext
      Â· simp [IsPullback.lift_snd, h]
      Â· apply idElimBase.isKernelPair.hom_ext
        Â· simp [IsPullback.lift_snd, h]
        Â· simp only [iUvPoly_p, IsPullback.lift_snd, IdElimBase.toI_comp_i2, â† h, toI_comp_i2]

lemma equivFst_verticalNatTrans_app {Î“ : Ctx} {X : Psh Ctx}
    (pair : y(Î“) âŸ¶ idElimBase.iFunctor.obj X) :
    idElimBase.equivFst pair = UvPoly.Equiv.fst (UvPoly.id M.Tm) X
    (pair â‰« idElimBase.verticalNatTrans.app X) := by
  dsimp [equivFst, verticalNatTrans]
  rw [â† UvPoly.fst_verticalNatTrans_app]

lemma equivSnd_verticalNatTrans_app {Î“ : Ctx} {X : Psh Ctx}
    (pair : y(Î“) âŸ¶ idElimBase.iFunctor.obj X) :
    UvPoly.Equiv.snd' (UvPoly.id M.Tm) X (pair â‰« idElimBase.verticalNatTrans.app X)
      (R := y(Î“)) (f := ğŸ™ _) (g := idElimBase.equivFst pair) (by
        convert reflCase_aux (idElimBase.equivFst pair)
        rw [equivFst_verticalNatTrans_app]) =
      ym(idElimBase.reflSubst (idElimBase.equivFst pair)) â‰«
      idElimBase.equivSnd pair :=
  calc _
  _ = _ â‰« idElimBase.equivSnd pair := by
    dsimp [equivSnd, verticalNatTrans]
    rw [UvPoly.snd'_verticalNatTrans_app (UvPoly.id M.Tm) idElimBase.iUvPoly
      (idElimBase.comparison) _ _ pair _]
    apply reflCase_aux (idElimBase.equivFst pair)
  _ = _ := by
    congr 1
    apply (M.disp_pullback _).hom_ext
    Â· conv => lhs; rw [â† toI_comp_i1]
      simp [reflSubst, comparison, mkRefl]
    Â· apply (M.disp_pullback _).hom_ext
      Â· slice_lhs 3 4 => rw [â† idElimBase.toK_comp_k1]
        slice_lhs 2 3 => rw [â† idElimBase.toI_comp_i2]
        simp [reflSubst]
      Â· simp [reflSubst]

lemma equivMk_comp_verticalNatTrans_app {Î“ : Ctx} {X : Psh Ctx} (a : y(Î“) âŸ¶ M.Tm)
    (x : y(idElimBase.motiveCtx a) âŸ¶ X) :
    idElimBase.equivMk a x â‰« (idElimBase.verticalNatTrans).app X =
    UvPoly.Equiv.mk' (UvPoly.id M.Tm) X a (R := y(Î“)) (f := ğŸ™ _) (g := a)
    (reflCase_aux a) (ym(idElimBase.reflSubst a) â‰« x) := by
  dsimp only [equivMk, verticalNatTrans]
  rw [UvPoly.mk'_comp_verticalNatTrans_app (R' := y(Î“)) (f' := ğŸ™ _) (g' := a)
    (H' := reflCase_aux a)]
  congr 2
  apply (M.disp_pullback _).hom_ext
  Â· conv => lhs; rw [â† toI_comp_i1]
    simp [reflSubst, comparison, mkRefl]
  Â· apply (M.disp_pullback _).hom_ext
    Â· slice_lhs 3 4 => rw [â† idElimBase.toK_comp_k1]
      slice_lhs 2 3 => rw [â† idElimBase.toI_comp_i2]
      simp [reflSubst]
    Â· simp [reflSubst]

end

end Equiv

end IdElimBase

/-- In the high-tech formulation by Richard Garner and Steve Awodey:
The full structure interpreting the natural model semantics for identity types
requires an `IdIntro`,
(and `IdElimBase` which can be generated by pullback in the presheaf category,)
and that the following commutative square generated by
`IdBaseComparison.verticalNatTrans` is a weak pullback.

```
  verticalNatTrans.app Tm
iFunctor Tm --------> P_ğŸ™Tm Tm
  |                    |
  |                    |
iFunctor tp           P_ğŸ™Tm tp
  |                    |
  |                    |
  V                    V
iFunctor Ty --------> P_ğŸ™Tm Ty
  verticalNatTrans.app Ty
```

This can be thought of as saying the following.
Fix `A : Ty` and `a : A` - we are working in the slice over `M.Tm`.
For any context `Î“`, any map `(a, r) : Î“ â†’ P_ğŸ™Tm Tm`
and `(a, C) : Î“ âŸ¶ iFunctor Ty` such that `r â‰« M.tp = C[x/y, refl_x/p]`,
there is a map `(a,c) : Î“ âŸ¶ iFunctor Tm` such that `c â‰« M.tp = C` and `c[a/y, refl_a/p] = r`.
Here we are thinking
  `Î“ (y : A) (p : A) âŠ¢ C : Ty`
  `Î“ âŠ¢ r : C[a/y, refl_a/p]`
  `Î“ (y : A) (p : A) âŠ¢ c : Ty`
This witnesses the elimination principle for identity types since
we can take `J (y.p.C;x.r) := c`.
-/
structure Id' (N : Universe Ctx) extends IdElimBase M where
  weakPullback : WeakPullback
    (toIdElimBase.verticalNatTrans.app N.Tm)
    (toIdElimBase.iFunctor.map N.tp)
    ((UvPoly.id M.Tm).functor.map N.tp)
    (toIdElimBase.verticalNatTrans.app N.Ty)

namespace Id'

variable {M} {N : Universe Ctx} (i : Id' M N)

variable {Î“ Î” : Ctx} (Ïƒ : Î” âŸ¶ Î“) (a : y(Î“) âŸ¶ M.Tm)
  (C : y(i.motiveCtx a) âŸ¶ N.Ty) (r : y(Î“) âŸ¶ N.Tm)
  (r_tp : r â‰« N.tp = ym(i.reflSubst a) â‰« C)

open IdElimBase IdIntro

lemma reflCase_aux : IsPullback (ğŸ™ y(Î“)) a a (UvPoly.id M.Tm).p :=
  have : IsIso (UvPoly.id M.Tm).p := by simp; infer_instance
  IsPullback.of_horiz_isIso (by simp)

/-- The variable `r` witnesses the motive for the case `refl`,
This gives a map `(a,r) : Î“ âŸ¶ P_ğŸ™Tm Tm â‰… Tm Ã— Tm` where
```
    fst â‰« r
Tm <--   Î“  --------> Tm
  <      â€–            â€–
   \     â€–   (pb)     â€– ğŸ™_Tm
  r \    â€–            â€–
     \   â€–            â€–
      \  Î“  --------> Tm
              a
```
-/
def reflCase : y(Î“) âŸ¶ (UvPoly.id M.Tm).functor.obj N.Tm :=
  UvPoly.Equiv.mk' (UvPoly.id M.Tm) N.Tm a (R := y(Î“)) (f := ğŸ™ _) (g := a)
  (reflCase_aux a) r
-- TODO: consider generalizing
-- TODO: consider showing UvPoly on identity `(P_ğŸ™_Y X)` is isomorphic to product `Y Ã— X`

/-- The variable `C` is the motive for elimination,
This gives a map `(a, C) : Î“ âŸ¶ iFunctor Ty`
```
    C
Ty <-- y(motiveCtx) ----> i
             |            |
             |            | i2 â‰« k2
             |            |
             V            V
             Î“  --------> Tm
                  a
```
-/
abbrev motive : y(Î“) âŸ¶ i.iFunctor.obj N.Ty :=
  i.equivMk a C

lemma motive_comp_left : ym(Ïƒ) â‰« i.motive a C =
    i.motive (ym(Ïƒ) â‰« a) (ym(i.motiveSubst Ïƒ a) â‰« C) := by
  dsimp [motive, equivMk]
  rw [UvPoly.Equiv.mk'_comp_left (iUvPoly i.toIdElimBase) _ a
    (i.motiveCtx_isPullback' a).flip C ym(Ïƒ) _ rfl (i.motiveCtx_isPullback' _).flip]
  congr 2
  simp only [Functor.map_comp, iUvPoly_p, Category.assoc, motiveSubst, substWk, substCons,
    Functor.FullyFaithful.map_preimage]
  apply (M.disp_pullback _).hom_ext <;> simp only [IsPullback.lift_fst, IsPullback.lift_snd]
  Â· simp [â† toI_comp_i1]
  Â· apply (M.disp_pullback _).hom_ext <;> simp
    Â· slice_lhs 3 4 => rw [â† i.toK_comp_k1]
      slice_rhs 2 3 => rw [â† i.toK_comp_k1]
      slice_lhs 2 3 => rw [â† i.toI_comp_i2]
      slice_rhs 1 2 => rw [â† i.toI_comp_i2]
      simp

def lift : y(Î“) âŸ¶ i.iFunctor.obj N.Tm :=
  i.weakPullback.coherentLift (reflCase a r) (motive i a C) (by
    dsimp only [motive, equivMk, verticalNatTrans, reflCase]
    rw [UvPoly.mk'_comp_verticalNatTrans_app (UvPoly.id M.Tm) i.iUvPoly i.comparison
      _ N.Ty a (i.motiveCtx_isPullback' a).flip C (reflCase_aux a),
      UvPoly.Equiv.mk'_comp_right, r_tp, reflSubst]
    congr
    apply (M.disp_pullback _).hom_ext
    Â· conv => right; rw [â† toI_comp_i1]
      simp [mkRefl, comparison]
    Â· apply (M.disp_pullback _).hom_ext
      Â· slice_rhs 3 4 => rw [â† i.toK_comp_k1]
        slice_rhs 2 3 => rw [â† toI_comp_i2]
        simp
      Â· simp)

lemma lift_comp_left {Î”} (Ïƒ : Î” âŸ¶ Î“) : i.lift (ym(Ïƒ) â‰« a) (ym(i.motiveSubst Ïƒ a) â‰« C)
    (ym(Ïƒ) â‰« r) (by simp [r_tp, comp_reflSubst'_assoc]) =
    ym(Ïƒ) â‰« i.lift a C r r_tp := by
  dsimp [lift]
  rw [WeakPullback.coherentLift_comp_left]
  congr 1
  Â· dsimp [reflCase]
    rw [UvPoly.Equiv.mk'_comp_left (UvPoly.id M.Tm) N.Tm a (reflCase_aux a) r ym(Ïƒ) _ rfl
      (reflCase_aux (ym(Ïƒ) â‰« a))]
    congr 2
    apply (reflCase_aux a).hom_ext
    Â· simp only [IsPullback.lift_fst]
      simp
    Â· simp
  Â· rw [motive_comp_left]

lemma equivFst_lift_eq : i.equivFst (i.lift a C r r_tp) = a :=
  calc i.equivFst (i.lift a C r r_tp)
  _ = i.equivFst (i.lift a C r r_tp â‰« i.iFunctor.map N.tp) := by
    dsimp [IdElimBase.equivFst]
    rw [UvPoly.Equiv.fst_comp_right]
  _ = _ := by
    dsimp [lift, motive, IdElimBase.equivFst, IdElimBase.equivMk]
    rw [WeakPullback.coherentLift_snd, UvPoly.Equiv.fst_mk']

/-- The elimination rule for identity types.
  `Î“ âŠ¢ A` is the type with a term `Î“ âŠ¢ a : A`.
  `Î“ (y : A) (h : Id(A,a,y)) âŠ¢ C` is the motive for the elimination.
  Then we obtain a section of the motive
  `Î“ (y : A) (h : Id(A,a,y)) âŠ¢ mkJ : A`
-/
def j : y(i.motiveCtx a) âŸ¶ N.Tm :=
  eqToHom (by rw [equivFst_lift_eq]) â‰« i.equivSnd (i.lift a C r r_tp)

/-- Typing for elimination rule `J` -/
lemma j_tp : j i a C r r_tp â‰« N.tp = C := by
  simp only [j, Category.assoc, IdElimBase.equivSnd, â† UvPoly.Equiv.snd'_comp_right]
  rw! [WeakPullback.coherentLift_snd]
  simp only [IdElimBase.equivMk]
  rw! [equivFst_lift_eq]
  simp

lemma comp_j : ym(i.motiveSubst Ïƒ _) â‰« j i a C r r_tp =
    j i (ym(Ïƒ) â‰« a) (ym(i.motiveSubst Ïƒ _) â‰« C) (ym(Ïƒ) â‰« r) (by
      simp [r_tp, IdIntro.comp_reflSubst'_assoc]) := by
  simp only [j]
  conv => rhs; rw! [i.lift_comp_left a C r r_tp]
  rw [i.equivSnd_comp_left]
  simp only [â† Category.assoc]
  congr 1
  simp [â† heq_eq_eq]
  rw [equivFst_lift_eq]

/-- Î² rule for identity types. Substituting `J` with `refl` gives the user-supplied value `r` -/
lemma reflSubst_j : ym(i.reflSubst a) â‰« j i a C r r_tp = r := by
  have h := i.equivSnd_verticalNatTrans_app (i.lift a C r r_tp)
  rw! [i.weakPullback.coherentLift_fst] at h
  unfold reflCase at h
  rw [UvPoly.Equiv.snd'_eq_snd', UvPoly.Equiv.snd'_mk', â† Iso.eq_inv_comp] at h
  conv => right; rw [h]
  simp only [j, â† Category.assoc, UvPoly.Equiv.fst_mk', UvPoly.id_p]
  congr 1
  have pb : IsPullback (ğŸ™ _) a a (ğŸ™ _) := IsPullback.of_id_fst
  have : (IsPullback.isoIsPullback y(Î“) M.Tm pb pb).inv = ğŸ™ _ := by
    apply pb.hom_ext
    Â· simp only [IsPullback.isoIsPullback_inv_fst]
      simp
    Â· simp
  simp only [â† heq_eq_eq, comp_eqToHom_heq_iff]
  rw! [equivFst_lift_eq]
  simp [this]

variable (b : y(Î“) âŸ¶ M.Tm) (b_tp : b â‰« M.tp = a â‰« M.tp)
  (h : y(Î“) âŸ¶ M.Tm) (h_tp : h â‰« M.tp = i.isKernelPair.lift b a (by aesop) â‰« i.Id)

def endPtSubst : Î“ âŸ¶ i.motiveCtx a :=
  M.substCons (M.substCons (ğŸ™ _) _ b (by aesop)) _ h (by
    simp only [h_tp, IdIntro.mkId, â† Category.assoc]
    congr 1
    apply i.isKernelPair.hom_ext
    Â· simp
    Â· simp)

/-- `Id'` is equivalent to `Id` (one half). -/
def toId : M.Id N i.toIdIntro where
  j := i.j
  j_tp := i.j_tp
  comp_j := i.comp_j
  reflSubst_j := i.reflSubst_j
-- TODO: prove the other half of the equivalence.
-- Generalize this version so that the universe for elimination is not also `M`

end Id'

namespace Id

variable {M} (base : M.IdElimBase) {N : Universe Ctx}
  (i : M.Id N base.toIdIntro)

open IdIntro IdElimBase

variable {Î“} (ar : y(Î“) âŸ¶ (UvPoly.id M.Tm).functor.obj N.Tm)
  (aC : y(Î“) âŸ¶ (IdElimBase.iFunctor base).obj N.Ty)
  (hrC : ar â‰« (UvPoly.id M.Tm).functor.map N.tp =
    aC â‰« (IdElimBase.verticalNatTrans base).app N.Ty)

include hrC in
lemma fst_eq_fst : UvPoly.Equiv.fst _ _ ar = base.equivFst aC :=
  calc _
  _ = UvPoly.Equiv.fst _ _ (ar â‰« (UvPoly.id M.Tm).functor.map N.tp) := by
    rw [UvPoly.Equiv.fst_comp_right]
  _ = UvPoly.Equiv.fst _ _  (aC â‰« (IdElimBase.verticalNatTrans base).app N.Ty) := by
    rw [hrC]
  _ = _ := by
    rw [base.equivFst_verticalNatTrans_app]

abbrev motive : y(base.motiveCtx (base.equivFst aC)) âŸ¶ N.Ty :=
  base.equivSnd aC

lemma comp_motive {Î”} (Ïƒ : Î” âŸ¶ Î“) : motive base (ym(Ïƒ) â‰« aC) =
    ym(base.motiveSubst Ïƒ (base.equivFst aC)) â‰« motive base aC := by
  simp only [motive, equivSnd_comp_left base aC Ïƒ]

abbrev reflCase : y(Î“) âŸ¶ N.Tm := UvPoly.Equiv.snd' _ _ ar (Id'.reflCase_aux _)

lemma comp_reflCase {Î”} (Ïƒ : Î” âŸ¶ Î“) : reflCase (ym(Ïƒ) â‰« ar) = ym(Ïƒ) â‰« reflCase ar := by
  simp only [reflCase]
  rw [UvPoly.Equiv.snd'_comp_left (UvPoly.id M.Tm) N.Tm ar
    (Id'.reflCase_aux (UvPoly.Equiv.fst (UvPoly.id M.Tm) N.Tm ar)) ym(Ïƒ)
    (Id'.reflCase_aux _)]
  congr 1
  apply (Id'.reflCase_aux (UvPoly.Equiv.fst (UvPoly.id M.Tm) N.Tm ar)).hom_ext
  Â· simp only [IsPullback.lift_fst]
    simp
  Â· simp

include hrC in
lemma reflCase_comp_tp : reflCase ar â‰« N.tp =
    ym(base.reflSubst (base.equivFst aC)) â‰« motive base aC := by
  dsimp [reflCase, motive]
  rw! [â† UvPoly.Equiv.snd'_comp_right, hrC]
  have H : IsPullback ym(M.disp (base.mkId
      (ym(M.disp (base.equivFst aC â‰« M.tp)) â‰« base.equivFst aC)
      (M.var (base.equivFst aC â‰« M.tp)) (by simp)) â‰«
      M.disp (base.equivFst aC â‰« M.tp))
    (base.toI (base.equivFst aC)) (UvPoly.Equiv.fst base.iUvPoly N.Ty aC) base.iUvPoly.p := by
    convert (base.motiveCtx_isPullback' (base.equivFst aC)).flip
    simp
  rw! [UvPoly.snd'_verticalNatTrans_app
    (R := y(base.motiveCtx (base.equivFst aC)))
    (H := H)
    (R' := y(Î“)) (f' := ğŸ™ _) (g' := UvPoly.Equiv.fst (UvPoly.id M.Tm) N.Tm ar)
    (H' := by
    rw [fst_eq_fst base ar aC hrC]
    exact Id'.reflCase_aux _)]
  simp only [Functor.map_comp, iUvPoly_p, equivSnd]
  congr 1
  apply (M.disp_pullback _).hom_ext <;>
    simp only [reflSubst, substCons_var, substCons_disp_functor_map, substCons_var]
  Â· simp [â† base.toI_comp_i1 (base.equivFst aC), fst_eq_fst base ar aC hrC, mkRefl]
  Â· apply (M.disp_pullback _).hom_ext
    Â· rw! [fst_eq_fst base ar aC hrC]
      slice_lhs 3 4 => rw [â† base.toK_comp_k1]
      slice_lhs 2 3 => rw [â† base.toI_comp_i2]
      simp
    Â· simp

def lift : y(Î“) âŸ¶ (IdElimBase.iFunctor base).obj N.Tm :=
  base.equivMk (base.equivFst aC) (i.j (base.equivFst aC) (motive base aC)
   (reflCase ar) (reflCase_comp_tp base ar aC hrC))

lemma lift_fst : lift base i ar aC hrC â‰« base.verticalNatTrans.app N.Tm = ar := by
  dsimp only [lift]
  rw [equivMk_comp_verticalNatTrans_app]
  apply UvPoly.Equiv.ext' (UvPoly.id M.Tm) N.Tm (by convert reflCase_aux (base.equivFst aC); simp)
  Â· rw! [i.reflSubst_j]
    simp [reflCase, fst_eq_fst base ar aC hrC]
  Â· simp [fst_eq_fst base ar aC hrC]

lemma lift_snd : lift base i ar aC hrC â‰« base.iFunctor.map N.tp = aC := by
  dsimp only [lift, equivMk]
  rw [UvPoly.Equiv.mk'_comp_right]
  apply UvPoly.Equiv.ext' base.iUvPoly N.Ty
  Â· rw! [i.j_tp]
    rw [UvPoly.Equiv.snd'_mk']
    simp [motive, equivSnd]
  Â· simp only [UvPoly.Equiv.fst_mk', iUvPoly_p]
    exact (base.motiveCtx_isPullback' _).flip
  Â· simp [equivFst]

lemma comp_lift {Î”} (Ïƒ : Î” âŸ¶ Î“) : ym(Ïƒ) â‰« lift base i ar aC hrC =
    lift base i (ym(Ïƒ) â‰« ar) (ym(Ïƒ) â‰« aC) (by simp [hrC]) := by
  dsimp [lift, equivMk]
  rw [UvPoly.Equiv.mk'_comp_left base.iUvPoly N.Tm (base.equivFst aC) _
    (i.j (base.equivFst aC) (motive base aC) (reflCase ar) _) ym(Ïƒ) _ rfl
    (by simp only [iUvPoly_p]; exact (base.motiveCtx_isPullback' _).flip)]
  congr 1
  have h := i.comp_j Ïƒ (base.equivFst aC) _ _ (reflCase_comp_tp base ar aC hrC)
  rw! (castMode := .all) [â† comp_motive, â† comp_reflCase, â† equivFst_comp_left] at h
  rw [â† h]
  congr 1
  simp only [iUvPoly_p, Category.assoc]
  apply (M.disp_pullback _).hom_ext
  Â· simp [toI_comp_left, â† toI_comp_i1]
  Â· apply (M.disp_pullback _).hom_ext
    Â· slice_rhs 3 4 => rw [â† toK_comp_k1 base]
      slice_rhs 2 3 => rw [â† toI_comp_i2]
      slice_lhs 3 4 => rw [â† toK_comp_k1 base]
      slice_lhs 2 3 => rw [â† toI_comp_i2]
      simp [toI_comp_left]
    Â· simp [motiveSubst, substWk]

def toId' : M.Id' N where
  __ := base
  weakPullback := RepPullbackCone.WeakPullback.mk
    ((IdElimBase.verticalNatTrans base).naturality _).symm
    (fun s => lift base i s.fst s.snd s.condition)
    (fun s => lift_fst base i s.fst s.snd s.condition)
    (fun s => lift_snd base i s.fst s.snd s.condition)
    (fun s _ Ïƒ => comp_lift base i s.fst s.snd s.condition Ïƒ)

end Id

end Universe

end NaturalModel
