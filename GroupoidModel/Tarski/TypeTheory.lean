import GroupoidModel.Tarski.NaturalModel
import GroupoidModel.ForMathlib

set_option autoImplicit true

section RawSyntax

mutual
inductive TyExpr where
  | univ
  | el (A : Expr)
  | pi (A B : TyExpr)
  deriving Repr

inductive Expr where
  /-- A de Bruijn index. -/
  | bvar (n : Nat)
  | app (B : TyExpr) (f a : Expr)
  | lam (ty : TyExpr) (val : Expr)
  | pi (A B : Expr)
  deriving Repr
end

/-! In this section we compute the action of substitutions.

Write `â†‘â¿` for the `n`-fold weakening substitution `{n+i/i}`.
Write `Ïƒ:k` for `Ïƒ.vâ‚–â‚‹â‚.â€¦.vâ‚.vâ‚€`.

The substitution `â†‘â¿âºáµ:k`,
i.e., `{0/0,â€¦,k-1/k-1, k+n/k,k+1+n/k+1,â€¦}`,
arises by starting with `â†‘â¿` and traversing under `k` binders:
for example, `(Î A. B)[â†‘Â¹] = Î A[â†‘Â¹]. B[â†‘Â².vâ‚€]`.

The substitution `â†‘áµ.e[â†‘áµ]:k`,
i.e., `{0/0,â€¦,k-1/k-1, e[â†‘áµ]/k, k/k+1,k+2/k+3,â€¦}`,
arises by starting with `id.e` and traversing under `k` binders:
for example `(Î A. B)[id.e] = Î A[id.e]. B[â†‘.e[â†‘].vâ‚€]`.

The substitution `id.e` is used in `Î²`-reduction:
`(Î»a) b â† a[id.b]`. -/
section Substitutions

/-- Evaluate `â†‘â¿âºáµ:k` at a de Bruijn index `i`. -/
def liftVar (n i : Nat) (k := 0) : Nat := if i < k then i else n + i

theorem liftVar_lt (h : i < k) : liftVar n i k = i := if_pos h
theorem liftVar_le (h : k â‰¤ i) : liftVar n i k = n + i := if_neg (Nat.not_lt.2 h)

theorem liftVar_base : liftVar n i = n + i := liftVar_le (Nat.zero_le _)
@[simp] theorem liftVar_base' : liftVar n i = i + n := Nat.add_comm .. â–¸ liftVar_le (Nat.zero_le _)

@[simp] theorem liftVar_zero : liftVar n 0 (k+1) = 0 := by simp [liftVar]
@[simp] theorem liftVar_succ : liftVar n (i+1) (k+1) = liftVar n i k + 1 := by
  simp [liftVar, Nat.succ_lt_succ_iff]; split <;> simp [Nat.add_assoc]

theorem liftVar_lt_add (self : i < k) : liftVar n i j < k + n := by
  simp [liftVar]
  split <;> rename_i h
  Â· exact Nat.lt_of_lt_of_le self (Nat.le_add_right ..)
  Â· rw [Nat.add_comm]; exact Nat.add_lt_add_right self _

variable (n : Nat) in
mutual
/-- Evaluate `â†‘â¿âºáµ:k` at a type expression. -/
def TyExpr.liftN : TyExpr â†’ (k : Nat := 0) â†’ TyExpr
  | .univ, _ => .univ
  | .el A, k => .el (A.liftN k)
  | .pi ty body, k => .pi (ty.liftN k) (body.liftN (k+1))

/-- Evaluate `â†‘â¿âºáµ:k` at an expression. -/
def Expr.liftN : Expr â†’ (k : Nat := 0) â†’ Expr
  | .bvar i, k => .bvar (liftVar n i k)
  | .app B fn arg, k => .app (B.liftN (k+1)) (fn.liftN k) (arg.liftN k)
  | .lam ty body, k => .lam (ty.liftN k) (body.liftN (k+1))
  | .pi ty body, k => .pi (ty.liftN k) (body.liftN (k+1))
end

/-- Evaluate `â†‘Â¹` at a type expression. -/
abbrev TyExpr.lift := TyExpr.liftN 1
/-- Evaluate `â†‘Â¹` at an expression. -/
abbrev Expr.lift := Expr.liftN 1

/-- Evaluate `â†‘áµ.e[â†‘áµ]:k` at a de Bruijn index `i`. -/
def instVar (i : Nat) (e : Expr) (k := 0) : Expr :=
  if i < k then .bvar i else if i = k then .liftN k e else .bvar (i - 1)

mutual
/-- Evaluate `â†‘áµ.e[â†‘áµ]:k` at a type expression. -/
def TyExpr.inst : TyExpr â†’ Expr â†’ (k :_:= 0) â†’ TyExpr
  | .univ, _, _ => .univ
  | .el a, e, k => .el (a.inst e k)
  | .pi ty body, e, k => .pi (ty.inst e k) (body.inst e (k+1))

/-- Evaluate `â†‘áµ.e[â†‘áµ]:k` at an expression. -/
def Expr.inst : Expr â†’ Expr â†’ (k :_:= 0) â†’ Expr
  | .bvar i, e, k => instVar i e k
  | .app B fn arg, e, k => .app (B.inst e (k+1)) (fn.inst e k) (arg.inst e k)
  | .lam ty body, e, k => .lam (ty.inst e k) (body.inst e (k+1))
  | .pi ty body, e, k => .pi (ty.inst e k) (body.inst e (k+1))
end

end Substitutions
end RawSyntax

/-! In this section we specify typing judgments of the type theory
as `Prop`-valued relations. -/
section Typing

mutual
inductive HasType : List TyExpr â†’ Expr â†’ TyExpr â†’ Prop
  | weak {e A Î“} : HasType Î“ e A â†’ HasType (A :: Î“) e.lift A.lift
  | bvar {A Î“} : HasType (A :: Î“) (.bvar 0) A.lift
  | app {A B f a Î“} :
    IsType (A :: Î“) B â†’ -- this assumption can be removed
    HasType Î“ f (.pi A B) â†’ HasType Î“ a A â†’ HasType Î“ (.app B f a) (.inst B a)
  | lam {A B e Î“} : IsType Î“ A â†’ HasType (A :: Î“) e B â†’ HasType Î“ (.lam A e) (.pi A B)

inductive IsType : List TyExpr â†’ TyExpr â†’ Prop
  -- Note: works in any context, including ill-formed ones,
  -- so we do not have wf-ctx inversion.
  | univ {Î“} : IsType Î“ .univ
  | el {A Î“} : HasType Î“ A .univ â†’ IsType Î“ (.el A)
  | pi {A B Î“} : IsType Î“ A â†’ IsType (A :: Î“) B â†’ IsType Î“ (.pi A B)
end

end Typing

open CategoryTheory NaturalModel
open Functor Limits Opposite
noncomputable section

variable {Ctx : Type u} [SmallCategory Ctx] [HasTerminal Ctx] [M : NaturalModel Ctx]

def wU : y(Î“) âŸ¶ M.Ty := yoneda.map (terminal.from Î“) â‰« U

@[simp]
theorem comp_wU (Î” Î“ : Ctx) (Ïƒ : y(Î”) âŸ¶ y(Î“)) : Ïƒ â‰« wU = wU := by
  aesop (add norm wU)

/-- `CtxStack Î“` witnesses that the semantic context `Î“`
is built by successive context extension operations. -/
inductive CtxStack : Ctx â†’ Type u where
  | nil : CtxStack (âŠ¤_ Ctx)
  | cons {Î“} (A : y(Î“) âŸ¶ Ty) : CtxStack Î“ â†’ CtxStack (M.ext Î“ A)

variable (Ctx) in
def Context : Type u := Î£ Î“ : Ctx, CtxStack Î“

abbrev Context.ty (Î“ : Context Ctx) := y(Î“.1) âŸ¶ Ty
abbrev Context.tm (Î“ : Context Ctx) := y(Î“.1) âŸ¶ Tm

def Context.typed (Î“ : Context Ctx) (A : Î“.ty) := { x : Î“.tm // x â‰« tp = A }

def Context.typed.cast {Î“ : Context Ctx} {A B : Î“.ty} (h : A = B) (x : Î“.typed A) : Î“.typed B :=
  âŸ¨x.1, h â–¸ x.2âŸ©

@[simp]
lemma Context.typed.val_cast {Î“ : Context Ctx} {A B : Î“.ty} (h : A = B) (x : Î“.typed A) :
    (x.cast h).val = x.val := rfl

def Context.nil : Context Ctx := âŸ¨_, .nilâŸ©

@[reducible]
def Context.cons (Î“ : Context Ctx) (A : Î“.ty) : Context Ctx := âŸ¨_, .cons A Î“.2âŸ©

@[simp] theorem Context.cons_fst (Î“ : Context Ctx) (A : Î“.ty) :
    (Î“.cons A).1 = ext Î“.1 A := rfl

def Context.weak (Î“ : Context Ctx) (A : Î“.ty)
  {P : Psh Ctx} (f : y(Î“.1) âŸ¶ P) : y((cons Î“ A).1) âŸ¶ P :=
  yoneda.map (disp Î“.1 A) â‰« f

protected def CtxStack.var {Î“ : Ctx} : CtxStack Î“ â†’ â„• â†’ Part (y(Î“) âŸ¶ Tm)
  | .nil, _ => .none
  | .cons _ _, 0 => pure <| var ..
  | .cons _ S, n+1 => Context.weak âŸ¨_, SâŸ© _ <$> S.var n

protected def Context.var (Î“ : Context Ctx) (i : â„•) : Part Î“.tm := Î“.2.var i

def substCons {Î“ Î” : Ctx} (Ïƒ : y(Î“) âŸ¶ y(Î”))
    (e : y(Î“) âŸ¶ Tm) (A : y(Î”) âŸ¶ Ty) (eTy : e â‰« tp = Ïƒ â‰« A) :
    y(Î“) âŸ¶ y(ext Î” A) :=
  let i : y(ext Î” A) â‰… pullback tp A := (disp_pullback A).isoPullback
  pullback.lift e Ïƒ eTy â‰« i.inv

@[reassoc (attr := simp)] theorem substCons_var {Î“ Î” : Ctx} (Ïƒ : y(Î“) âŸ¶ y(Î”))
    (e : y(Î“) âŸ¶ Tm) (A : y(Î”) âŸ¶ Ty) (eTy : e â‰« tp = Ïƒ â‰« A) :
    substCons Ïƒ e A eTy â‰« var _ _ = e := by
  simp [substCons]

@[reassoc (attr := simp)] theorem substCons_disp {Î“ Î” : Ctx} (Ïƒ : y(Î“) âŸ¶ y(Î”))
    (e : y(Î“) âŸ¶ Tm) (A : y(Î”) âŸ¶ Ty) (eTy : e â‰« tp = Ïƒ â‰« A) :
    substCons Ïƒ e A eTy â‰« yoneda.map (disp ..) = Ïƒ := by
  simp [substCons]

theorem comp_substUnit {Î” Î“ : Ctx} (Ïƒ : y(Î”) âŸ¶ y(Î“)) (f : Î“ âŸ¶ âŠ¤_ Ctx) (f' : Î” âŸ¶ âŠ¤_ Ctx) :
    Ïƒ â‰« yoneda.map f = yoneda.map f' := by
  apply Yoneda.fullyFaithful.homEquiv.symm.injective; ext

@[reassoc]
theorem comp_substCons {Î“ Î“' Î” : Ctx} (Ï„ : y(Î“') âŸ¶ y(Î“)) (Ïƒ : y(Î“) âŸ¶ y(Î”))
    (e : y(Î“) âŸ¶ Tm) (A : y(Î”) âŸ¶ Ty) (eTy : e â‰« tp = Ïƒ â‰« A) :
    Ï„ â‰« substCons Ïƒ e A eTy = substCons (Ï„ â‰« Ïƒ) (Ï„ â‰« e) A (by simp [eTy]) := by
  simp only [substCons, â† Category.assoc]
  congr 1
  apply pullback.hom_ext <;> simp

def substFst {Î“ Î” : Ctx} {A : y(Î”) âŸ¶ Ty} (Ïƒ : y(Î“) âŸ¶ y(ext Î” A)) : y(Î“) âŸ¶ y(Î”) :=
  Ïƒ â‰« yoneda.map (disp _ _)

def substSnd {Î“ Î” : Ctx} {A : y(Î”) âŸ¶ Ty} (Ïƒ : y(Î“) âŸ¶ y(ext Î” A)) : y(Î“) âŸ¶ Tm := Ïƒ â‰« var _ _

theorem substSnd_ty {Î“ Î” : Ctx} {A : y(Î”) âŸ¶ Ty} (Ïƒ : y(Î“) âŸ¶ y(ext Î” A)) :
    substSnd Ïƒ â‰« tp = substFst Ïƒ â‰« A := by
  simp [substSnd, substFst]; rw [(disp_pullback _).w]

def weakSubst {Î” Î“ : Ctx} (Ïƒ : y(Î”) âŸ¶ y(Î“)) (A : y(Î“) âŸ¶ Ty) : y(ext Î” (Ïƒ â‰« A)) âŸ¶ y(ext Î“ A) :=
  substCons (yoneda.map (disp ..) â‰« Ïƒ) (var ..) _ (by simpa using (disp_pullback (Ïƒ â‰« A)).w)

def weakSubst' {Î” Î“ : Ctx} (Ïƒ : y(Î”) âŸ¶ y(Î“)) (A : y(Î“) âŸ¶ Ty) {A'} (e : Ïƒ â‰« A = A') :
    y(ext Î” A') âŸ¶ y(ext Î“ A) := eqToHom (e â–¸ rfl) â‰« weakSubst Ïƒ A

@[simps] def Context.typed.subst {Î“ Î” : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1)) {A : Î“.ty}
    (x : Î“.typed A) : Î”.typed (Ïƒ â‰« A) := âŸ¨Ïƒ â‰« x.1, by simp [x.2]âŸ©

def mkEl {Î“ : Context Ctx} (A : Î“.typed wU) : Î“.ty :=
  substCons (yoneda.map $ terminal.from _) A.1 _ A.2 â‰« El

theorem comp_mkEl {Î” Î“ : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1)) (A : Î“.typed wU) :
    Ïƒ â‰« mkEl A = mkEl ((A.subst Ïƒ).cast (comp_wU ..)) := by
  simp [mkEl]; rw [â† Category.assoc, comp_substCons]; congr 2; apply comp_substUnit

def mkP_equiv {Î“ : Ctx} {X : Psh Ctx} :
    (y(Î“) âŸ¶ (P tp).obj X) â‰ƒ (A : y(Î“) âŸ¶ Ty) Ã— (y(ext Î“ A) âŸ¶ X) :=
  ((uvPoly tp).equiv' y(Î“) X).trans <|
  Equiv.sigmaCongrRight fun A =>
  ((yoneda.obj X).mapIso (disp_pullback A).isoPullback.op).toEquiv

theorem mkP_equiv_naturality_left {Î” Î“ : Ctx} {X : Psh Ctx} (Ïƒ : y(Î”) âŸ¶ y(Î“))
    (f : y(Î“) âŸ¶ (P tp).obj X) :
    mkP_equiv (Ïƒ â‰« f) =
      let âŸ¨A, aâŸ© := mkP_equiv f
      âŸ¨Ïƒ â‰« A, weakSubst Ïƒ A â‰« aâŸ© := by
  sorry

def mkP {Î“ : Ctx} {X : Psh Ctx} (A : y(Î“) âŸ¶ Ty) (B : y(ext Î“ A) âŸ¶ X) :
    y(Î“) âŸ¶ (P tp).obj X := mkP_equiv.symm âŸ¨A, BâŸ©

theorem mkP_app {Î“ : Ctx} {X Y : Psh Ctx} (A : y(Î“) âŸ¶ Ty)
    (F : X âŸ¶ Y) (B : y(ext Î“ A) âŸ¶ X) :
    mkP A B â‰« (P tp).map F = mkP A (B â‰« F) := by
  sorry -- left naturality of UvPoly.equiv + left naturality of sigmaCongrRight

theorem comp_mkP {Î” Î“ : Ctx} (Ïƒ : y(Î”) âŸ¶ y(Î“)) (A : y(Î“) âŸ¶ Ty) (B : y(ext Î“ A) âŸ¶ X) :
    Ïƒ â‰« mkP A B = mkP (Ïƒ â‰« A) (weakSubst Ïƒ A â‰« B) := by
  sorry -- right naturality of UvPoly.equiv + right naturality of sigmaCongrRight

def mkPi {Î“ : Context Ctx} (A : Î“.ty) (B : (Î“.cons A).ty) : Î“.ty :=
  mkP A B â‰« NaturalModelPi.Pi

theorem comp_mkPi {Î“ Î” : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1)) (A : Î“.ty) (B : (Î“.cons A).ty) :
    Ïƒ â‰« mkPi A B = mkPi (Ïƒ â‰« A) (weakSubst Ïƒ A â‰« B) := by simp [mkPi, â† comp_mkP]

def mkLam' {Î“ : Context Ctx} (A : Î“.ty) (e : (Î“.cons A).tm) : Î“.tm :=
  mkP A e â‰« NaturalModelPi.lam

theorem comp_mkLam' {Î“ Î” : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1)) (A : Î“.ty) (B : (Î“.cons A).tm) :
    Ïƒ â‰« mkLam' A B = mkLam' (Ïƒ â‰« A) (weakSubst Ïƒ A â‰« B) := by simp [mkLam', â† comp_mkP]

def Context.subst {Î“ : Context Ctx} {X : Psh Ctx}
    (A : Î“.ty) (B : y((Î“.cons A).1) âŸ¶ X) (a : Î“.typed A) : y(Î“.1) âŸ¶ X :=
  substCons (ğŸ™ _) a.1 A (by simpa using a.2) â‰« B

theorem Context.comp_subst {Î“ Î” : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1)) {X : Psh Ctx}
    (A : Î“.ty) (B : y((Î“.cons A).1) âŸ¶ X) (a : Î“.typed A) :
    Ïƒ â‰« Î“.subst A B a = Î”.subst (Ïƒ â‰« A) (weakSubst Ïƒ A â‰« B) (a.subst Ïƒ) := by
  simp [subst, weakSubst, comp_substCons_assoc]

def mkTyped {Î“ Î” : Context Ctx} {A : Î”.ty} (Ïƒ : y(Î“.1) âŸ¶ y(ext Î”.1 A))
    {AÏƒ} (eq : substFst Ïƒ â‰« A = AÏƒ) :
    Î“.typed AÏƒ := âŸ¨substSnd _, eq â–¸ substSnd_ty _âŸ©

def mkLam {Î“ : Context Ctx} (A : Î“.ty) (B : (Î“.cons A).ty) (e : (Î“.cons A).typed B) :
    Î“.typed (mkPi A B) := by
  refine âŸ¨mkLam' A e.1, ?_âŸ©
  simp [mkLam', mkPi, NaturalModelPi.Pi_pullback.w]
  rw [â† Category.assoc, mkP_app, e.2]

theorem comp_mkLam {Î“ Î” : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1))
    (A : Î“.ty) (B : (Î“.cons A).ty) (e : (Î“.cons A).typed B) :
    Ïƒ â‰« (mkLam A B e).1 =
      (mkLam (Ïƒ â‰« A) (weakSubst Ïƒ A â‰« B) (e.subst (Î” := (Î”.cons (Ïƒ â‰« A))) (weakSubst Ïƒ A))).1 := by
  simp [mkLam, comp_mkLam']

def mkPApp'_aux {Î“ : Ctx} (A : y(Î“) âŸ¶ Ty) (B : y(ext Î“ A) âŸ¶ Ty)
    (f : y(Î“) âŸ¶ Tm) (f_tp : f â‰« tp = mkP A B â‰« NaturalModelPi.Pi) :
    (A : y(Î“) âŸ¶ Ty) Ã— (y(ext Î“ A) âŸ¶ Tm) :=
  mkP_equiv <|
    -- NOTE: can be replaced by Pi_pullback.lift (new API)
    NaturalModelPi.Pi_pullback.isLimit.lift <|
      PullbackCone.mk f (mkP A B) f_tp

theorem mkPApp'_aux_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ Ty) (B : y(ext Î“ A) âŸ¶ Ty)
    (f : y(Î“) âŸ¶ Tm) (f_tp : f â‰« tp = mkP A B â‰« NaturalModelPi.Pi) :
    let p := mkPApp'_aux A B f f_tp
    (âŸ¨p.1, p.2 â‰« tpâŸ© : (A : y(Î“) âŸ¶ Ty) Ã— (y(ext Î“ A) âŸ¶ Ty)) = âŸ¨A, BâŸ© := by
  unfold mkPApp'_aux
  set g := NaturalModelPi.Pi_pullback.isLimit.lift (PullbackCone.mk f (mkP A B) f_tp)
  intro p
  apply mkP_equiv.symm.injective
  have : g â‰« (P tp).map tp = mkP A B :=
    NaturalModelPi.Pi_pullback.isLimit.fac _ (some .right)
  show mkP p.1 (p.2 â‰« tp) = mkP A B
  rw [â† mkP_app]
  simp [mkP, p, this]

@[simp]
theorem mkPApp'_aux_fst {Î“ : Ctx} (A : y(Î“) âŸ¶ Ty) (B : y(ext Î“ A) âŸ¶ Ty)
    (f : y(Î“) âŸ¶ Tm) (f_tp : f â‰« tp = mkP A B â‰« NaturalModelPi.Pi) :
    (mkPApp'_aux A B f f_tp).fst = A :=
  congrArg Sigma.fst (mkPApp'_aux_tp ..)

@[simp]
theorem mkPApp'_aux_snd_tp {Î“ : Ctx} (A : y(Î“) âŸ¶ Ty) (B : y(ext Î“ A) âŸ¶ Ty)
    (f : y(Î“) âŸ¶ Tm) (f_tp : f â‰« tp = mkP A B â‰« NaturalModelPi.Pi) :
    (mkPApp'_aux A B f f_tp).snd â‰« tp = eqToHom (by simp) â‰« B := by
  have h := mkPApp'_aux_tp A B f f_tp
  simp only [Sigma.mk.inj_iff, mkPApp'_aux_fst, true_and] at h
  set B' := eqToHom _ â‰« B
  have : HEq B B' := by -- purely HEq nonsense
    simp only [B']
    set pf := of_eq_true _
    . clear_value pf
      have := mkPApp'_aux_fst A B f f_tp
      generalize (mkPApp'_aux A B f f_tp).fst = x at pf this
      cases this
      simp
    . simp
  exact eq_of_heq (h.trans this)

def comp_mkPApp'_aux {Î” Î“ : Ctx} (Ïƒ : y(Î”) âŸ¶ y(Î“))
    (A : y(Î“) âŸ¶ Ty) (B : y(ext Î“ A) âŸ¶ Ty)
    (f : y(Î“) âŸ¶ Tm) (f_tp : f â‰« tp = mkP A B â‰« NaturalModelPi.Pi) :
    mkPApp'_aux (Ïƒ â‰« A) (weakSubst Ïƒ A â‰« B) (Ïƒ â‰« f) (by simp [f_tp, â† comp_mkP]) =
      let âŸ¨A, aâŸ© := mkPApp'_aux A B f f_tp
      âŸ¨Ïƒ â‰« A, weakSubst Ïƒ A â‰« aâŸ© := by
  have := mkP_equiv_naturality_left Ïƒ
    (NaturalModelPi.Pi_pullback.isLimit.lift (PullbackCone.mk f (mkP A B) f_tp))
  dsimp at this -- bad; `mkP_equiv_naturality_left` is not about dsimpNFs
  simp only [mkPApp'_aux, â† this, PullbackCone.IsLimit.comp_lift]
  congr 3
  simp [comp_mkP]

def mkPApp {Î“ : Context Ctx} (A : Î“.ty) (B : (Î“.cons A).ty)
    (f : Î“.typed (mkPi A B)) : (Î“.cons A).typed B :=
  âŸ¨eqToHom (by simp) â‰« (mkPApp'_aux A B f.1 f.2).2, by simpâŸ©

theorem comp_mkPApp {Î“ Î” : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1)) (A : Î“.ty) (B : (Î“.cons A).ty)
    (f : Î“.typed (mkPi A B)) :
    weakSubst Ïƒ A â‰« (mkPApp A B f).1 =
    (mkPApp (Ïƒ â‰« A) (weakSubst Ïƒ A â‰« B) ((f.subst Ïƒ).cast (comp_mkPi ..))).1 := by
  simp only [mkPApp, Context.typed.val_cast, Context.typed.subst_coe]
  slice_lhs 1 2 => rw [eqToHom_naturality _ (mkPApp'_aux_fst ..).symm]
  simp only [Category.assoc]
  congr! 1
  . congr 2
    rw [comp_mkPApp'_aux]
  . congr! 1
  . rw [comp_mkPApp'_aux]

def mkApp {Î“ : Context Ctx} (A : Î“.ty) (B : (Î“.cons A).ty)
    (f : Î“.typed (mkPi A B)) (a : Î“.typed A) : Î“.typed (Î“.subst A B a) := by
  refine âŸ¨Î“.subst A (mkPApp A B f).1 a, ?_âŸ©
  simp [Context.subst]
  congr! 1; exact (mkPApp A B f).2

theorem comp_mkApp {Î“ Î” : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1)) (A : Î“.ty) (B : (Î“.cons A).ty)
    (f : Î“.typed (mkPi A B)) (a : Î“.typed A) :
    Ïƒ â‰« (mkApp A B f a).1 =
    (mkApp (Ïƒ â‰« A) (weakSubst Ïƒ A â‰« B) ((f.subst Ïƒ).cast (comp_mkPi ..)) (a.subst Ïƒ)).1 := by
  simp [mkApp, Context.comp_subst, comp_mkPApp]

def mkSmallPi {Î“ : Context Ctx} (A : Î“.typed wU) (B : (Î“.cons (mkEl A)).typed wU) : Î“.typed wU := by
  refine mkTyped (Î” := .nil) (?a â‰« NaturalModelSmallPi.SmallPi (Ctx := Ctx)) ?b
  case b => simp only [Context.nil, Psh, wU, substFst]; rw [comp_substUnit]
  refine ((uvPoly (Ctx := Ctx) _).equiv' _ _).2 âŸ¨?_, ?_âŸ©
  Â· exact substCons (yoneda.map $ terminal.from _) A.1 _ A.2
  Â· refine ?_ â‰« substCons (yoneda.map $ terminal.from _) B.1 _ B.2
    dsimp [uvPoly]
    refine (disp_pullback (Ctx := Ctx) _).isLimit.lift <|
      PullbackCone.mk (pullback.fst .. â‰« var _ _) (pullback.snd ..) ?_
    rw [mkEl, Category.assoc, (disp_pullback _).w, â† Category.assoc,
      pullback.condition, Category.assoc]

theorem comp_mkSmallPi {Î“ Î” : Context Ctx} (Ïƒ : y(Î”.1) âŸ¶ y(Î“.1))
    (A : Î“.typed wU) (B : (Î“.cons (mkEl A)).typed wU) :
    Ïƒ â‰« (mkSmallPi A B).1 = (mkSmallPi ((A.subst Ïƒ).cast (comp_wU ..))
      ((B.subst (weakSubst' Ïƒ (mkEl A) (comp_mkEl ..))).cast (comp_wU ..))).1 := by
  sorry

mutual

def ofType (Î“ : Context Ctx) : TyExpr â†’ Part Î“.ty
  | .univ => pure wU
  | .pi A B => do
    let A â† ofType Î“ A
    let B â† ofType (Î“.cons A) B
    pure (mkPi A B)
  | .el e => do
    let v â† ofTerm Î“ e
    Part.assert _ fun h => pure <| mkEl âŸ¨v, hâŸ©

def ofTerm (Î“ : Context Ctx) : Expr â†’ Part Î“.tm
  | .bvar i => Context.var _ i
  -- | .univ => .none
  | .pi A B => do
    let A â† ofTerm Î“ A
    Part.assert (A â‰« tp = wU) fun hA => do
    let B â† ofTerm (Î“.cons (mkEl âŸ¨A, hAâŸ©)) B
    Part.assert (B â‰« tp = wU) fun hB => do
    pure (mkSmallPi âŸ¨A, hAâŸ© âŸ¨B, hBâŸ©).1
  | .lam A e => do
    let A â† ofType Î“ A
    let e â† ofTerm (Î“.cons A) e
    pure (mkLam A _ âŸ¨e, rflâŸ©).1
  | .app B f a => do
    let f â† ofTerm Î“ f
    let a â† ofTerm Î“ a
    let B â† ofType (Î“.cons (a â‰« tp)) B
    Part.assert (f â‰« tp = mkPi (a â‰« tp) B) fun h =>
    pure (mkApp _ B âŸ¨f, hâŸ© âŸ¨a, rflâŸ©).1

end

def ofCtx : List TyExpr â†’ Part (Context Ctx)
  | [] => pure .nil
  | A :: Î“ => do let Î“ â† ofCtx Î“; Î“.cons (â† ofType Î“ A)

theorem ofTerm_app (Î“ : Context Ctx) {f a e'} :
    e' âˆˆ ofTerm Î“ (.app B f a) â†”
      âˆƒ f' âˆˆ ofTerm Î“ f, âˆƒ a' âˆˆ ofTerm Î“ a, âˆƒ t', âˆƒ ht' : a' â‰« tp = t',
      âˆƒ B' âˆˆ ofType (Î“.cons t') B, âˆƒ hB : f' â‰« tp = mkPi t' B',
      e' = (mkApp _ B' âŸ¨f', hBâŸ© âŸ¨a', ht'âŸ©).1 := by
  simp only [ofTerm, Part.pure_eq_some, Part.bind_eq_bind, Part.mem_bind_iff, Part.mem_assert_iff,
    Part.mem_some_iff]
  exact âŸ¨
    fun âŸ¨_, hf, _, ha, _, hB, H, eâŸ© => âŸ¨_, hf, _, ha, _, rfl, _, hB, H, eâŸ©,
    fun âŸ¨_, hf, _, ha, _, rfl, _, hB, H, eâŸ© => âŸ¨_, hf, _, ha, _, hB, H, eâŸ©âŸ©

@[simp] def CtxStack.size {Î“ : Ctx} : CtxStack Î“ â†’ Nat
  | .nil => 0
  | .cons _ S => S.size + 1

/-- Given the context stack `1.Aâ‚™.â€¦.Aâ‚–â‚‹â‚.â€¦.Aâ‚€`, return `1.Aâ‚™.â€¦.Aâ‚–`. -/
@[simp] def CtxStack.dropN {Î“ : Ctx} : âˆ€ k (S : CtxStack Î“), k â‰¤ S.size â†’ Context Ctx
  | 0, S, _ => âŸ¨Î“, SâŸ©
  | _+1, .cons .., h => CtxStack.dropN _ _ (Nat.le_of_succ_le_succ h)

/-- Given the context stack `1.Aâ‚™.â€¦.Aâ‚–â‚‹â‚.â€¦.Aâ‚€`,
return the display map `1.Aâ‚™.â€¦.Aâ‚–â‚‹â‚.â€¦.Aâ‚€ âŸ¶ 1.Aâ‚™.â€¦.Aâ‚–`. -/
@[simp] def CtxStack.dropN_disp {Î“ : Ctx} :
    âˆ€ (k : Nat) (S : CtxStack Î“) (h : k â‰¤ S.size), Î“ âŸ¶ (S.dropN k h).1
  | 0, _, _ => ğŸ™ _
  | _+1, .cons .., h => disp .. â‰« CtxStack.dropN_disp _ _ (Nat.le_of_succ_le_succ h)

/-- Given the context stack `1.Aâ‚™.â€¦.Aâ‚–â‚‹â‚.â€¦.Aâ‚€` and a type `X : 1.Aâ‚™.â€¦.Aâ‚– âŸ¶ Ty`,
return the map `1.Aâ‚™.â€¦.Aâ‚–.B.Aâ‚–â‚‹â‚[â†‘].â€¦.Aâ‚€[â‹¯] âŸ¶ 1.Aâ‚™.â€¦.Aâ‚–â‚‹â‚.â€¦.Aâ‚€`. -/
@[simp] def CtxStack.extN {Î“ : Ctx} : âˆ€ {k : Nat} {S : CtxStack Î“} {h : k â‰¤ S.size},
    (S.dropN k h).ty â†’ Î£ Î” : Context Ctx, y(Î”.1) âŸ¶ y(Î“)
  | 0, _, _, X => âŸ¨.cons _ X, yoneda.map (disp ..)âŸ©
  | k+1, .cons A _, h, X =>
    let âŸ¨Î”, wkâŸ© := CtxStack.extN (k := k) (h := Nat.le_of_succ_le_succ h) X
    âŸ¨.cons Î” (wk â‰« A), weakSubst ..âŸ©

def Context.tyN (Î“ : Context Ctx) (k : Nat) : Type u := Î£' h : k â‰¤ Î“.2.size, (Î“.2.dropN k h).ty

def Context.tyN.up {Î“ : Context Ctx} {k : Nat} {A} : Î“.tyN k â†’ (Î“.cons A).tyN (k+1)
  | âŸ¨h, XâŸ© => âŸ¨Nat.succ_le_succ h, XâŸ©

def Context.tyN.down {Î“ : Context Ctx} {k : Nat} {A} : (Î“.cons A).tyN (k+1) â†’ Î“.tyN k
  | âŸ¨h, XâŸ© => âŸ¨Nat.le_of_succ_le_succ h, XâŸ©

def Context.consN (Î“ : Context Ctx) (A : Î“.tyN k) : Context Ctx := (Î“.2.extN A.2).1

def Context.dispN (Î“ : Context Ctx) (A : Î“.tyN k) : y((consN Î“ A).1) âŸ¶ y(Î“.1) := (Î“.2.extN A.2).2

@[simp] theorem Context.dispN_cons (Î“ : Context Ctx) (A : Î“.ty) (X : Î“.tyN k) :
    (Î“.cons A).dispN X.up = weakSubst (Î“.dispN X) A := rfl

@[simp] theorem Context.consN_cons (Î“ : Context Ctx) (A : Î“.ty) (X : Î“.tyN k) :
    (Î“.cons A).consN X.up = (Î“.consN X).cons (Î“.dispN X â‰« A) := rfl

mutual

theorem ofType_liftN {k : Nat} {Î“ : Context Ctx} :
  âˆ€ {A A'} (X : Î“.tyN k), A' âˆˆ ofType Î“ A â†’ Context.dispN Î“ X â‰« A' âˆˆ ofType (Î“.consN X) (A.liftN 1 k)
  | .univ, _, _, H => by
    simp only [ofType, Part.pure_eq_some, Part.mem_some_iff] at H
    subst H; simp only [TyExpr.liftN, ofType, Part.pure_eq_some, Part.mem_some_iff]; apply comp_wU
  | .pi A B, _, X, H => by
    simp only [ofType, Part.pure_eq_some, Part.bind_eq_bind, Part.mem_bind_iff,
      Part.mem_some_iff] at H
    obtain âŸ¨A', hA, B', hB, rflâŸ© := H
    simp only [TyExpr.liftN, ofType, Part.pure_eq_some, Part.bind_eq_bind, Part.mem_bind_iff,
      Part.mem_some_iff]
    refine âŸ¨_, ofType_liftN X hA, _, ofType_liftN X.up hB, ?_âŸ©
    apply comp_mkPi
  | .el A, _, X, H => by
    simp only [ofType, Part.pure_eq_some, Part.bind_eq_bind, Part.mem_bind_iff, Part.mem_assert_iff,
      Part.mem_some_iff] at H
    obtain âŸ¨A', hA, ha, rflâŸ© := H
    simp only [TyExpr.liftN, ofType, Part.pure_eq_some, Part.bind_eq_bind, Part.mem_bind_iff,
      Part.mem_assert_iff, Part.mem_some_iff]
    refine âŸ¨_, ofTerm_liftN X hA, by simp [ha], ?_âŸ©
    simp only [mkEl, â† Category.assoc]; congr 1
    rw [comp_substCons]; congr 1; apply comp_substUnit

theorem ofTerm_liftN {k : Nat} {Î“ : Context Ctx} : âˆ€ {e e'} (X : Î“.tyN k),
    e' âˆˆ ofTerm Î“ e â†’ Context.dispN Î“ X â‰« e' âˆˆ ofTerm (Î“.consN X) (e.liftN 1 k)
  | .bvar n, e', âŸ¨hX, XâŸ©, H => by
    let âŸ¨Î“, SâŸ© := Î“
    simp only [Expr.liftN, ofTerm, Context.var, Context.consN, Context.dispN] at H âŠ¢
    dsimp [Context.tm] at X hX e'
    induction k generalizing n Î“ with
    | zero =>
      simp only [CtxStack.dropN, CtxStack.extN, Context.cons, liftVar_base', CtxStack.var,
        Part.map_eq_map, Part.mem_map_iff]
      exact âŸ¨_, H, rflâŸ©
    | succ k ih =>
      obtain _ | âŸ¨A, SâŸ© := S; Â· nomatch hX
      cases n with simp only [CtxStack.var, Part.pure_eq_some, Part.mem_some_iff, Part.map_eq_map,
        Part.mem_map_iff] at H
      | zero =>
        subst e'
        simp only [CtxStack.extN, weakSubst, substCons_var, liftVar_zero, CtxStack.var,
          Part.pure_eq_some, Part.mem_some_iff]
      | succ n =>
        obtain âŸ¨e, he, rflâŸ© := H
        simp only [CtxStack.extN, liftVar_succ, CtxStack.var, Part.map_eq_map, Part.mem_map_iff]
        refine âŸ¨_, ih n _ S e (Nat.le_of_succ_le_succ hX) X he, ?_âŸ©
        simp only [Context.weak, weakSubst, substCons_disp_assoc, Category.assoc]
  | .app B f a, _, X, H => by
    simp only [ofTerm_app, Expr.liftN] at H âŠ¢
    obtain âŸ¨f', hf, a', ha, _, rfl, B, hB, h, rflâŸ© := H
    refine âŸ¨_, ofTerm_liftN X hf, _, ofTerm_liftN X ha,
      Î“.dispN X â‰« a' â‰« tp, by simp, _, ofType_liftN X.up hB, ?_, ?_âŸ©
    Â· simp only [Category.assoc, h, comp_mkPi, Context.dispN_cons]
    Â· rw [comp_mkApp]; rfl
  | .lam t e, _, X, H => by
    simp only [ofTerm, Part.pure_eq_some, Part.bind_eq_bind, Part.mem_bind_iff,
      Part.mem_some_iff, Expr.liftN] at H âŠ¢
    obtain âŸ¨t', ht, e', he, rflâŸ© := H
    refine âŸ¨_, ofType_liftN X ht, _, ofTerm_liftN X.up he, ?_âŸ©
    simp only [comp_mkLam]; rfl
  | .pi A B, _, X, H => by
    simp only [ofTerm, Part.pure_eq_some, Part.bind_eq_bind, Part.mem_bind_iff,
      Part.mem_assert_iff, Part.mem_some_iff, Expr.liftN] at H âŠ¢
    obtain âŸ¨A', hA, HA, B', hB, HB, rflâŸ© := H
    refine âŸ¨_, ofTerm_liftN X hA, ?_, _, ?_, ?_, comp_mkSmallPi ..âŸ©
    Â· simp only [Category.assoc, HA, comp_wU]
    Â· have : âˆ€ A'' (h : Î“.dispN X â‰« mkEl âŸ¨A', HAâŸ© = A''),
        eqToHom (h â–¸ rfl) â‰« weakSubst (Î“.dispN X) (mkEl âŸ¨A', HAâŸ©) â‰« B' âˆˆ
          ofTerm ((Î“.consN X).cons A'') (Expr.liftN 1 B (k + 1)) := by
        rintro _ rfl; exact ofTerm_liftN X.up hB
      exact this _ (comp_mkEl ..)
    Â· simp only [weakSubst', Context.typed.subst_coe, Category.assoc, HB, comp_wU]

end

theorem ofType_lift {Î“ : Context Ctx} {A A'} (X : Î“.ty) (H : A' âˆˆ ofType Î“ A) :
    Context.weak Î“ X A' âˆˆ ofType (Î“.cons X) A.lift := ofType_liftN âŸ¨Nat.zero_le _, XâŸ© H

theorem ofTerm_lift {Î“ : Context Ctx} {e e'} (X : Î“.ty) (H : e' âˆˆ ofTerm Î“ e) :
    Context.weak Î“ X e' âˆˆ ofTerm (Î“.cons X) e.lift := ofTerm_liftN âŸ¨Nat.zero_le _, XâŸ© H

theorem ofType_inst {Î“ : Context Ctx} {X : Î“.ty} {x A A'} (x' : Î“.typed X)
    (Hx : x'.1 âˆˆ ofTerm Î“ x) (He : A' âˆˆ ofType (Î“.cons X) A) :
    Context.subst X A' x' âˆˆ ofType Î“ (A.inst x) := sorry

theorem ofTerm_inst {Î“ : Context Ctx} {X : Î“.ty} {x e e'} (x' : Î“.typed X)
    (Hx : x'.1 âˆˆ ofTerm Î“ x) (He : e' âˆˆ ofTerm (Î“.cons X) e) :
    Context.subst X e' x' âˆˆ ofTerm Î“ (e.inst x) := sorry

theorem ofTerm_ofType_correct :
    (âˆ€ {Î“ e A}, HasType Î“ e A â†’ âˆ€ {Î“'}, Î“' âˆˆ ofCtx (Ctx := Ctx) Î“ â†’
      âˆƒ A' âˆˆ ofType Î“' A, âˆƒ e' âˆˆ ofTerm Î“' e, e' â‰« tp = A') âˆ§
    (âˆ€ {Î“ A}, IsType Î“ A â†’ âˆ€ {Î“'}, Î“' âˆˆ ofCtx (Ctx := Ctx) Î“ â†’
      (ofType Î“' A).Dom) := by
  let ofTerm_correct Î“ e A := âˆ€ {Î“'}, Î“' âˆˆ ofCtx (Ctx := Ctx) Î“ â†’
      âˆƒ A' âˆˆ ofType Î“' A, âˆƒ e' âˆˆ ofTerm Î“' e, e' â‰« tp = A'
  let ofType_correct Î“ A := âˆ€ {Î“'}, Î“' âˆˆ ofCtx (Ctx := Ctx) Î“ â†’ (ofType Î“' A).Dom
  refine
    âŸ¨@HasType.rec (fun Î“ e A _ => ofTerm_correct Î“ e A) (fun Î“ A _ => ofType_correct Î“ A)
      ?weak ?bvar ?app ?lam ?univ ?el ?pi,
     @IsType.rec (fun Î“ e A _ => ofTerm_correct Î“ e A) (fun Î“ A _ => ofType_correct Î“ A)
      ?weak ?bvar ?app ?lam ?univ ?el ?piâŸ©
  case bvar =>
    intro A Î“ Î“' hÎ“
    simp [ofCtx] at hÎ“
    obtain âŸ¨Î“', _, A', hA, rflâŸ© := hÎ“
    refine âŸ¨_, ofType_lift A' hA, _, by rw [ofTerm]; apply Part.mem_some, ?_âŸ©
    rw [(disp_pullback A').w]; rfl
  case weak =>
    intro e A Î“ _ ihe Î“' hÎ“
    simp [ofCtx] at hÎ“
    obtain âŸ¨Î“', hÎ“, A', hA, rflâŸ© := hÎ“
    obtain âŸ¨_, hA', e', he', rflâŸ© := ihe hÎ“
    cases Part.mem_unique hA hA'
    refine âŸ¨_, ofType_lift _ hA, _, ofTerm_lift _ he', by simp [Context.weak]âŸ©
  case app =>
    intro A B f a Î“ _ _ _ ihB ihf iha Î“' hÎ“
    obtain âŸ¨_, hA, a', ha, rflâŸ© := iha hÎ“
    obtain âŸ¨B', hBâŸ© := Part.dom_iff_mem.1 <| ihB (by simpa [ofCtx] using âŸ¨_, hÎ“, _, hA, rflâŸ©)
    obtain âŸ¨_, hP, f', hf, rflâŸ© := ihf hÎ“
    have := Part.mem_unique hP (by simp [ofType]; exact âŸ¨_, hA, _, hB, rflâŸ©)
    simp [ofTerm_app]
    exact âŸ¨_, ofType_inst âŸ¨_, rflâŸ© ha hB, _, âŸ¨_, hf, _, ha, _, rfl, _, hB, this, rflâŸ©, (mkApp ..).2âŸ©
  case lam =>
    intro A B e Î“ _ _ ihA ihe Î“' hÎ“
    have âŸ¨A', hAâŸ© := Part.dom_iff_mem.1 (ihA hÎ“)
    obtain âŸ¨_, hB, e', he, rflâŸ© := ihe (by simpa [ofCtx] using âŸ¨_, hÎ“, _, hA, rflâŸ©)
    simp [ofType, ofTerm]
    exact âŸ¨_, âŸ¨_, hA, _, hB, rflâŸ©, _, âŸ¨_, hA, _, he, rflâŸ©, (mkLam ..).2âŸ©
  case el =>
    intro A Î“ _ ihA Î“' hÎ“
    simp [ofType, Part.assert]
    have := ihA hÎ“; simp [ofType] at this
    have âŸ¨_, âŸ¨h, rflâŸ©, eqâŸ© := this
    exact âŸ¨h, eqâŸ©
  case pi =>
    intro A B Î“ _ _ ihA ihB Î“' hÎ“
    simp [ofType]
    refine âŸ¨ihA hÎ“, ihB ?_âŸ©; simp [ofCtx]
    exact âŸ¨_, hÎ“, _, Part.get_mem _, rflâŸ©
  case univ => intro Î“ Î“' _; simp [ofType]

theorem ofTerm_correct {Î“ e A} (H : HasType Î“ e A) {Î“'} (hÎ“ : Î“' âˆˆ ofCtx (Ctx := Ctx) Î“) :
    âˆƒ A' âˆˆ ofType Î“' A, âˆƒ e' âˆˆ ofTerm Î“' e, e' â‰« tp = A' := ofTerm_ofType_correct.1 H hÎ“

theorem ofTerm_correct_ty {Î“ e A} (H : HasType Î“ e A) {Î“'} (hÎ“ : Î“' âˆˆ ofCtx (Ctx := Ctx) Î“) :
    (ofType Î“' A).Dom :=
  let âŸ¨_, âŸ¨h, rflâŸ©, _âŸ© := ofTerm_correct H hÎ“
  h

theorem ofTerm_correct_tm {Î“ e A} (H : HasType Î“ e A) {Î“'} (hÎ“ : Î“' âˆˆ ofCtx (Ctx := Ctx) Î“) :
    (ofTerm Î“' e).Dom :=
  let âŸ¨_, _, _, âŸ¨h, rflâŸ©, _âŸ© := ofTerm_correct H hÎ“
  h

theorem ofTerm_correct_tp {Î“ A} (H : HasType Î“ e A) {Î“'} (hÎ“ : Î“' âˆˆ ofCtx (Ctx := Ctx) Î“) :
    (ofTerm Î“' e).get (ofTerm_correct_tm H hÎ“) â‰« tp =
    (ofType Î“' A).get (ofTerm_correct_ty H hÎ“) :=
  let âŸ¨_, âŸ¨_, rflâŸ©, _, âŸ¨_, rflâŸ©, eqâŸ© := ofTerm_correct H hÎ“
  eq

theorem ofType_correct {Î“ A} (H : IsType Î“ A) {Î“'} (hÎ“ : Î“' âˆˆ ofCtx (Ctx := Ctx) Î“) :
    (ofType Î“' A).Dom := ofTerm_ofType_correct.2 H hÎ“

section Examples

def Typed (Î“ A) := { e // HasType Î“ e A }

def foo._hott : Typed [] (TyExpr.pi .univ .univ) := âŸ¨Expr.lam .univ (.bvar 0), .lam .univ .bvarâŸ©

-- example : HasType [] (Expr.lam .univ (.bvar 0)) (TyExpr.pi .univ .univ) :=
--   .lam .univ .bvar

def toModelType {A} (e : Typed [] A) : y(âŠ¤_ _) âŸ¶ M.Ty :=
  (ofType .nil A).get (ofTerm_correct_ty (Ctx := Ctx) e.2 âŸ¨trivial, rflâŸ©)

def toModel {A} (e : Typed [] A) : y(âŠ¤_ _) âŸ¶ M.Tm :=
  (ofTerm .nil e.1).get (ofTerm_correct_tm (Ctx := Ctx) e.2 âŸ¨trivial, rflâŸ©)

theorem toModel_type {A} (e : Typed [] A) : toModel e â‰« tp = toModelType (Ctx := Ctx) e :=
  ofTerm_correct_tp (Ctx := Ctx) e.2 âŸ¨trivial, rflâŸ©

example : y(âŠ¤_ _) âŸ¶ M.Tm := toModel foo._hott

end Examples
